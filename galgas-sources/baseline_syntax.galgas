syntax baseline_syntax (piccolo_lexique) {

#----------------------------------------------------*

rule <configuration_definition>
  ?!@configDefinitionList ioConfigDefinitionList

rule <ram_definition>
  ?!@ramDefinitionList ioRamDefinitionList

rule <constant_definition>
  ?!@constantDefinitionList ioConstantDefinitionList

rule <register_parsing>
  !@registerExpression outRegisterExpression

rule <optional_w_as_dest>
  !@bool out_W_isDestination

rule <bit_number_parsing>
  !@bitNumberExpression outBitNumber

rule <immediate_expression>
  !@immediatExpression outExpression

#----------------------------------------------------*

rule <section_list>
  ?!@baseline_routineDefinitionList ioRoutineDefinitionList
  ?!@lstringlist ioUnusedRoutineList
  ?!@ramDefinitionList ioRamDefinitionList
  ?!@lstringlist unusedRegisterList
  ?!@configDefinitionList ioConfigDefinitionList
  ?!@constantDefinitionList ioConstantDefinitionList {
  repeat
  while
    $unused$
    $byte$
    repeat
      $identifier$ ?let @lstring unusedRegister
      unusedRegisterList += !unusedRegister
    while 
      $,$
    end
  while
    $unused$
    $routine$
    repeat
      $identifier$ ?let @lstring unusedRoutine
      ioUnusedRoutineList += !unusedRoutine
    while 
      $,$
    end
  while
    <configuration_definition> !?ioConfigDefinitionList
  while
    <ram_definition> !?ioRamDefinitionList
  while
    <constant_definition> !?ioConstantDefinitionList
  while
    <baseline_routine_definition> !?ioRoutineDefinitionList
  end
}

#----------------------------------------------------*

rule <baseline_instruction_list>
  !@baseline_instructionList outInstructionList
  ?!@labelMap ioLabelMap
{
  outInstructionList = @baseline_instructionList ()
  repeat
  while
    <baseline_simple_instruction> ?let instruction
    outInstructionList += !instruction
  while
    <baseline_structured_instruction>
      ?let instruction
      !?ioLabelMap
    outInstructionList += !instruction
  end
}

#----------------------------------------------------*

rule <baseline_routine_definition>
  ?!@baseline_routineDefinitionList ioRoutineDefinitionList {
  let @bool noreturn
  select
    noreturn = false
  or
    $noreturn$
    noreturn = true
  end
  $routine$
  $identifier$ ?let @lstring routineName indexing routineDefinition
#--- Page
  let @luint page
  select
    page = @luint { !0 !@location.here}
  or
    $page$
    $integer$ ? page
  end
#---
  ${$
  var @labelMap labelMap = @()
  <baseline_instruction_list>
    ?let instructionList
    !?labelMap
  $}$
  ioRoutineDefinitionList +=
    !routineName
    !page
    !noreturn
    !instructionList
    !@location.here
}

#----------------------------------------------------*

rule <baseline_structured_instruction>
  !@baseline_instruction outInstruction
  ?!@labelMap ioLabelMap
{
  select
    $do$
    let @lstring doLabel
    select
      doLabel = @{!"" !.here}
    or
      $label$ ?doLabel
      [!?ioLabelMap insertKey !doLabel]
    end
    let @location instructionLocation = @location.here
    $($
    $identifier$ ?let constantName
    $:$
    <immediate_expression> ?let @immediatExpression lowBoundExpression
    $...$
    <immediate_expression> ?let @immediatExpression highBoundExpression
    $)$
    <baseline_instruction_list> ?let @baseline_instructionList instructionList !?ioLabelMap
    outInstruction = @baseline_instruction_STATIC_REPEAT {
      !instructionLocation
      !constantName
      !lowBoundExpression
      !highBoundExpression
      !instructionList
      !.here
    }
    $end$
    let @lstring endDoLabel
    select
      endDoLabel = @{!"" !.here}
    or
      $label$ ?endDoLabel
    end
    if doLabel.string != endDoLabel.string then
      error endDoLabel : "'end' label does not match 'do' label, it should be "
         + (if doLabel.string != "" then doLabel.string else "empty" end)
    end 
  or
    $jump$
    let @location instructionLocation = @location.here
    $identifier$ ?let @lstring targetLabelName
    outInstruction = @baseline_instruction_JUMP {
      !instructionLocation
      !targetLabelName}
  or
    $jsr$
    let @location instructionLocation = @location.here
    $identifier$ ?let @lstring targetLabelName
    outInstruction = @baseline_instruction_JSR {
      !instructionLocation
      !targetLabelName}
  or
    $forever$
    let @lstring foreverLabel
    select
      foreverLabel = @{!"" !.here}
    or
      $label$ ?foreverLabel
      [!?ioLabelMap insertKey !foreverLabel]
    end
    let instructionLocation = @location.here
    <baseline_instruction_list> ?let instructionList !?ioLabelMap
    $end$
    let @lstring endForeverLabel
    select
      endForeverLabel = @{!"" !.here}
    or
      $label$ ?endForeverLabel
    end
    if foreverLabel.string != endForeverLabel.string then
      error endForeverLabel : "'end' label does not match 'fovever' label, it should be "
         + (if foreverLabel.string != "" then foreverLabel.string else "empty" end)
    end 
    outInstruction = @baseline_instruction_FOREVER {
      !instructionLocation
      !instructionList
      !@location.here
    }
  or
    $if$
    let @location instructionLocation = @location.here
    let @registerExpression registerExpression
    let @bitNumberExpression bitNumberExpression
    let @bool skipIfSet
    select
      $!$
      <register_parsing> ? registerExpression
      $.$
      <bit_number_parsing> ? bitNumberExpression
      skipIfSet = true
    or
      <register_parsing> ? registerExpression
      $.$
      <bit_number_parsing> ? bitNumberExpression
      skipIfSet = false
    end
    $:$
    <baseline_simple_instruction> ?let instruction
    outInstruction = @baseline_instruction_IF_BitTest {
      !instructionLocation
      !instruction
      !skipIfSet
      !registerExpression
      !bitNumberExpression}
  or
    $if$
    let @location instructionLocation = @location.here
    let @bool increment
    select
      $decf$ ; increment = false
    or
      $incf$ ; increment = true
    end
    <register_parsing> ?let registerExpression
    let @bool w_isTarget
    select
      w_isTarget = false
    or
      $,$ ; $w$ ; w_isTarget = true
    end
    $identifier$ ?let @lstring conditionString
    let @string condition = [conditionString.string uppercaseString]
    if condition != "NZ" then
      error conditionString:"invalid '" + condition + "' condition; it should be 'nz'"
    end
    $:$
    <baseline_simple_instruction> ?let instruction
    outInstruction = @baseline_instruction_IF_IncDec {
      !instructionLocation
      !instruction
      !increment
      !registerExpression
      !w_isTarget
    }
  or
    $if$
    let @lstring ifLabel
    select
      ifLabel = @{!"" !.here}
    or
      $label$ ?ifLabel
      [!?ioLabelMap insertKey !ifLabel]
    end
    <baseline_structured_if_instruction> !ifLabel ?outInstruction !?ioLabelMap
    $end$
    let @lstring endIfLabel
    select
      endIfLabel = @{!"" !.here}
    or
      $label$ ?endIfLabel
    end
    if ifLabel.string != endIfLabel.string then
      error endIfLabel : "'end' label does not match 'if' label, it should be "
         + (if ifLabel.string != "" then ifLabel.string else "empty" end)
    end 
  or
    $do$
    let @lstring doLabel
    select
      doLabel = @{!"" !.here}
    or
      $label$ ?doLabel
      [!?ioLabelMap insertKey !doLabel]
    end
    let instructionLocation = @location.here
    <baseline_instruction_list> ?let repeatedInstructionList !?ioLabelMap
    let endOfRepeatedInstructionList = @location.here
    var whilePartList = @baseline_partList ()
    repeat
      $while$
      let @lstring whileLabel
      select
        whileLabel = @{!"" !.here}
      or
        $label$ ?whileLabel
      end
      if doLabel.string != whileLabel.string then
        error whileLabel : "'while' label does not match 'do' label, it should be "
           + (if doLabel.string != "" then doLabel.string else "empty" end)
      end 
      <baseline_condition_expression> ?let whileCondition
      <baseline_instruction_list> ?let instructionList !?ioLabelMap
      whilePartList += !whileCondition !instructionList !@location.here
    while
    end
    $end$
    let @lstring endDoLabel
    select
      endDoLabel = @{!"" !.here}
    or
      $label$ ?endDoLabel
    end
    if doLabel.string != endDoLabel.string then
      error endDoLabel : "'end' label does not match 'do' label, it should be "
         + (if doLabel.string != "" then doLabel.string else "empty" end)
    end 
    outInstruction = @baseline_instruction_do_while {
      !instructionLocation
      !repeatedInstructionList
      !endOfRepeatedInstructionList
      !whilePartList}
  or
    $mnop$
    let @location instructionLocation = @location.here
    $integer$ ?let @luint occurrenceFactor
    outInstruction = @baseline_instruction_MNOP { !instructionLocation !occurrenceFactor}
  end
}

#----------------------------------------------------*

rule <baseline_structured_if_instruction>
  ?let @lstring ifLabel
  !@baseline_instruction outInstruction
  ?!@labelMap ioLabelMap
 {
  let @location instructionLocation = @location.here
  <baseline_condition_expression> ?let structured_if_condition
  <baseline_instruction_list> ?let thenInstructionList !?ioLabelMap
  var @baseline_instructionList elseInstructionList
  select
    elseInstructionList = @baseline_instructionList ()
  or
    $elsif$
    let @lstring elsifIfLabel
    select
      elsifIfLabel = @{!"" !.here}
    or
      $label$ ?elsifIfLabel
    end
    if ifLabel.string != elsifIfLabel.string then
      error elsifIfLabel : "'elsif' label does not match 'if' label, it should be "
         + (if ifLabel.string != "" then ifLabel.string else "empty" end)
    end 
    <baseline_structured_if_instruction> !ifLabel ?let elsifPartInstruction !?ioLabelMap
    elseInstructionList = @baseline_instructionList ()
    elseInstructionList += !elsifPartInstruction
  or
    $else$
    let @lstring elseLabel
    select
      elseLabel = @{!"" !.here}
    or
      $label$ ?elseLabel
    end
    if ifLabel.string != elseLabel.string then
      error elseLabel : "'else' label does not match 'if' label, it should be "
         + (if ifLabel.string != "" then ifLabel.string else "empty" end)
    end 
    <baseline_instruction_list> ?elseInstructionList !?ioLabelMap
  end
  let @location endOfElsePartLocation = @location.here
  outInstruction = @baseline_instruction_structured_if {
    !instructionLocation
    !structured_if_condition
    !thenInstructionList
    !elseInstructionList
    !endOfElsePartLocation}
}

#----------------------------------------------------*

rule <baseline_simple_instruction> !@baseline_instruction outInstruction {
  select
    let @baseline_instruction_FD_base_code baseCode
    select
      $addwf$
      baseCode = .ADDWF
    or
      $andwf$
      baseCode = .ANDWF
    or
      $comf$
      baseCode = .COMF
    or
      $decf$
      baseCode = .DECF
    or
      $incf$
      baseCode = .INCF
    or
      $iorwf$
      baseCode = .IORWF
    or
      $movf$
      baseCode = .MOVF
    or
      $rlf$
      baseCode = .RLF
    or
      $rrf$
      baseCode = .RRF
    or
      $subwf$
      baseCode = .SUBWF
    or
      $swapf$
      baseCode = .SWAPF
    or
      $xorwf$
      baseCode = .XORWF
    end
    let instructionLocation = @location.here
    <register_parsing> ?let registerExpression
    <optional_w_as_dest> ?let W_isDestination
    outInstruction = @baseline_instruction_FD {
      !instructionLocation
      !baseCode
      !registerExpression
      !W_isDestination
    }
  #--- Emit a warning if instruction is "movf" and W is not destination
    if (baseCode == .MOVF) && not W_isDestination then
      warning instructionLocation :  "This instruction does not move data, it only sets flags. "
        "Use 'movf *, reg' instead of 'movf reg' to suppress this warning" 
    end
  or
    $movf$
    let @location instructionLocation = .here
    $*$
    $,$
    <register_parsing> ?let registerName
    outInstruction = @baseline_instruction_FD {
      !instructionLocation
      !.MOVF
      !registerName
      !false # W is not destination
    }
  or
    let @baseline_F_instruction_base_code FAinstruction
    select
      $clrf$
      FAinstruction = @baseline_F_instruction_base_code. CLRF
    or
      $movwf$
      FAinstruction = @baseline_F_instruction_base_code. MOVWF
    end
    let @location instructionLocation = @location.here
    <register_parsing> ?let registerExpression
    outInstruction = @baseline_instruction_F {
      !instructionLocation
      !FAinstruction
      !registerExpression}
  or
    let @baseline_bit_oriented_op bitOrientedOp
    select
      $bcf$ ; bitOrientedOp = @baseline_bit_oriented_op. BCF
    or
      $bsf$ ; bitOrientedOp = @baseline_bit_oriented_op. BSF
    end
    let @location instructionLocation = @location.here
    <register_parsing> ?let registerExpression
    $.$
    <bit_number_parsing> ?let bitNumberExpression
    outInstruction = @baseline_instruction_FB {
      !instructionLocation
      !bitOrientedOp
      !registerExpression
      !bitNumberExpression}
  or
    $goto$
    let @location instructionLocation = @location.here
    $identifier$ ?let @lstring targetLabelName indexing routineCall
    outInstruction = @baseline_instruction_GOTO {
      !instructionLocation
      !targetLabelName}
  or
    $call$
    let @location instructionLocation = @location.here
    $identifier$ ?let @lstring targetLabelName indexing routineCall
    outInstruction = @baseline_instruction_CALL {
      !instructionLocation
      !targetLabelName}
  or
    $clrw$
    outInstruction = @baseline_instruction_WO_OPERAND { !@location.here !@baseline_WO_OPERAND_group. CLRW}
  or
    $clrwdt$
    outInstruction = @baseline_instruction_WO_OPERAND { !@location.here !@baseline_WO_OPERAND_group. CLRWDT}
  or
    $nop$
    outInstruction = @baseline_instruction_WO_OPERAND { !@location.here !@baseline_WO_OPERAND_group. NOP}
  or
    $option$
    outInstruction = @baseline_instruction_WO_OPERAND { !@location.here !@baseline_WO_OPERAND_group. OPTION_}
  or
    $sleep$
    outInstruction = @baseline_instruction_WO_OPERAND { !@location.here !@baseline_WO_OPERAND_group. SLEEP}
  or
    $tris$
    $identifier$ ?let @lstring operand
    outInstruction = @baseline_instruction_TRIS { !@location.here !operand}
  or
    let @baseline_literal_instruction_opcode literalInstruction
    select
      $andlw$ ; literalInstruction = @baseline_literal_instruction_opcode. ANDLW
    or
      $iorlw$ ; literalInstruction = @baseline_literal_instruction_opcode. IORLW
    or
      $movlw$ ; literalInstruction = @baseline_literal_instruction_opcode. MOVLW
    or
      $xorlw$ ; literalInstruction = @baseline_literal_instruction_opcode. XORLW
    end
    let @location instructionLocation = @location.here
    <immediate_expression> ?let expression
    outInstruction = @baseline_instruction_literalOperation {
      !instructionLocation
      !literalInstruction
      !expression}
  end
}

#----------------------------------------------------*

rule <baseline_condition_factor>
  !@baseline_conditionExpression outCondition {
  $!$
  <baseline_condition_factor> ?let condition
  outCondition = @baseline_negateCondition { !condition}
}

#----------------------------------------------------*

rule <baseline_condition_factor>
  !@baseline_conditionExpression outCondition {
  <baseline_condition_expression> ? outCondition
}

#----------------------------------------------------*

rule <baseline_condition_factor>
  !@baseline_conditionExpression outCondition {
  select
    let @bool isIncrement
    select
      $incf$ ; isIncrement = true
    or
      $decf$ ; isIncrement = false
    end
    <register_parsing> ?let registerExpression
    let @bool W_isDestination
    select
      W_isDestination = false
    or
      $,$ ; $w$ ; W_isDestination = true
    end
    $identifier$ ?let @lstring conditionString
    let @string condition = [conditionString.string uppercaseString]
    let @bool branchIfZero
    if condition == "Z" then
      branchIfZero = true
    elsif condition == "NZ" then
      branchIfZero = false
    else
      error conditionString: "invalid '" + conditionString
      + "' condition; valid ones are: 'z' and 'nz'"
      : branchIfZero
    end
    outCondition = @baseline_incDecRegisterInCondition {
      !registerExpression
      !isIncrement
      !W_isDestination
      !branchIfZero}
  or
    <register_parsing> ?let registerExpression
    $.$
    <bit_number_parsing> ?let bitNumberExpression
    outCondition = @baseline_bitTest_in_structured_if_condition {
      !registerExpression
      !bitNumberExpression
    }
  end
}

#----------------------------------------------------*

rule <baseline_condition_expression>
  !@baseline_conditionExpression outCondition {
  $($
  <baseline_condition_term> ? outCondition
  repeat
  while
    $|$
    <baseline_condition_term> ?let c
    let @baseline_conditionExpression rc = @baseline_negateCondition { !c}
    let @baseline_conditionExpression lc = @baseline_negateCondition { !outCondition}
    outCondition = @baseline_negateCondition { !@baseline_andCondition { !lc !rc}}
  end
  $)$
}

#----------------------------------------------------*

rule <baseline_condition_term>
  !@baseline_conditionExpression outCondition {
  <baseline_condition_factor> ? outCondition
  repeat
  while
    $&$
    <baseline_condition_factor> ?let c
    outCondition = @baseline_andCondition { !outCondition !c}
  end
}

#----------------------------------------------------*

}