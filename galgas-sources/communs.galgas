syntax communs (piccolo_lexique) {

#----------------------------------------------------*

rule <configuration_definition>
  ?!@configDefinitionList ioConfigDefinitionList
{
  $configuration$
  let defLocation = @location.here
  var settingList =@configSettingList.emptyList{}
  ${$
  repeat
  while
    $identifier$ ?let @lstring settingName
    $:$
    let @lstring settingValue
    select
      $identifier$ ? settingValue
    or
      $literal_string$ ? settingValue
    end
    settingList += !settingName !settingValue
  end
  $}$
  ioConfigDefinitionList += !defLocation !settingList
}

#----------------------------------------------------*

rule <declaration_in_ram> !@declarationInRam outDeclaration {
  let @registerProtection protection
  select
    protection = .publicRegister
  or
    $protected$
    protection = .protectedRegister
  or
    $private$
    protection = .privateRegister
  end
  $byte$
  $identifier$ ?let @lstring name indexing variableDeclaration
  let @immediatExpression sizeExpression
  select
    sizeExpression = @immediatInteger. new {!.new {!1 !.here}}
  or
    $[$
    <immediate_expression> ?sizeExpression
    $]$
  end
  var bitSliceTable = @bitSliceTable.emptyMap{}
  var @string bitDefinitionString
  select
    bitDefinitionString = ""
  or
    $<$
    bitDefinitionString = " <"
    var @sint sliceIndex = 8
    repeat
     select
        var @uint sliceWidth = 0 # Means undefined
        repeat
          var @uint sliceBase = 0
          if sliceIndex >= 0 then
            sliceBase = [sliceIndex uint]
          end
          $identifier$ ?let @lstring sliceName
          select
            if (sliceWidth != 0) & (sliceWidth != 1) then
              error sliceName:"a slice of 1 bit is expected for '" + sliceName + "'" 
            end
            sliceWidth = 1
            bitDefinitionString += sliceName.string 
          or
            $[$
            $integer$ ?let @luint sliceSize
            if (sliceWidth != 0) & (sliceWidth != sliceSize.uint) then
              error sliceName:"a slice of " + [sliceWidth string] + " bits is expected for '" + sliceName + "'" 
            end
            sliceWidth = sliceSize.uint
            sliceBase = sliceBase + 1 - sliceSize.uint
            bitDefinitionString += sliceName.string + "[" + [sliceSize.uint string] + "]" 
            $]$
          end
          var @uint base = 0
          if sliceIndex >= [sliceWidth sint] then
            base = [sliceIndex uint] - sliceWidth
          end
          [!?bitSliceTable insertKey !sliceName !base !sliceWidth]
        while
          $/$
          bitDefinitionString += "/"
        end
        sliceIndex = sliceIndex - [sliceWidth sint]
      or
        $-$
        bitDefinitionString += "-"
        sliceIndex -= 1
      or
        $integer$ ?let bitCount
        if bitCount.uint == 0 then
          error bitCount.location: "the bit count should be > 0"
        end
        for * in 0 ..< bitCount.uint
          do bitDefinitionString += "-"
          between bitDefinitionString += ", "
        end
        sliceIndex = sliceIndex - [bitCount.uint sint]
      end
    while
      $,$
      bitDefinitionString += ", " 
    end
    if sliceIndex != 0 then
      error name:"Incorrect bit definition for register '" + name + "': " + [8 - sliceIndex string] + " bits (s) are defined, instead of 8"
    end
    $>$
    bitDefinitionString += ">"
  end
  outDeclaration = @byteDeclarationInRam.new {!name !sizeExpression !bitSliceTable !bitDefinitionString !protection}
}

#----------------------------------------------------*

rule <ram_definition>
  ?!@ramDefinitionList ioRamDefinitionList
{
  $ram$
  $identifier$ ?let @lstring bankName
  ${$
  var declarationInRamList =@declarationInRamList.emptyList{}
  repeat
  while
    <declaration_in_ram> ?let @declarationInRam declaration
    declarationInRamList += !declaration
  end
  ioRamDefinitionList += !bankName !declarationInRamList
  $}$
}

#----------------------------------------------------*

rule <constant_definition>
  ?!@constantDefinitionList ioConstantDefinitionList {
  $const$
  $identifier$ ?let @lstring constantName indexing constantDeclaration
  $=$
  <immediate_expression> ?let @immediatExpression expression
  ioConstantDefinitionList += !constantName !expression
}

#----------------------------------------------------*

rule <immediate_expression> !@immediatExpression outExpression {
  <immediate_bitwise_term> ?outExpression
  repeat
  while
    $|$
    <immediate_bitwise_term> ?let @immediatExpression rightExpression
    outExpression = @immediatOr. new { !outExpression! rightExpression}
  while
    $^$
    <immediate_bitwise_term> ?let @immediatExpression rightExpression
    outExpression = @immediatXor. new { !outExpression! rightExpression}
  end
}

#----------------------------------------------------*

rule  <immediate_bitwise_term> !@immediatExpression outExpression {
  <immediate_bitwise_factor> ?outExpression
  repeat
  while
    $&$
    <immediate_bitwise_factor> ?let rightExpression
    outExpression = @immediatAnd. new { !outExpression! rightExpression}
  end
}

#----------------------------------------------------*

rule <immediate_bitwise_factor> !@immediatExpression outExpression {
  <immediate_term> ?outExpression
  select
  or
    $==$ 
    <immediate_term> ?let rightExpression
    outExpression = @immediatEqualTest. new { !outExpression! rightExpression}
  or
    $!=$ 
    <immediate_term> ?let rightExpression
    outExpression = @immediatNotEqualTest. new { !outExpression! rightExpression}
  or
    $>=$ 
    <immediate_term> ?let rightExpression
    outExpression = @immediatGreaterOrEqualTest. new { !outExpression! rightExpression}
  or
    $<=$ 
    <immediate_term> ?let rightExpression
    outExpression = @immediatLowerOrEqualTest. new { !outExpression! rightExpression}
  or
    $>$ 
    <immediate_term> ?let rightExpression
    outExpression = @immediatGreaterTest. new { !outExpression! rightExpression}
  or
    $<$ 
    <immediate_term> ?let rightExpression
    outExpression = @immediatLowerTest. new { !outExpression! rightExpression}
  end
}

#----------------------------------------------------*

rule <immediate_term> !@immediatExpression outExpression {
  <immediate_factor> ?outExpression
  repeat
  while
    $+$
    <immediate_factor> ?let rightExpression
    outExpression = @immediatAdd. new { !outExpression! rightExpression}
  while
    $-$
    <immediate_factor> ?let rightExpression
    outExpression = @immediatSub. new { !outExpression! rightExpression}
  while
    $<<$
    <immediate_factor> ?let rightExpression
    outExpression = @immediatLeftShift. new { !outExpression! rightExpression}
  while
    $>>$
    <immediate_factor> ?let rightExpression
    outExpression = @immediatRightShift. new { !outExpression! rightExpression}
  end
}

#----------------------------------------------------*

rule <immediate_factor> !@immediatExpression outExpression {
  <immediate_primary> ?outExpression
  repeat
  while
    $*$
    <immediate_primary> ?let rightExpression
    outExpression = @immediatMul. new { !outExpression! rightExpression}
  while
    $%$
    <immediate_primary> ?let rightExpression
    outExpression = @immediatMod. new { !outExpression! rightExpression}
  while
    $/$
    <immediate_primary> ?let rightExpression
    outExpression = @immediatDiv. new { !outExpression! rightExpression}
  end
}


#----------------------------------------------------*

rule <immediate_primary> !@immediatExpression outExpression {
  select
    $-$
    <immediate_primary> ?let expression
    outExpression = @immediatNegate. new { !expression}
  or
    $~$
    <immediate_primary> ?let expression
    outExpression = @immediatComplement. new { !expression}
  or
    $integer$ ?let @luint constant
    outExpression = @immediatInteger. new { !constant}
  or
    $literal_char$ ?let @lchar literalCharValue
    outExpression = @immediatInteger. new { !@luint. new { ![literalCharValue.char uint] !literalCharValue.location}}
  or
    $identifier$ ?let @lstring registerName indexing variableUse
    let @immediatExpression offset
    let @location endOfOffsetExpression
    select
      offset = @immediatInteger.new {!.new {!0 !.here}}
      endOfOffsetExpression = .here
    or
      $[$
      <immediate_expression> ?offset
      endOfOffsetExpression = .here
      $]$
    end
    let registerExpression = @registerExpression.new{
      !registerName
      !offset
      !endOfOffsetExpression
    }
    outExpression = @immediatRegister. new { !registerExpression}
  or
    $($
    <immediate_expression> ?outExpression
    $)$
  or
    $identifier$ ?let @lstring registerName
    $($
    var sliceExpressionList =@immediatSliceExpressionList.emptyList{}
    repeat
      $identifier$ ?let @lstring sliceName
      $:$
      <immediate_expression> ?let expression
      sliceExpressionList += !sliceName !expression
    while
      $,$
    end
    $)$
    outExpression = @immediatSlice.new {!registerName !sliceExpressionList}
  end
}

#----------------------------------------------------*

rule <register_parsing> !@registerExpression outRegisterExpression {
  $identifier$ ?let @lstring registerName indexing variableUse
  let @immediatExpression offset
  let @location endOfOffsetExpression
  select
    offset = @immediatInteger.new {!.new {!0 !.here}}
    endOfOffsetExpression = .here
  or
    $[$
    <immediate_expression> ?offset
    endOfOffsetExpression = .here
    $]$
  end
  outRegisterExpression = @registerExpression.new {
    !registerName
    !offset
    !endOfOffsetExpression
  }
}

#----------------------------------------------------*

rule <bit_number_parsing> !@bitNumberExpression outBitNumber {
  $identifier$ ?let @lstring bitNumberLabel
  let @luint bitIndex
  select
    bitIndex = @luint. new { !0 !@location.here}
  or
    $[$
    $integer$ ? bitIndex
    $]$
  end
  outBitNumber = @bitNumberLabelValue.new {!bitNumberLabel !bitIndex}
}

#----------------------------------------------------*

rule <bit_number_parsing> !@bitNumberExpression outBitNumber {
  $integer$ ?let bitIndex
  outBitNumber = @bitNumberLiteralValue.new {!bitIndex}
}

#----------------------------------------------------*

rule <bit_number_parsing> !@bitNumberExpression outBitNumber {
  $($
  <immediate_expression> ?let bitNumberExpression
  let endOfExpression = @location.here
  $)$
  outBitNumber = @bitNumberLiteralExpression.new {!bitNumberExpression !endOfExpression}
}

#----------------------------------------------------*

rule <optional_w_as_dest> !@bool out_W_isDestination {
  select
    out_W_isDestination = false
  or
    $,$
    $w$
    out_W_isDestination = true
  end
}

#----------------------------------------------------*

}