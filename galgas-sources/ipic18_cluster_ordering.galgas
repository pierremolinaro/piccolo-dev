

#----------------------------------------------------------------------------*

list @clusterList {
  @ipic18BlockList mBlockList
}

#----------------------------------------------------------------------------*

map @neededConversionForClusterOrder {
  @uint mConversions
  insert insertKey error message "the '%K' key is already declared in %L"
  search searchKey error message "the '%K' key is not declared"
}

#----------------------------------------------------------------------------*

func sortKey ?let @clusterList inClusterList -> @string outKey {
  outKey = ""
  for () in  inClusterList
  do
    outKey += [mBlockList mBlockAtIndex !0].mLabel.string
  between 
    outKey += "."
  end
}

#----------------------------------------------------------------------------*

proc perform_cluster_ordering
  ?let @clusterList inInitialClusterList
  ?!@ipic18BlockList ioBlockList
  ?!@string ioListFileContents {
  let @bool optimize = [option piccolo_options.performOptimizations value]
    & ([option piccolo_options.performOptimizations value] | [[option piccolo_options.optimizationFlags value] containsCharacter !'B'])
    & not [option piccolo_options.doNotReorderCluster value]
  if not optimize then
    ioListFileContents += "Option '--" + [option piccolo_options.doNotReorderCluster string] + "': this optimization is not performed.n\n"
  else
    let @uint clusterCount = [inInitialClusterList length]
    ioListFileContents += ["" stringByLeftAndRightPadding !79 !'*'] + "\n"
    ioListFileContents += "*" + ["CLUSTER ORDERING OPTIMIZATION" stringByLeftAndRightPadding !77 !' '] + "*\n"
    ioListFileContents += ["" stringByLeftAndRightPadding !79 !'*'] + "\n\n"
    @string s = "Cluster ordering optimization (" + clusterCount + " cluster" + if clusterCount > 1 then "s" else "" end + "):\n"
    ioListFileContents += s + "\n"
  #---
    if [option.verbose_output value] then
      message s
    end
  #------------------- Optimization loop
    @clusterList clusterList = inInitialClusterList
    var neededConversionForClusterOrder =@neededConversionForClusterOrder.emptyMap{}
    @bool continue = true
    @uint pass = 0
    loop( [clusterList length] * [clusterList length])
    while continue do
      pass ++
      ioListFileContents += "Pass " + pass + ":\n"
    #--- Build working block list and symbol table
      var workingBlockList =@ipic18BlockList.emptyList{}
      var symbolTable =@symbolTableForClusterOrdering.emptyMap{}
      for () in  clusterList do(clusterIndex)
        workingBlockList += mBlockList
        for () in  mBlockList do
          [!?symbolTable insertKey !mBlock.mLabel !clusterIndex]
        end
      end
    #--- Add symbols from last relocatable blocks
      let @uint insertionIdx = [workingBlockList length]
      for > () in  ioBlockList while continue do
        continue = mBlock.mAddress != @uint. max
        if continue then
          [!?symbolTable insertKey !mBlock.mLabel !@uint. max]
          [!?workingBlockList insertAtIndex !mBlock !insertionIdx]
        end
      end 
    #--- Check for overflows
      ipic18RelativeBranchOverflow (
        !workingBlockList
        !ioBlockList # For entering absolute labels
        ?let @branchOverflowMap overflowMap
      )
      @uint overflowCount = 0
      for () in  overflowMap do
        for () in  mList do
          overflowCount ++
          [symbolTable searchKey ![key nowhere] ?let @uint sourceCluster]
          [symbolTable searchKey ![mValue nowhere] ?let @uint targetCluster]
          ioListFileContents += "  " + key + " (cluster #" + sourceCluster + ") --> " + mValue + " (cluster #" + targetCluster + ")\n"
        end
      end
      [!?neededConversionForClusterOrder insertKey
        ![sortKey (!clusterList) nowhere]
        !overflowCount
      ]
      if [option.verbose_output value] then
        @string s = "  Pass " + pass + ": "
        if overflowCount == 0 then
          s += "no branch overflow"
        elsif overflowCount == 1 then
          s += "1 branch overflow"
        else
          s += [overflowCount string] + " branch overflows"
        end
        message s
      end
    #--- Correction
      @bool correctionLoop = true
      for () in  overflowMap while correctionLoop do
        for () in  mList while correctionLoop do
          [symbolTable searchKey ![mValue nowhere] ?let @uint targetCluster]
          if targetCluster != @uint. max then
            [symbolTable searchKey ![key nowhere] ?let @uint sourceCluster]
            if (sourceCluster + 1) == targetCluster then
              correctionLoop = false
              continue = true
              [!?clusterList removeAtIndex ?let @ipic18BlockList last !targetCluster ]
              [!?clusterList removeAtIndex ?let @ipic18BlockList first !sourceCluster ]
              [!?clusterList insertAtIndex !first + last !sourceCluster]
              let @string m = "merge #" + targetCluster + " into #" + sourceCluster + "]"
              ioListFileContents += "  --> " + m + "\n"
              if [option.verbose_output value] then
                message " -> " + m
              end
            elsif sourceCluster == (targetCluster + 1) then
              correctionLoop = false
              continue = true
              [!?clusterList removeAtIndex ?let @ipic18BlockList last !sourceCluster ]
              [!?clusterList removeAtIndex ?let @ipic18BlockList first !targetCluster ]
              [!?clusterList insertAtIndex !first + last !targetCluster]
              let @string m = "merge #" + sourceCluster + " into #" + targetCluster + "]"
              ioListFileContents += "  --> " + m + "\n"
              if [option.verbose_output value] then
                message " -> " + m
              end
            elsif sourceCluster < targetCluster then
              @clusterList candidate = clusterList
              [!?candidate removeAtIndex ?let @ipic18BlockList b !targetCluster ]
              [!?candidate insertAtIndex !b !(targetCluster + sourceCluster) / 2 ]
              if not [neededConversionForClusterOrder hasKey !sortKey (!candidate)] then
                clusterList = candidate
                correctionLoop = false
                continue = true
                let @string m = "roll up [#" + ((targetCluster + sourceCluster) / 2) + ", #" + targetCluster + "]"
                ioListFileContents += "  --> " + m + "\n"
                if [option.verbose_output value] then
                  message " -> " + m
                end
              end
            elsif sourceCluster > targetCluster then
              @clusterList candidate = clusterList
              [!?candidate removeAtIndex ?let @ipic18BlockList b !sourceCluster ]
              [!?candidate insertAtIndex !b !(targetCluster + sourceCluster) / 2 ]
              if not [neededConversionForClusterOrder hasKey !sortKey (!candidate)] then
                clusterList = candidate
                correctionLoop = false
                continue = true
                let @string m = "roll up [#" + targetCluster+ ", #" + ((targetCluster + sourceCluster) / 2) + "]"
                ioListFileContents += "  --> " + m + "\n"
                if [option.verbose_output value] then
                  message " -> " + m
                end
              end
            end
          end
        end
      end
      if [option.verbose_output value] then
        message ".\n"
      end
    end
    if [option.verbose_output value] then
      message "\n"
    end
    ioListFileContents += "\n"
  #---------------------------------- Build the new ordered block list
    var newBlockList =@ipic18BlockList.emptyList{}
  #--- First insert absolute blocks from 0
    continue = true
    for () in  ioBlockList while continue do
      continue = mBlock.mAddress != @uint. max
      if continue then
        newBlockList += !mBlock
      end
    end 
  #--- Insert sorted blocks 
    for () in  clusterList do
      newBlockList += mBlockList
    end
  #--- Insert absolute blocks from the end
    let @uint insertionIndex = [newBlockList length]
    continue = true
    for > () in  ioBlockList while continue do
      continue = mBlock.mAddress != @uint. max
      if continue then
        [!?newBlockList insertAtIndex !mBlock !insertionIndex]
      end
    end 
  #--- Set result
    ioBlockList = newBlockList
  end
}      

#----------------------------------------------------------------------------*


