syntax midrange_syntax (piccolo_lexique) {

#----------------------------------------------------*

rule <configuration_definition>
  ?!@configDefinitionList ioConfigDefinitionList

rule <declaration_in_ram>
  !@declarationInRam outDeclaration

rule <ram_definition>
  ?!@ramDefinitionList ioRamDefinitionList

rule <constant_definition>
  ?!@constantDefinitionList ioConstantDefinitionList

rule <register_parsing>
  !@registerExpression outRegisterExpression

rule <optional_w_as_dest>
  !@bool out_W_isDestination

rule <bit_number_parsing>
  !@bitNumberExpression outBitNumber

rule <immediate_expression>
  !@immediatExpression outExpression

#----------------------------------------------------*

rule <start_symbol> {
  $midrange$
  $identifier$ ?var  @lstring programName
  $literal_string$ ?var  @lstring deviceName
  $:$
  var interruptDefinitionList =@midrange_interruptDefinitionList.emptyList{}
  var routineDefinitionList =@midrange_routineDefinitionList.emptyList{}
  var ramDefinitionList =@ramDefinitionList.emptyList{}
  var unusedRegisterList =@lstringlist.emptyList{}
  var unusedRoutineList =@lstringlist.emptyList{}
  var configDefinitionList =@configDefinitionList.emptyList{}
  var constantDefinitionList =@constantDefinitionList.emptyList{}
  var @bool needsComputedGoto2 = false
  var @bool needsComputedGoto4 = false
  repeat
  while
    $unused$
    $byte$
    repeat
      $identifier$ ?var @lstring unusedRegister
      unusedRegisterList += !unusedRegister
    while 
      $,$
    end
  while
    $unused$
    $routine$
    repeat
      $identifier$ ?var @lstring unusedRoutine
      unusedRoutineList += !unusedRoutine
    while 
      $,$
    end
  while
    <configuration_definition> !?configDefinitionList
  while
    <ram_definition> !?ramDefinitionList
  while
    <constant_definition> !?constantDefinitionList
  while
    <midrange_interrupt_definition>
      !?interruptDefinitionList
      !?needsComputedGoto2
      !?needsComputedGoto4
  while
    <midrange_routine_definition>
      !?routineDefinitionList
      !?needsComputedGoto2
      !?needsComputedGoto4
  end
  $end$
#--- Semantic analysis
  if @uint. errorCount == 0 then
    var piccoloModel =@midrange_model.new{
      !programName
      !deviceName
      !configDefinitionList
      !ramDefinitionList
      !unusedRegisterList
      !interruptDefinitionList
      !constantDefinitionList
      !routineDefinitionList
      !unusedRoutineList
      !needsComputedGoto2
      !needsComputedGoto4
      !@location.here
    }
    midrange_analyze ( !piccoloModel !@string. stringWithSourceFilePath )
  else
    var @string hexDestinationFile = [@string. stringWithSourceFilePath stringByDeletingPathExtension] + ".hex"
    if [hexDestinationFile fileExists] then
      [@string deleteFile !hexDestinationFile]
    end
    var @string asmDestinationFile = [@string. stringWithSourceFilePath stringByDeletingPathExtension] + ".asm"
    if [asmDestinationFile fileExists] then
      [@string deleteFile !asmDestinationFile]
    end
  end
}
label importBootloaderSpecification
  !@piccoloDeviceModel outPiccoloDeviceModel
  !@string outDeviceName
  !@uint outAccessBankSplitOffset
  !@ramBankTable outRamBank
  !@registerTable outRegisterTable
  !@bootloaderReservedRAMmap outBootloaderReservedRAMmap
  !@routineDeclarationList outBootloaderRoutineDeclarationList
  !@routineDeclarationList outUserRoutineDeclarationList
  !@luint outBootloaderReservedROMsize {
  $midrange$
  $identifier$ ?var  @lstring programName
  error programName: "this file does not define a bootloader specification"
   : outPiccoloDeviceModel, outAccessBankSplitOffset, outRamBank, outRegisterTable,
      outBootloaderReservedRAMmap, outBootloaderRoutineDeclarationList,
      outUserRoutineDeclarationList, outDeviceName,
      outBootloaderReservedROMsize
  $literal_string$ ? *
  $:$
  repeat
  while
    $unused$
    $byte$
    repeat
      $identifier$ ?*
    while 
      $,$
    end
  while
    $unused$
    $routine$
    repeat
      $identifier$ ?*
    while 
      $,$
    end
  while
     parse<configuration_definition>
  while
     parse<ram_definition>
  while
     parse<constant_definition>
  while
     parse<midrange_interrupt_definition>
  while
     parse<midrange_routine_definition>
  end
  $end$
}

#----------------------------------------------------*

rule <midrange_instruction_list>
  !@midrange_instructionList outInstructionList
  ?!@bool ioNeedsComputedGoto2
  ?!@bool ioNeedsComputedGoto4
  ?!@labelMap ioLabelMap
  {
  outInstructionList = @midrange_instructionList. emptyList
  repeat
  while
    <midrange_simple_instruction> ?let instruction
    outInstructionList += !instruction
  while
    <midrange_structured_instruction>
      ?let instruction
      !?ioNeedsComputedGoto2
      !?ioNeedsComputedGoto4
      !?ioLabelMap
    outInstructionList += !instruction
  end
}

#----------------------------------------------------*

rule <midrange_interrupt_definition>
  ?!@midrange_interruptDefinitionList ioInterruptDefinitionList
  ?!@bool ioNeedsComputedGoto2 
  ?!@bool ioNeedsComputedGoto4 {
  $interrupt$
  var @location interruptionLocation = @location.here
  $contextsave$
  $identifier$ ?var  @lstring firstSaveRegister
  $,$
  $identifier$ ?var  @lstring secondSaveRegister
  ${$
  var @labelMap labelMap = {}
  <midrange_instruction_list>
    ?let instructionList
    !?ioNeedsComputedGoto2
    !?ioNeedsComputedGoto4
    !?labelMap
  $}$
  ioInterruptDefinitionList +=
   !interruptionLocation
   !instructionList
   !firstSaveRegister
   !secondSaveRegister
   !.here
}

#----------------------------------------------------*

rule <midrange_routine_definition>
  ?!@midrange_routineDefinitionList ioRoutineDefinitionList
  ?!@bool ioNeedsComputedGoto2 
  ?!@bool ioNeedsComputedGoto4
{
  var @bool noreturn
  select
    noreturn = false
  or
    $noreturn$
    noreturn = true
  end
  $routine$
  $identifier$ ?var  @lstring routineName indexing routineDefinition
#--- Page
  var @luint page
  select
    page = @luint. new { !0 !@location.here}
  or
    $page$
    $integer$ ? page
  end
#--- Bank
  var @luint requiredBank
  var @luint returnedBank
  var @bool preservesBank
  select
    requiredBank = @luint. new { !@uint. max !@location.here}
    returnedBank = @luint. new { !@uint. max !@location.here}
    preservesBank = false
  or
    $bank$
    $:$
    select
      $preserved$
      if noreturn then
        error @location.here: "a \"noreturn\" routine cannot accept \"bank:preserved\" setting"
      end
      preservesBank = true
      requiredBank = @luint. new { !@uint. max !@location.here}
      returnedBank = @luint. new { !@uint. max !@location.here}
    or
      $requires$
      $integer$ ? requiredBank
      preservesBank = false
      select
        returnedBank = @luint. new { !@uint. max !@location.here}
      or
        $ensures$
        $integer$ ? returnedBank
        if noreturn then
          error @location.here: "a \"noreturn\" routine cannot accept \"bank:returned\" setting"
        end
      end
    or
      $ensures$
      $integer$ ? returnedBank
      if noreturn then
        error @location.here: "a \"noreturn\" routine cannot accept \"bank:returned\" setting"
      end
      preservesBank = false
      requiredBank = @luint. new { !@uint. max !@location.here}
    end
  end
  ${$
  var @labelMap labelMap = {}
  <midrange_instruction_list>
    ?let instructionList
    !?ioNeedsComputedGoto2
    !?ioNeedsComputedGoto4
    !?labelMap
  $}$
  ioRoutineDefinitionList +=
    !routineName
    !page
    !requiredBank
    !returnedBank
    !preservesBank
    !noreturn
    !instructionList
    !@location.here
}

#----------------------------------------------------*

rule <midrange_structured_instruction>
  !@midrange_instruction outInstruction
  ?!@bool ioNeedsComputedGoto2
  ?!@bool ioNeedsComputedGoto4
  ?!@labelMap ioLabelMap
 {
  select
    $do$
    var @lstring doLabel
    select
      doLabel = .new {!"" !.here}
    or
      $label$ ?doLabel
      [!?ioLabelMap insertKey !doLabel]
    end
    let instructionLocation = @location.here
    $($
    $identifier$ ?let constantName
    $:$
    <immediate_expression> ?let @immediatExpression lowBoundExpression
    $...$
    <immediate_expression> ?let @immediatExpression highBoundExpression
    $)$
    <midrange_instruction_list>
      ?let @midrange_instructionList instructionList
      !?ioNeedsComputedGoto2
      !?ioNeedsComputedGoto4
      !?ioLabelMap
    outInstruction = @midrange_instruction_STATIC_REPEAT. new {
      !instructionLocation
      !constantName
      !lowBoundExpression
      !highBoundExpression
      !instructionList
      !@location.here}
    $end$
    var @lstring endDoLabel
    select
      endDoLabel = .new {!"" !.here}
    or
      $label$ ?endDoLabel
    end
    if doLabel.string != endDoLabel.string then
      error endDoLabel : "'end' label does not match 'do' label, it should be "
         + (if doLabel.string != "" then doLabel.string else "empty" end)
    end 
  or
    $checkbank$
    $integer$ ?var  @luint bankIndex
    outInstruction = @midrangeInstruction_checkbank. new { !bankIndex.location !bankIndex.uint}
  or
    $checknobank$
    outInstruction = @midrangeInstruction_checknobank. new { !@location.here}
  or
    $banksel$
    var @location instructionLocation = @location.here
    <register_parsing> ?var @registerExpression register
    var @bool warningOnUselessBanksel
    select
      warningOnUselessBanksel = true
    or
      $,$
      $*$
      warningOnUselessBanksel = false
    end
    outInstruction = @midrange_instruction_banksel_register. new { !instructionLocation !register !warningOnUselessBanksel}
  or
    $banksel$
    var @location instructionLocation = @location.here
    $integer$ ?var  @luint bank
    var @bool warningOnUselessBanksel
    select
      warningOnUselessBanksel = true
    or
      $,$
      $*$
      warningOnUselessBanksel = false
    end
    outInstruction = @midrange_instruction_banksel. new { !instructionLocation !bank !warningOnUselessBanksel}
  or
    $banksave$
    var @location instructionLocation = @location.here
    <register_parsing> ?var @registerExpression register
    var @midrange_instructionList instructionList
    <midrange_instruction_list>
      ?instructionList
      !?ioNeedsComputedGoto2
      !?ioNeedsComputedGoto4
      !?ioLabelMap
    $end$
    outInstruction = @midrange_instruction_savebank. new {
      !instructionLocation
      !register
      !instructionList
      !@location.here}
  or
    $nobank$
    outInstruction = @midrange_instruction_nobanksel. new { !@location.here}
  or
    $forever$
    var @lstring foreverLabel
    select
      foreverLabel = .new {!"" !.here}
    or
      $label$ ?foreverLabel
      [!?ioLabelMap insertKey !foreverLabel]
    end
    var @location instructionLocation = @location.here
    <midrange_instruction_list>
      ?let instructionList
      !?ioNeedsComputedGoto2
      !?ioNeedsComputedGoto4
      !?ioLabelMap
    $end$
    var @lstring endForeverLabel
    select
      endForeverLabel = .new {!"" !.here}
    or
      $label$ ?endForeverLabel
    end
    if foreverLabel.string != endForeverLabel.string then
      error endForeverLabel : "'end' label does not match 'fovever' label, it should be "
         + (if foreverLabel.string != "" then foreverLabel.string else "empty" end)
    end 
    outInstruction = @midrange_instruction_FOREVER. new {
      !instructionLocation
      !instructionList
      !@location.here}
  or
    $if$
    var @location instructionLocation = @location.here
    var @registerExpression registerExpression
    var @bitNumberExpression bitNumberExpression
    var @bool skipIfSet
    select
      $!$
      <register_parsing> ? registerExpression
      $.$
      <bit_number_parsing> ? bitNumberExpression
      skipIfSet = true
    or
      <register_parsing> ? registerExpression
      $.$
      <bit_number_parsing> ? bitNumberExpression
      skipIfSet = false
    end
    $:$
    var @midrange_instruction instruction
    <midrange_simple_instruction> ?instruction
    outInstruction = @midrange_instruction_IF_BitTest. new {
      !instructionLocation
      !instruction
      !skipIfSet
      !registerExpression
      !bitNumberExpression}
  or
    $if$
    var @location instructionLocation = @location.here
    var @bool increment
    select
      $decf$ increment = false
    or
      $incf$ increment = true
    end
    var @registerExpression registerExpression
    <register_parsing> ? registerExpression
    var @bool w_isTarget
    select
      w_isTarget = false
    or
      $,$ $w$ w_isTarget = true
    end
    $identifier$ ?var  @lstring conditionString
    var @string condition = [conditionString.string uppercaseString]
    if condition != "NZ" then
      error conditionString:"invalid '" + condition + "' condition; it should be 'nz'"
    end
    $:$
    var @midrange_instruction instruction
    <midrange_simple_instruction> ?instruction
    outInstruction = @midrange_instruction_IF_IncDec. new {
      !instructionLocation
      !instruction
      !increment
      !registerExpression
      !w_isTarget
    }
  or
    $if$
    var @lstring ifLabel
    select
      ifLabel = .new {!"" !.here}
    or
      $label$ ?ifLabel
      [!?ioLabelMap insertKey !ifLabel]
    end
    <midrange_structured_if_instruction>
      !ifLabel
      ?outInstruction
      !?ioNeedsComputedGoto2
      !?ioNeedsComputedGoto4
      !?ioLabelMap
    $end$
    var @lstring endIfLabel
    select
      endIfLabel = .new {!"" !.here}
    or
      $label$ ?endIfLabel
    end
    if ifLabel.string != endIfLabel.string then
      error endIfLabel : "'end' label does not match 'if' label, it should be "
         + (if ifLabel.string != "" then ifLabel.string else "empty" end)
    end 
  or
    $do$
    var @lstring doLabel
    select
      doLabel = .new {!"" !.here}
    or
      $label$ ?doLabel
      [!?ioLabelMap insertKey !doLabel]
    end
    var @location instructionLocation = @location.here
    var @midrange_instructionList repeatedInstructionList
    <midrange_instruction_list>
      ?repeatedInstructionList
      !?ioNeedsComputedGoto2
      !?ioNeedsComputedGoto4
      !?ioLabelMap
    var @location endOfRepeatedInstructionList = @location.here
    var whilePartList =@midrange_partList.emptyList{}
    repeat
      $while$
      var @lstring whileLabel
      select
        whileLabel = .new {!"" !.here}
      or
        $label$ ?whileLabel
      end
      if doLabel.string != whileLabel.string then
        error whileLabel : "'while' label does not match 'do' label, it should be "
           + (if doLabel.string != "" then doLabel.string else "empty" end)
      end 
      var @midrange_conditionExpression whileCondition
      <midrange_condition_expression> ?whileCondition
      <midrange_instruction_list>
        ?let instructionList
        !?ioNeedsComputedGoto2
        !?ioNeedsComputedGoto4
      !?ioLabelMap
      whilePartList += !whileCondition !instructionList !@location.here
    while
    end
    $end$
    var @lstring endDoLabel
    select
      endDoLabel = .new {!"" !.here}
    or
      $label$ ?endDoLabel
    end
    if doLabel.string != endDoLabel.string then
      error endDoLabel : "'end' label does not match 'do' label, it should be "
         + (if doLabel.string != "" then doLabel.string else "empty" end)
    end 
    outInstruction = @midrange_instruction_do_while. new {
      !instructionLocation
      !repeatedInstructionList
      !endOfRepeatedInstructionList
      !whilePartList}
  or
    $mnop$
    var @location instructionLocation = @location.here
    $integer$ ?var  @luint occurrenceFactor
    outInstruction = @midrange_instruction_MNOP. new { !instructionLocation !occurrenceFactor}
  or
    $jsr$
    var @location instructionLocation = @location.here
    $identifier$ ?var  @lstring targetLabelName
    outInstruction = @midrange_instruction_JSR. new {
      !instructionLocation
      !targetLabelName}
  or
    $jump$
    var @location instructionLocation = @location.here
    $identifier$ ?var  @lstring targetLabelName
    outInstruction = @midrange_instruction_JUMP. new {
      !instructionLocation
      !targetLabelName}
  end
}

#----------------------------------------------------*

rule <midrange_structured_if_instruction>
  ?let @lstring ifLabel
  !@midrange_instruction outInstruction
  ?!@bool ioNeedsComputedGoto2
  ?!@bool ioNeedsComputedGoto4
  ?!@labelMap ioLabelMap

{
  var @location instructionLocation = @location.here
  var @midrange_conditionExpression structured_if_condition
  <midrange_condition_expression> ?structured_if_condition
  <midrange_instruction_list>
    ?let thenInstructionList
    !?ioNeedsComputedGoto2
    !?ioNeedsComputedGoto4
    !?ioLabelMap
  var @midrange_instructionList elseInstructionList
  select
    elseInstructionList = @midrange_instructionList. emptyList
  or
    $elsif$
    var @lstring elsifIfLabel
    select
      elsifIfLabel = .new {!"" !.here}
    or
      $label$ ?elsifIfLabel
    end
    if ifLabel.string != elsifIfLabel.string then
      error elsifIfLabel : "'elsif' label does not match 'if' label, it should be "
         + (if ifLabel.string != "" then ifLabel.string else "empty" end)
    end 
    <midrange_structured_if_instruction>
      !ifLabel
      ?let elsifPartInstruction
      !?ioNeedsComputedGoto2
      !?ioNeedsComputedGoto4
      !?ioLabelMap
    elseInstructionList = @midrange_instructionList. emptyList
    elseInstructionList += !elsifPartInstruction
  or
    $else$
    var @lstring elseLabel
    select
      elseLabel = .new {!"" !.here}
    or
      $label$ ?elseLabel
    end
    if ifLabel.string != elseLabel.string then
      error elseLabel : "'else' label does not match 'if' label, it should be "
         + (if ifLabel.string != "" then ifLabel.string else "empty" end)
    end 
    <midrange_instruction_list>
      ?elseInstructionList
      !?ioNeedsComputedGoto2
      !?ioNeedsComputedGoto4
      !?ioLabelMap
  end
  var @location endOfElsePartLocation = @location.here
  outInstruction = @midrange_instruction_structured_if. new {
    !instructionLocation
    !structured_if_condition
    !thenInstructionList
    !elseInstructionList
    !endOfElsePartLocation}
}

#----------------------------------------------------*

rule <midrange_simple_instruction> !@midrange_instruction outInstruction {
  select
    var @midrange_instruction_FD_base_code baseCode
    select
      $addwf$
      baseCode = .ADDWF
    or
      $andwf$
      baseCode = .ANDWF
    or
      $comf$
      baseCode = .COMF
    or
      $decf$
      baseCode = .DECF
    or
      $incf$
      baseCode = .INCF
    or
      $iorwf$
      baseCode = .IORWF
    or
      $movf$
      baseCode = .MOVF
    or
      $rlf$
      baseCode = .RLF
    or
      $rrf$
      baseCode = .RRF
    or
      $subwf$
      baseCode = .SUBWF
    or
      $swapf$
      baseCode = .SWAPF
    or
      $xorwf$
      baseCode = .XORWF
    end
    let instructionLocation = @location.here
    <register_parsing> ?let registerExpression
    <optional_w_as_dest> ?let W_isDestination
    outInstruction = @midrange_instruction_FD. new {
      !instructionLocation
      !baseCode
      !registerExpression
      !W_isDestination
    }
  #--- Emit a warning if instruction is "movf" and W is not destination
    if (baseCode == .MOVF) && not W_isDestination then
      warning instructionLocation :  "This instruction does not move data, it only sets flags. "
        "Use 'movf *, reg' instead of 'movf reg' to suppress this warning" 
    end
  or
    $movf$
    let @location instructionLocation = .here
    $*$
    $,$
    <register_parsing> ?let registerName
    outInstruction = @midrange_instruction_FD.new {
      !instructionLocation
      !.MOVF
      !registerName
      !false # W is not destination
    }
  or
    var @midrange_F_instruction_base_code FAinstruction
    select
      $clrf$
      FAinstruction = @midrange_F_instruction_base_code. CLRF
    or
      $movwf$
      FAinstruction = @midrange_F_instruction_base_code. MOVWF
    end
    var @location instructionLocation = @location.here
    var @registerExpression registerExpression
    <register_parsing> ? registerExpression
    outInstruction = @midrange_instruction_F. new {
      !instructionLocation
      !FAinstruction
      !registerExpression}
  or
    var @midrange_bit_oriented_op bitOrientedOp
    select
      $bcf$ bitOrientedOp = @midrange_bit_oriented_op. BCF
    or
      $bsf$ bitOrientedOp = @midrange_bit_oriented_op. BSF
    end
    var @location instructionLocation = @location.here
    var @registerExpression registerExpression
    <register_parsing> ? registerExpression
    $.$
    var @bitNumberExpression bitNumberExpression
    <bit_number_parsing> ? bitNumberExpression
    outInstruction = @midrange_instruction_FB. new {
      !instructionLocation
      !bitOrientedOp
      !registerExpression
      !bitNumberExpression}
  or
    $goto$
    var @location instructionLocation = @location.here
    $identifier$ ?var  @lstring targetLabelName indexing routineCall
    outInstruction = @midrange_instruction_GOTO. new {
      !instructionLocation
      !targetLabelName}
  or
    $call$
    let @location instructionLocation = @location.here
    $identifier$ ?var  @lstring targetLabelName indexing routineCall
    outInstruction = @midrange_instruction_CALL. new {
      !instructionLocation
      !targetLabelName}
  or
    $clrw$
    outInstruction = @midrange_instruction_CLRW. new { !@location.here}
  or
    $clrwdt$
    outInstruction = @midrange_instruction_CLRWDT. new { !@location.here}
  or
    $nop$
    outInstruction = @midrange_instruction_NOP. new { !@location.here}
  or
    $sleep$
    outInstruction = @midrange_instruction_SLEEP. new { !@location.here}
  or
    var @midrange_literal_instruction_opcode literalInstruction
    select
      $addlw$ literalInstruction = @midrange_literal_instruction_opcode. ADDLW
    or
      $andlw$ literalInstruction = @midrange_literal_instruction_opcode. ANDLW
    or
      $iorlw$ literalInstruction = @midrange_literal_instruction_opcode. IORLW
    or
      $movlw$ literalInstruction = @midrange_literal_instruction_opcode. MOVLW
    or
      $sublw$ literalInstruction = @midrange_literal_instruction_opcode. SUBLW
    or
      $xorlw$ literalInstruction = @midrange_literal_instruction_opcode. XORLW
    end
    var @location instructionLocation = @location.here
    var @immediatExpression expression
    <immediate_expression> ?expression
    outInstruction = @midrange_instruction_literalOperation. new {
      !instructionLocation
      !literalInstruction
      !expression}
  end
}

#----------------------------------------------------*

rule <midrange_condition_factor>
  !@midrange_conditionExpression outCondition {
  $!$
  var @midrange_conditionExpression condition
  <midrange_condition_factor> ? condition
  outCondition = @midrange_negateCondition. new { !condition}
}

#----------------------------------------------------*

rule <midrange_condition_factor>
  !@midrange_conditionExpression outCondition {
  <midrange_condition_expression> ? outCondition
}

#----------------------------------------------------*

rule <midrange_condition_factor>
  !@midrange_conditionExpression outCondition {
  select
    var @bool isIncrement
    select
      $incf$ isIncrement = true
    or
      $decf$ isIncrement = false
    end
    var @registerExpression registerExpression
    <register_parsing> ? registerExpression
    var @bool W_isDestination
    select
      W_isDestination = false
    or
      $,$ $w$ W_isDestination = true
    end
    $identifier$ ?var  @lstring conditionString
    var @string condition = [conditionString.string uppercaseString]
    var @bool branchIfZero
    if condition == "Z" then
      branchIfZero = true
    elsif condition == "NZ" then
      branchIfZero = false
    else
      error conditionString: "invalid '" + conditionString
      + "' condition; valid ones are: 'z' and 'nz'"
      : branchIfZero
    end
    outCondition = @midrange_incDecRegisterInCondition. new {
      !registerExpression
      !isIncrement
      !W_isDestination
      !branchIfZero}
  or
    var @registerExpression registerExpression
    var @bitNumberExpression bitNumberExpression
#    @bool BTFSSinstruction
    <register_parsing> ? registerExpression
    $.$
    <bit_number_parsing> ?bitNumberExpression
#    select
#      BTFSSinstruction = false
#    or
#      $identifier$ ?var  @lstring conditionString
#      @string condition = [[conditionString string] uppercaseString]
#      if condition == "Z" then
#        BTFSSinstruction = true
#        @string s = "! " + [[registerExpression mRegisterName] string]
#        if [[registerExpression mOffset] uint] > 0 then
#          s += " [" + [[[registerExpression mOffset] uint] string] + "]"
#        end
#        s += "." [bitNumberExpression display !?s]
#        warning conditionString: "In Piccolo 1.2.0 and later, this construction is deprecated; use '" + s + "' instead" 
#      elsif condition == "NZ" then
#        BTFSSinstruction = false
#        @string s = [[registerExpression mRegisterName] string]
#        if [[registerExpression mOffset] uint] > 0 then
#          s += " [" + [[[registerExpression mOffset] uint] string] + "]"
#        end
#        s += "." [bitNumberExpression display !?s]
#        warning conditionString: "In Piccolo 1.2.0 and later, this construction is deprecated; use '" + s + "' instead" 
#      else
#        error conditionString: "invalid '" + conditionString
#        + "' condition; valid ones are: 'z' and 'nz'"
#        : BTFSSinstruction
#      end
#    end
    outCondition = @midrange_bitTest_in_structured_if_condition. new {
      !registerExpression
      !bitNumberExpression
#      !false
    }
  end
}

#----------------------------------------------------*

rule <midrange_condition_expression>
  !@midrange_conditionExpression outCondition {
  $($
  <midrange_condition_term> ? outCondition
  repeat
  while
    $|$
    var @midrange_conditionExpression c
    <midrange_condition_term> ? c
    var @midrange_conditionExpression rc = @midrange_negateCondition. new { !c}
    var @midrange_conditionExpression lc = @midrange_negateCondition. new { !outCondition}
    outCondition = @midrange_negateCondition. new { !@midrange_andCondition. new { !lc !rc}}
  end
  $)$
}

#----------------------------------------------------*

rule <midrange_condition_term>
  !@midrange_conditionExpression outCondition {
  <midrange_condition_factor> ? outCondition
  repeat
  while
    $&$
    var @midrange_conditionExpression c
    <midrange_condition_factor> ? c
    outCondition = @midrange_andCondition. new { !outCondition !c}
  end
}

#----------------------------------------------------*

}