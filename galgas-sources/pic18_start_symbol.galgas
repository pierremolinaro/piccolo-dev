syntax pic18_start_symbol (piccolo_lexique) {

#----------------------------------------------------*

rule <body>
  ?!@pic18InterruptDefinitionList interruptDefinitionList
  ?!@pic18RoutineDefinitionList routineDefinitionList
  ?!@pic18MacroDefinitionList ioMacroDefinitionList
  ?!@lstringlist unusedRoutineList
  ?!@lstringlist inlinedRoutineList
  ?!@ramDefinitionList ramDefinitionList
  ?!@lstringlist unusedRegisterList
  ?!@configDefinitionList configDefinitionList
  ?!@constantDefinitionList constantDefinitionList
  ?!@checkpicList ioCheckpicList
  ?!@dataList dataList
  ?!@bool needsComputedGoto2
  ?!@bool needsComputedGoto4

#----------------------------------------------------*

rule <start_symbol> {
  $pic18$
  $identifier$ ?let @lstring programName
  let @lstring deviceNameOrBootloaderReference
  let @programKind programKind
  select
    programKind = @programKind. regularProgram
  or
    $implements$
    $bootloader$
    programKind = @programKind. bootloaderProgram
  or
    $uses$
    $bootloader$
    programKind = @programKind. userProgram
  end
  $literal_string$ ? deviceNameOrBootloaderReference
  $:$
  var interruptDefinitionList =@pic18InterruptDefinitionList ()
  var routineDefinitionList =@pic18RoutineDefinitionList ()
  var @pic18MacroDefinitionList macroDefinitionList = @()
  var unusedRoutineList =@lstringlist ()
  var ramDefinitionList =@ramDefinitionList ()
  var unusedRegisterList =@lstringlist ()
  var configDefinitionList =@configDefinitionList ()
  var constantDefinitionList =@constantDefinitionList ()
  var dataList = @dataList ()
  var checkpicList =@checkpicList ()
  var inlinedRoutineList =@lstringlist ()
  var @bool needsComputedGoto2 = false
  var @bool needsComputedGoto4 = false
  <body>
    !?interruptDefinitionList
    !?routineDefinitionList
    !?macroDefinitionList
    !?unusedRoutineList
    !?inlinedRoutineList
    !?ramDefinitionList
    !?unusedRegisterList
    !?configDefinitionList
    !?constantDefinitionList
    !?checkpicList
    !?dataList
    !?needsComputedGoto2
    !?needsComputedGoto4
  $end$
#--- Semantic analysis
  if @uint. errorCount == 0 then
    let piccoloModel = @pic18AST {
      !programName
      !programKind
      !deviceNameOrBootloaderReference
      !configDefinitionList
      !ramDefinitionList
      !unusedRegisterList
      !checkpicList
      !dataList
      !interruptDefinitionList
      !constantDefinitionList
      !routineDefinitionList
      !macroDefinitionList
      !unusedRoutineList
      !inlinedRoutineList
      !needsComputedGoto2
      !needsComputedGoto4
      !@location.here
    }
    pic18_analyze (!piccoloModel !@string.stringWithSourceFilePath)
  else
    let @string hexDestinationFile = [@string. stringWithSourceFilePath stringByDeletingPathExtension] + ".hex"
    if [hexDestinationFile fileExists] then
      [@string deleteFile !hexDestinationFile]
    end
    let @string asmDestinationFile = [@string. stringWithSourceFilePath stringByDeletingPathExtension] + ".asm"
    if [asmDestinationFile fileExists] then
      [@string deleteFile !asmDestinationFile]
    end
  end
}
label importBootloaderSpecification
  !@piccoloDeviceModel outPiccoloDeviceModel
  !@string outDeviceName
  !@uint outAccessBankSplitOffset
  !@ramBankTable outRamBank
  !@registerTable outRegisterTable
  !@bootloaderReservedRAMmap outBootloaderReservedRAMmap
  !@routineDeclarationList outBootloaderRoutineDeclarationList
  !@routineDeclarationList outUserRoutineDeclarationList
  !@luint outBootloaderReservedROMsize {
  $pic18$
  $identifier$ ?let @lstring programName
  error programName: "this file does not define a bootloader specification"
   : outPiccoloDeviceModel, outAccessBankSplitOffset, outRamBank, outRegisterTable,
      outBootloaderReservedRAMmap, outDeviceName,
      outBootloaderRoutineDeclarationList,
      outUserRoutineDeclarationList,
      outBootloaderReservedROMsize
  select
  or
    $implements$
    $bootloader$
  or
    $uses$
    $bootloader$
  end
  $literal_string$ ? *
  $:$
   parse<body>
  $end$
}

#----------------------------------------------------*

}