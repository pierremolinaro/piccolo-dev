syntax pic18_syntax (piccolo_lexique) {

#----------------------------------------------------*

rule <configuration_definition>
  ?!@configDefinitionList ioConfigDefinitionList

rule <ram_definition>
  ?!@ramDefinitionList ioRamDefinitionList

rule <constant_definition>
  ?!@constantDefinitionList ioConstantDefinitionList

rule <immediate_expression>
  !@immediatExpression outExpression

rule <register_parsing>
  !@registerExpression outRegisterExpression

rule <optional_w_as_dest>
  !@bool out_W_isDestination

rule <bit_number_parsing>
  !@bitNumberExpression outBitNumber

#----------------------------------------------------*

rule <body>
  ?!@pic18InterruptDefinitionList interruptDefinitionList
  ?!@pic18RoutineDefinitionList routineDefinitionList
  ?!@pic18MacroDefinitionList ioMacroDefinitionList
  ?!@lstringlist unusedRoutineList
  ?!@lstringlist inlinedRoutineList
  ?!@ramDefinitionList ramDefinitionList
  ?!@lstringlist unusedRegisterList
  ?!@configDefinitionList configDefinitionList
  ?!@constantDefinitionList constantDefinitionList
  ?!@checkpicList ioCheckpicList
  ?!@dataList dataList
  ?!@bool needsComputedGoto2
  ?!@bool needsComputedGoto4 {
  repeat
  while
    $mark$
    $literal_string$ ?var @lstring s
    warning s : "obsolete construct: use a comment beginning by #!"
  while
    $unused$
    $byte$
    repeat
      $identifier$ ?var @lstring unusedRegister
      unusedRegisterList += !unusedRegister
    while 
      $,$
    end
  while
    $unused$
    $routine$
    repeat
      $identifier$ ?var @lstring unusedRoutine
      unusedRoutineList += !unusedRoutine
    while 
      $,$
    end
  while
    $inline$
    repeat
      $identifier$ ?var @lstring inlinedRoutine
      inlinedRoutineList += !inlinedRoutine
    while 
      $,$
    end
  while
    <configuration_definition> !?configDefinitionList
  while
    <ram_definition> !?ramDefinitionList
  while
    <constant_definition> !?constantDefinitionList
  while
    <interrupt_definition>
      !?interruptDefinitionList
      !?needsComputedGoto2
      !?needsComputedGoto4
  while
    <routine_definition>
      !?routineDefinitionList
      !?needsComputedGoto2
      !?needsComputedGoto4
  while
    <data_definition> !?dataList
  while
    <checkpic_definition> !?ioCheckpicList
  while
    $include$
    $literal_string$ ?var @lstring includedFilePath
    grammar pic18_include_grammar in includedFilePath
      !?interruptDefinitionList
      !?routineDefinitionList
      !?ioMacroDefinitionList
      !?unusedRoutineList
      !?inlinedRoutineList
      !?ramDefinitionList
      !?unusedRegisterList
      !?configDefinitionList
      !?constantDefinitionList
      !?ioCheckpicList
      !?dataList
      !?needsComputedGoto2
      !?needsComputedGoto4
  while
    $macro$
    $identifier$ ?let macroName
    $($
    @lstringlist constantNameList = {}
    repeat
      $identifier$ ?let constantName
      constantNameList += !constantName
    while
      $,$
    end
    $)$
    ${$
    <instruction_list>
      ?let @pic18InstructionList instructionList
      !?needsComputedGoto2
      !?needsComputedGoto4
    $}$
    ioMacroDefinitionList +=
      !macroName
      !constantNameList
      !instructionList
  end
}

#----------------------------------------------------*

rule <checkpic_definition>
  ?!@checkpicList ioList {
  $checkpic$
  let @location loc = @location.here
  var valueList =@lstringlist.emptyList{}
  repeat
    $literal_string$ ?var @lstring s
    valueList += !s
  while
    $,$
  end
  ioList += !loc !valueList
}
    
#----------------------------------------------------*

rule <data_definition>
  ?!@dataList ioDataList {
  $data$
  $identifier$ ?let @lstring dataName
  ${$
  var valueList =@immediatExpressionList.emptyList{}
  repeat
    @immediatExpression expression
    <immediate_expression> ?expression
    valueList += !expression !@location.here
  while
    $,$
  end
  $}$
  ioDataList += !dataName !valueList
}
    
#----------------------------------------------------*

rule <instruction_list>
  !@pic18InstructionList outInstructionList
  ?!@bool ioNeedsComputedGoto2
  ?!@bool ioNeedsComputedGoto4 {
  outInstructionList = @pic18InstructionList. emptyList
  repeat
  while
    <simple_instruction> ?let @pic18PiccoloSimpleInstruction instruction
    outInstructionList += !instruction
  while
    @pic18PiccoloInstruction instruction
    <structured_instruction>
      ?instruction
      !?ioNeedsComputedGoto2
      !?ioNeedsComputedGoto4
    outInstructionList += !instruction
  end
}

#----------------------------------------------------*

rule <interrupt_definition>
  ?!@pic18InterruptDefinitionList ioInterruptDefinitionList
  ?!@bool ioNeedsComputedGoto2 
  ?!@bool ioNeedsComputedGoto4 {
  $interrupt$
  $identifier$ ?let @lstring interruptName
  @bool isFast
  select
    isFast = false
  or
    $fast$
    isFast = true
  end
  ${$
  @pic18InstructionList instructionList
  <instruction_list>
    ?instructionList
    !?ioNeedsComputedGoto2
    !?ioNeedsComputedGoto4
  $}$
  ioInterruptDefinitionList += !interruptName !isFast !instructionList !@location.here
}

#----------------------------------------------------*

rule <routine_definition>
  ?!@pic18RoutineDefinitionList ioRoutineDefinitionList
  ?!@bool ioNeedsComputedGoto2 
  ?!@bool ioNeedsComputedGoto4 {
  @bool noreturn
  select
    noreturn = false
  or
    $noreturn$
    noreturn = true
  end
  $routine$
  $identifier$ ?let @lstring routineName indexing routineDefinition
  @luint requiredBank
  @luint returnedBank
  @bool preservesBank
  select
    requiredBank = @luint. new { !@uint. max !@location.here}
    returnedBank = @luint. new { !@uint. max !@location.here}
    preservesBank = false
  or
    $bank$
    $:$
    select
      $preserved$
      if noreturn then
        error @location.here: "a \"noreturn\" routine cannot accept \"bank:preserved\" setting"
      end
      preservesBank = true
      requiredBank = @luint. new { !@uint. max !@location.here}
      returnedBank = @luint. new { !@uint. max !@location.here}
    or
      $requires$
      $integer$ ? requiredBank
      preservesBank = false
      select
        returnedBank = @luint. new { !@uint. max !@location.here}
      or
        $ensures$
        $integer$ ? returnedBank
        if noreturn then
          error @location.here: "a \"noreturn\" routine cannot accept \"bank:returned\" setting"
        end
      end
    or
      $ensures$
      $integer$ ? returnedBank
      if noreturn then
        error @location.here: "a \"noreturn\" routine cannot accept \"bank:returned\" setting"
      end
      preservesBank = false
      requiredBank = @luint. new { !@uint. max !@location.here}
    end
  end
  ${$
  @pic18InstructionList instructionList
  <instruction_list>
    ?instructionList
    !?ioNeedsComputedGoto2
    !?ioNeedsComputedGoto4
  $}$
  ioRoutineDefinitionList += !routineName !requiredBank !returnedBank !preservesBank !noreturn !instructionList !@location.here
}

#----------------------------------------------------*

rule <condition_factor>
  !@pic18ConditionExpression outCondition {
  $!$
  @pic18ConditionExpression condition
  <condition_factor> ? condition
  outCondition = @pic18NegateCondition. new { !condition}
}

#----------------------------------------------------*

rule <condition_factor>
  !@pic18ConditionExpression outCondition {
  <condition_expression> ? outCondition
}

#----------------------------------------------------*

rule <condition_factor>
  !@pic18ConditionExpression outCondition {
  select
    @bool isIncrement
    select
      $incf$ isIncrement = true
    or
      $decf$ isIncrement = false
    end
    @registerExpression registerExpression
    <register_parsing> ? registerExpression
    @bool W_isDestination
    select
      W_isDestination = false
    or
      $,$ $w$ W_isDestination = true
    end
    $identifier$ ?let @lstring conditionString
    @string condition = [[conditionString string] uppercaseString]
    @bool branchIfZero
    if condition == "Z" then
      branchIfZero = true
    elsif condition == "NZ" then
      branchIfZero = false
    else
      error conditionString: "invalid '" + conditionString
      + "' condition; valid ones are: 'z' and 'nz'"
      : branchIfZero
    end
    outCondition = @pic18IncDecRegisterInCondition. new {
      !registerExpression
      !isIncrement
      !W_isDestination
      !branchIfZero}
  or
    @registerExpression registerExpression
    <register_parsing> ? registerExpression
    @pic18RegisterComparison comparison
    select
      $!=$ $w$ comparison = @pic18RegisterComparison. notEqual
    or
      $==$ $w$ comparison = @pic18RegisterComparison. equal
    or
      $<=$ $w$ comparison = @pic18RegisterComparison. lowerOrEqual
    or
      $>=$ $w$ comparison = @pic18RegisterComparison. greaterOrEqual
    or
      $<$ $w$ comparison = @pic18RegisterComparison. lower
    or
      $>$ $w$ comparison = @pic18RegisterComparison. greater
    end
    outCondition = @pic18RegisterComparisonCondition. new {
      !registerExpression
      !comparison}
  or
    @registerExpression registerExpression
    <register_parsing> ? registerExpression
    $.$
    @bitNumberExpression bitNumberExpression
    <bit_number_parsing> ?bitNumberExpression
    outCondition = @pic18BitTestInStructuredCondition. new {
      !registerExpression
      !bitNumberExpression
    }
  or
    @registerExpression registerName
    <register_parsing> ?registerName
    $identifier$ ?let @lstring conditionString
    @string condition = [[conditionString string] uppercaseString]
    @bool branchIfZero
    if condition == "Z" then
      branchIfZero = true
    elsif condition == "NZ" then
      branchIfZero = false
    else
      error conditionString: "invalid '" + conditionString
      + "' condition; valid ones are: 'z' and 'nz'"
      : branchIfZero
    end
    outCondition = @pic18RegisterTestCondition. new { !registerName !branchIfZero}
  or
    @conditional_branch conditional_branch
    $identifier$ ?let @lstring conditionString
    @string condition = [[conditionString string] uppercaseString]
    if condition == "Z" then
      conditional_branch = @conditional_branch. bz
    elsif condition == "NZ" then
      conditional_branch = @conditional_branch. bnz
    elsif condition == "NN" then
      conditional_branch = @conditional_branch. bnn
    elsif condition == "N" then
      conditional_branch = @conditional_branch. bn
    elsif condition == "C" then
      conditional_branch = @conditional_branch. bc
    elsif condition == "NC" then
      conditional_branch = @conditional_branch. bnc
    elsif condition == "OV" then
      conditional_branch = @conditional_branch. bov
    elsif condition == "NOV" then
      conditional_branch = @conditional_branch. bnov
    else
      error conditionString: "invalid '" + conditionString
      + "' condition; valid ones are: 'z', 'nz', 'n', 'nn', 'c', 'nc', 'ov' and 'nov'"
      : conditional_branch
    end
    outCondition = @pic18BccInStructuredCondition. new { !conditionString.location !conditional_branch}
  end
}

#----------------------------------------------------*

rule <condition_expression>
  !@pic18ConditionExpression outCondition {
  $($
  <condition_term> ? outCondition
  repeat
  while
    $|$
    @pic18ConditionExpression c
    <condition_term> ? c
    @pic18ConditionExpression rc = @pic18NegateCondition. new { !c}
    @pic18ConditionExpression lc = @pic18NegateCondition. new { !outCondition}
    outCondition = @pic18NegateCondition. new { !@pic18AndCondition. new { !lc !rc}}
  end
  $)$
}

#----------------------------------------------------*

rule <condition_term>
  !@pic18ConditionExpression outCondition {
  <condition_factor> ? outCondition
  repeat
  while
    $&$
    <condition_factor> ?let @pic18ConditionExpression c
    outCondition = @pic18AndCondition. new { !outCondition !c}
  end
}

#----------------------------------------------------*

rule <block_termination>
  !@abstractBlockTerminationForBlockInstruction outBlockTermination {
  $-$
  outBlockTermination = @exitBlockTerminationForBlockInstruction. new { !@location.here}
}

#----------------------------------------------------*

rule <block_termination>
  !@abstractBlockTerminationForBlockInstruction outBlockTermination {
  $identifier$ ?var @lstring nextBlockName
  outBlockTermination = @gotoTerminationForBlockInstruction. new { !nextBlockName}
}

#----------------------------------------------------*

rule <block_termination>
  !@abstractBlockTerminationForBlockInstruction outBlockTermination {
  let @location location = @location.here
   <condition_expression> ?let @pic18ConditionExpression condition
   $?$
   <block_termination> ?let @abstractBlockTerminationForBlockInstruction terminationIfTrue
   $:$
   <block_termination> ?let @abstractBlockTerminationForBlockInstruction terminationIfFalse
   outBlockTermination = @testTerminationForBlockInstruction. new { !condition !terminationIfTrue !terminationIfFalse !location}
}

#----------------------------------------------------*

rule <structured_instruction>
  !@pic18PiccoloInstruction outInstruction
  ?!@bool ioNeedsComputedGoto2
  ?!@bool ioNeedsComputedGoto4
{
  select
    $switch$
    let @location instructionLocation = @location.here
    $w$
    var caseList =@pic18SwitchInstructionCaseList.emptyList{}
    repeat
      $case$
      let @location caseLocation = @location.here
      var caseExpressionList =@pic18CaseExpressionList.emptyList{}
      repeat
        select
          <immediate_expression> ?let @immediatExpression caseExpression
          caseExpressionList += !@pic18SimpleConstantCaseItem. new { !caseExpression !@location.here}
        or
          $[$
          <immediate_expression> ?let @immediatExpression minExpression
          $,$
          <immediate_expression> ?let @immediatExpression maxExpression
          $]$
          caseExpressionList += !@pic18IntervalCaseItem. new { !minExpression !maxExpression !@location.here}
        end
      while
        $,$
      end
      $:$
      <instruction_list> ?let @pic18InstructionList instructionList !?ioNeedsComputedGoto2 !?ioNeedsComputedGoto4
      caseList += !caseLocation !caseExpressionList !instructionList
    while
    end
    @pic18InstructionList elseInstructionList
    @location elseLocation
    select
      elseLocation = @location.here
      elseInstructionList = @pic18InstructionList. emptyList
    or
      $else$
      elseLocation = @location.here
      <instruction_list> ?elseInstructionList !?ioNeedsComputedGoto2 !?ioNeedsComputedGoto4
    end
    outInstruction = @pic18Instruction_switch. new {
      !instructionLocation
      !caseList
      !elseLocation
      !elseInstructionList}
    $end$
  or
    $block$
    let @location instructionLocation = @location.here
    $identifier$ ?var @lstring startBlockName
    $:$
    var blockList =@pic18BlockInstructionBlockList.emptyList{}
    repeat
      $identifier$ ?var @lstring blockName
      ${$
      <instruction_list> ?let @pic18InstructionList instructionList !?ioNeedsComputedGoto2 !?ioNeedsComputedGoto4
      $}$
      let @location endOfBlock = @location.here
      <block_termination> ?var @abstractBlockTerminationForBlockInstruction blockTermination
      blockList += !blockName !instructionList !blockTermination !endOfBlock
    while
    end    
    outInstruction = @pic18Instruction_block. new {
      !instructionLocation
      !startBlockName
      !blockList
      !@location.here}
    $end$
  or
    $do$
    let @location instructionLocation = @location.here
    $($
    <immediate_expression> ?let @immediatExpression repeatExpression
    $)$
    <instruction_list> ?let @pic18InstructionList instructionList !?ioNeedsComputedGoto2 !?ioNeedsComputedGoto4
    outInstruction = @pic18Instruction_repetitionStatique. new {
      !instructionLocation
      !repeatExpression
      !instructionList
      !@location.here}
    $end$
  or
    $checkbank$
    $integer$ ?var  @luint bankIndex
    outInstruction = @pic18Instruction_checkbank. new { !bankIndex.location !bankIndex.uint}
  or
    $checknobank$
    outInstruction = @pic18Instruction_checknobank. new { !@location.here}
  or
    $ldataptr$
    let @location instructionLocation = @location.here
    $identifier$ ?var @lstring dataName
    $[$
    $integer$ ?var  @luint dataIndex
    $]$
    outInstruction = @pic18Instruction_LDATAPTR. new { !instructionLocation !dataName !dataIndex}
  or
    $ltblptr$
    let @location instructionLocation = @location.here
    <immediate_expression> ?let @immediatExpression expression
    outInstruction = @pic18Instruction_LTBLPTR. new { !instructionLocation !expression}
  or
    $banksel$
    let @location instructionLocation = @location.here
    $integer$ ?var  @luint bank
    @bool warningOnUselessBanksel
    select
      warningOnUselessBanksel = true
    or
      $,$
      $*$
      warningOnUselessBanksel = false
    end
    outInstruction = @pic18Instruction_banksel. new { ! instructionLocation !bank !warningOnUselessBanksel}
  or
    $banksel$
    let @location instructionLocation = @location.here
    <register_parsing> ?var @registerExpression register
    @bool warningOnUselessBanksel
    select
      warningOnUselessBanksel = true
    or
      $,$
      $*$
      warningOnUselessBanksel = false
    end
    outInstruction = @pic18Instruction_banksel_register. new { !instructionLocation !register !warningOnUselessBanksel}
  or
    $nobank$
    outInstruction = @pic18Instruction_nobanksel. new { !@location.here}
  or
    $banksave$
    let @location instructionLocation = @location.here
    <register_parsing> ?var @registerExpression register
    <instruction_list> ?let @pic18InstructionList instructionList !?ioNeedsComputedGoto2 !?ioNeedsComputedGoto4
    $end$
    outInstruction = @pic18Instruction_savebank. new {
      !instructionLocation
      !register
      !instructionList
      !@location.here}
  or
    $computed$
    let @location instructionLocation = @location.here
    $[$
    @immediatExpression sizeExpression
    <immediate_expression> ?sizeExpression
    $]$
    select
      $retlw$
      var argumentList =@immediatExpressionList.emptyList{}
      repeat
        @immediatExpression expression
        <immediate_expression> ?expression
        argumentList += !expression !@location.here
      while
        $,$
      end
      outInstruction = @pic18Instruction_computed_retlw. new {
        !instructionLocation
        !sizeExpression
        !argumentList
        !true}
      ioNeedsComputedGoto2 = true
    or
      $bra$
      var argumentList =@lstringlist.emptyList{}
      repeat
        $identifier$ ?let @lstring targetRoutine indexing routineCall
        argumentList += ! targetRoutine
      while
        $,$
     end
       outInstruction = @pic18Instruction_computed_bra. new {
        !instructionLocation
        !sizeExpression
        !argumentList
        !true}
      ioNeedsComputedGoto2 = true
    or
      $goto$
      var argumentList =@lstringlist.emptyList{}
      repeat
        $identifier$ ?let @lstring targetRoutine indexing routineCall
        argumentList += ! targetRoutine
      while
        $,$
      end
      outInstruction = @pic18Instruction_computed_goto. new {
        !instructionLocation
        !sizeExpression
        !argumentList
        !true}
      ioNeedsComputedGoto4 = true
    or
      $rcall$
      var argumentList =@lstringlist.emptyList{}
      repeat
        $identifier$ ?let @lstring targetRoutine indexing routineCall
        argumentList += ! targetRoutine
      while
        $,$
      end
      outInstruction = @pic18Instruction_computed_rcall. new {
        !instructionLocation
        !sizeExpression
        !argumentList
        !true}
      ioNeedsComputedGoto4 = true
    end
  or
    $forever$
    let @location instructionLocation = @location.here
    @pic18InstructionList instructionList
    <instruction_list> ?instructionList !?ioNeedsComputedGoto2 !?ioNeedsComputedGoto4
    $end$
    outInstruction = @pic18Instruction_FOREVER. new {
      !instructionLocation
      !instructionList
      !@location.here}
  or
    $if$
    let @location instructionLocation = @location.here
    @registerExpression registerName
    <register_parsing> ?registerName
    @if_semi_colon_op opCode
    select
      $!=$ $w$ opCode = @if_semi_colon_op. CPFSEQ
    or
      $<=$ $w$ opCode = @if_semi_colon_op. CPFSGT
    or
      $>=$ $w$ opCode = @if_semi_colon_op. CPFSLT
    or
      $identifier$ ?let @lstring conditionString
      @string condition = [[conditionString string] uppercaseString]
      if condition != "NZ" then
        error conditionString:"invalid '" + condition + "' condition; it should be 'nz'"
      end
      opCode = @if_semi_colon_op. TSTFSZ
    end
    $:$
    <simple_instruction> ?let @pic18PiccoloSimpleInstruction instruction
    outInstruction = @pic18Instruction_IF_FA_SEMI_COLON. new {
      !instructionLocation
      !instruction
      !opCode
      !registerName}
  or
    $if$
    let @location instructionLocation = @location.here
    @registerExpression registerName
    @bitNumberExpression bitNumber
    @bool skipIfSet
    select
      $!$
      <register_parsing> ?registerName
      $.$
      <bit_number_parsing> ?bitNumber
      skipIfSet = true
    or
      <register_parsing> ?registerName
      $.$
      <bit_number_parsing> ?bitNumber
      skipIfSet = false
    end
    $:$
    <simple_instruction> ?let @pic18PiccoloSimpleInstruction instruction
    outInstruction = @pic18Instruction_IF_BitTest. new {
      !instructionLocation
      !instruction
      !skipIfSet
      !registerName
      !bitNumber
    }
  or
    $if$
    let @location instructionLocation = @location.here
    @bool increment
    select
      $decf$ increment = false
    or
      $incf$ increment = true
    end
    @registerExpression registerName
    <register_parsing> ?registerName
    @bool w_isTarget
    select
      w_isTarget = false
    or
      $,$ $w$ w_isTarget = true
    end
    @bool skipIfZero
    $identifier$ ?let @lstring conditionString
    @string condition = [[conditionString string] uppercaseString]
    if condition == "Z" then
      skipIfZero = false
    elsif condition == "NZ" then
      skipIfZero = true
    else
      error conditionString:"invalid '" + condition + "' condition; it should be 'z' or 'nz'" : skipIfZero
    end
    $:$
    <simple_instruction> ?let @pic18PiccoloSimpleInstruction instruction
    outInstruction = @pic18Instruction_IF_IncDec. new {
      !instructionLocation
      !instruction
      !increment
      !skipIfZero
      !registerName
      !w_isTarget}
  or
    $if$
    <structured_if_instruction>
      ?outInstruction
      !?ioNeedsComputedGoto2
      !?ioNeedsComputedGoto4
    $end$
  or
    $do$
    let @location instructionLocation = @location.here
    @pic18InstructionList repeatedInstructionList
    <instruction_list>
      ?repeatedInstructionList
      !?ioNeedsComputedGoto2
      !?ioNeedsComputedGoto4
    @location endOfRepeatedInstructionList = @location.here
    var whilePartList =@pic18DoWhilePartList.emptyList{}
    repeat
      $while$
      @pic18ConditionExpression whileCondition
      <condition_expression> ?whileCondition
      @pic18InstructionList instructionList
      <instruction_list>
        ?instructionList
        !?ioNeedsComputedGoto2
        !?ioNeedsComputedGoto4
      whilePartList += !whileCondition !instructionList !@location.here
    while
    end
    $end$
    outInstruction = @pic18Instruction_do_while. new {
      !instructionLocation
      !repeatedInstructionList
      !endOfRepeatedInstructionList
      !whilePartList}
  or
    $mnop$
    let @location instructionLocation = @location.here
    $integer$ ?var  @luint occurrenceFactor
    outInstruction = @pic18Instruction_MNOP. new { !instructionLocation !occurrenceFactor}
  or
    @conditional_branch condition
    select
      $bc$ condition = @conditional_branch. bc
    or
      $bnc$ condition = @conditional_branch. bnc
    or
      $bn$ condition = @conditional_branch. bn
    or
      $bnn$ condition = @conditional_branch. bnn
    or
      $bov$ condition = @conditional_branch. bov
    or
      $bnov$ condition = @conditional_branch. bnov
    or
      $bz$ condition = @conditional_branch. bz
    or
      $bnz$ condition = @conditional_branch. bnz
    end
    let @location instructionLocation = @location.here
    $identifier$ ?let @lstring targetLabelName indexing routineCall
    outInstruction = @pic18Instruction_JUMPCC. new {
      !instructionLocation
      !targetLabelName
      !condition
      !true}
  or
    $jump$
    let @location instructionLocation = @location.here
    @conditional_branch conditional_branch
    $identifier$ ?var @lstring conditionString
    @string condition = [[conditionString string] uppercaseString]
    if condition == "Z" then
      conditional_branch = @conditional_branch. bz
    elsif condition == "NZ" then
      conditional_branch = @conditional_branch. bnz
    elsif condition == "NN" then
      conditional_branch = @conditional_branch. bnn
    elsif condition == "N" then
      conditional_branch = @conditional_branch. bn
    elsif condition == "C" then
      conditional_branch = @conditional_branch. bc
    elsif condition == "NC" then
      conditional_branch = @conditional_branch. bnc
    elsif condition == "OV" then
      conditional_branch = @conditional_branch. bov
    elsif condition == "NOV" then
      conditional_branch = @conditional_branch. bnov
    else
      error conditionString: "invalid '" + conditionString
      + "' condition; valid ones are: 'z', 'nz', 'n', 'nn', 'c', 'nc', 'ov' and 'nov'"
      : conditional_branch
    end
    $identifier$ ?let @lstring targetLabelName indexing routineCall
    outInstruction = @pic18Instruction_JUMPCC. new {
      !instructionLocation
      !targetLabelName
      !conditional_branch
      !false}
  or
    $jump$
    let @location instructionLocation = @location.here
    $identifier$ ?let @lstring targetLabelName indexing routineCall
    outInstruction = @pic18Instruction_JUMP. new {
      !instructionLocation
      !targetLabelName
      !@jumpInstructionKind. ipicRelative}
  or
    $goto$
    let @location instructionLocation = @location.here
    $identifier$ ?let @lstring targetLabelName indexing routineCall
    outInstruction = @pic18Instruction_JUMP. new {
      !instructionLocation
      !targetLabelName
      !@jumpInstructionKind. absolute}
  or
    $bra$
    let instructionLocation = @location.here
    $identifier$ ?let @lstring targetLabelName indexing routineCall
    outInstruction = @pic18Instruction_JUMP. new {
      !instructionLocation
      !targetLabelName
      !@jumpInstructionKind.relative
    }
  or
    $macro$
    $identifier$ ?let macroName
    $($
    @immediatExpressionList immediatExpressionList = {}
    repeat
      <immediate_expression> ?let @immediatExpression constantExpression
      immediatExpressionList += !constantExpression !.here
    while
      $,$
    end
    $)$
    outInstruction = @pic18Instruction_macro.new {
      !macroName.location
      !macroName
      !immediatExpressionList
    }
  end
}

#----------------------------------------------------*

rule <structured_if_instruction>
  !@pic18PiccoloInstruction outInstruction
  ?!@bool ioNeedsComputedGoto2
  ?!@bool ioNeedsComputedGoto4 {
  @location instructionLocation = @location.here
  @pic18ConditionExpression structured_if_condition
  <condition_expression> ?structured_if_condition
  @pic18InstructionList thenInstructionList
  <instruction_list>
    ?thenInstructionList
    !?ioNeedsComputedGoto2
    !?ioNeedsComputedGoto4
  @pic18InstructionList elseInstructionList
  select
    elseInstructionList = @pic18InstructionList. emptyList
  or
    $elsif$
    @pic18PiccoloInstruction elsifPartInstruction
    <structured_if_instruction>
      ?elsifPartInstruction
      !?ioNeedsComputedGoto2
      !?ioNeedsComputedGoto4
    elseInstructionList = @pic18InstructionList. emptyList
    elseInstructionList += !elsifPartInstruction
  or
    $else$
    <instruction_list>
      ?elseInstructionList
      !?ioNeedsComputedGoto2
      !?ioNeedsComputedGoto4
  end
  @location endOfElsePartLocation = @location.here
  outInstruction = @pic18Instruction_structured_if. new {
    !instructionLocation
    !structured_if_condition
    !thenInstructionList
    !elseInstructionList
    !endOfElsePartLocation}
}

#----------------------------------------------------*

rule <simple_instruction> !@pic18PiccoloSimpleInstruction outInstruction {
  select
    @pic18Instruction_FDA_base_code_AST baseCode
    select
      $addwf$
      baseCode = @pic18Instruction_FDA_base_code_AST. ADDWF
    or
      $addwfc$
      baseCode = @pic18Instruction_FDA_base_code_AST. ADDWFC
    or
      $andwf$
      baseCode = @pic18Instruction_FDA_base_code_AST. ANDWF
    or
      $comf$
      baseCode = @pic18Instruction_FDA_base_code_AST. COMF
    or
      $decf$
      baseCode = @pic18Instruction_FDA_base_code_AST. DECF
    or
      $incf$
      baseCode = @pic18Instruction_FDA_base_code_AST. INCF
    or
      $iorwf$
      baseCode = @pic18Instruction_FDA_base_code_AST. IORWF
    or
      $movf$
      baseCode = @pic18Instruction_FDA_base_code_AST. MOVF
    or
      $rlcf$
      baseCode = @pic18Instruction_FDA_base_code_AST. RLCF
    or
      $rlncf$
      baseCode = @pic18Instruction_FDA_base_code_AST. RLNCF
    or
      $rrcf$
      baseCode = @pic18Instruction_FDA_base_code_AST. RRCF
    or
      $rrncf$
      baseCode = @pic18Instruction_FDA_base_code_AST. RRNCF
    or
      $subfwb$
      baseCode = @pic18Instruction_FDA_base_code_AST. SUBFWB
    or
      $subwf$
      baseCode = @pic18Instruction_FDA_base_code_AST. SUBWF
    or
      $subwfb$
      baseCode = @pic18Instruction_FDA_base_code_AST. SUBWFB
    or
      $swapf$
      baseCode = @pic18Instruction_FDA_base_code_AST. SWAPF
    or
      $xorwf$
      baseCode = @pic18Instruction_FDA_base_code_AST. XORWF
    end
    let @location instructionLocation = @location.here
    @registerExpression registerName
    <register_parsing> ?registerName
    @bool W_isDestination
    <optional_w_as_dest> ?W_isDestination
    outInstruction = @pic18Instruction_FDA. new {
      !instructionLocation
      !baseCode
      !registerName
      !W_isDestination}
  or
    @FA_instruction_base_code FAinstruction
    select
      $clrf$
      FAinstruction = @FA_instruction_base_code. CLRF
    or
      $movwf$
      FAinstruction = @FA_instruction_base_code. MOVWF
    or
      $mulwf$
      FAinstruction = @FA_instruction_base_code. MULWF
    or
      $negf$
      FAinstruction = @FA_instruction_base_code. NEGF
    or
      $setf$
      FAinstruction = @FA_instruction_base_code. SETF
    end
    let @location instructionLocation = @location.here
    @registerExpression registerName
    <register_parsing> ?registerName
    outInstruction = @pic18Instruction_FA. new {
      !instructionLocation
      !FAinstruction
      !registerName}
  or
    $movff$
    let @location instructionLocation = @location.here
    @registerExpression sourceRegisterName
    <register_parsing> ?sourceRegisterName
    $,$
    @registerExpression destinationRegisterName
    <register_parsing> ? destinationRegisterName
    outInstruction = @pic18Instruction_MOVFF. new {
      !instructionLocation
      !sourceRegisterName
      !destinationRegisterName}
  or
    @bit_oriented_op bitOrientedOp
    select
      $bcf$ bitOrientedOp = @bit_oriented_op. BCF
    or
      $bsf$ bitOrientedOp = @bit_oriented_op. BSF
    or
      $btg$ bitOrientedOp = @bit_oriented_op. BTG
    end
    let @location instructionLocation = @location.here
    <register_parsing> ?let @registerExpression registerName
    $.$
    <bit_number_parsing> ?let @bitNumberExpression bitNumber
    outInstruction = @pic18Instruction_FBA. new {
      !instructionLocation
      !bitOrientedOp
      !registerName
      !bitNumber}
  or
    $jsr$
    let @location instructionLocation = @location.here
    $identifier$ ?let @lstring targetLabelName indexing routineCall
    outInstruction = @pic18Instruction_JSR. new {
      !instructionLocation
      !targetLabelName
      !@jumpInstructionKind. ipicRelative}
  or
    $call$
    let @location instructionLocation = @location.here
    $identifier$ ?let @lstring targetLabelName indexing routineCall
    outInstruction = @pic18Instruction_JSR. new {
      !instructionLocation
      !targetLabelName
      !@jumpInstructionKind. absolute}
  or
    $rcall$
    let @location instructionLocation = @location.here
    $identifier$ ?let @lstring targetLabelName indexing routineCall
    outInstruction = @pic18Instruction_JSR. new {
      !instructionLocation
      !targetLabelName
      !@jumpInstructionKind. relative}
  or
    $clrwdt$
    outInstruction = @pic18Instruction_withNoOperand. new { !@location.here !@pic18InstructionWithNoOperandKind. CLRWDT}
  or
    $daw$
    outInstruction = @pic18Instruction_withNoOperand. new { !@location.here !@pic18InstructionWithNoOperandKind. DAW}
  or
    $nop$
    outInstruction = @pic18Instruction_withNoOperand. new { !@location.here !@pic18InstructionWithNoOperandKind. NOP}
  or
    $pop$
    outInstruction = @pic18Instruction_withNoOperand. new { !@location.here !@pic18InstructionWithNoOperandKind. POP}
  or
    $push$
    outInstruction = @pic18Instruction_withNoOperand. new { !@location.here !@pic18InstructionWithNoOperandKind. PUSH}
  or
    $reset$
    outInstruction = @pic18Instruction_withNoOperand. new { !@location.here !@pic18InstructionWithNoOperandKind. RESET}
  or
    $sleep$
    outInstruction = @pic18Instruction_withNoOperand. new { !@location.here !@pic18InstructionWithNoOperandKind. SLEEP}
  or
    $fnop$
    let @location instructionLocation = @location.here
    <immediate_expression> ?let @immediatExpression expression
    outInstruction = @pic18Instruction_fnop. new {
      !instructionLocation
      !expression}
  or
    @literal_instruction_opcode literalInstruction
    select
      $addlw$ literalInstruction = @literal_instruction_opcode. ADDLW
    or
      $andlw$ literalInstruction = @literal_instruction_opcode. ANDLW
    or
      $iorlw$ literalInstruction = @literal_instruction_opcode. IORLW
    or
      $movlw$ literalInstruction = @literal_instruction_opcode. MOVLW
    or
      $mullw$ literalInstruction = @literal_instruction_opcode. MULLW
    or
      $sublw$ literalInstruction = @literal_instruction_opcode. SUBLW
    or
      $xorlw$ literalInstruction = @literal_instruction_opcode. XORLW
    end
    let @location instructionLocation = @location.here
    <immediate_expression> ?let @immediatExpression expression
    outInstruction = @pic18Instruction_literalOperation. new {
      !instructionLocation
      !literalInstruction
      !expression}
  or
    $lfsr$
    let @location instructionLocation = @location.here
    $integer$ ?var  @luint FSRindex
    $,$
    @immediatExpression expression
    <immediate_expression> ?expression
    outInstruction = @pic18Instruction_LFSR. new { !instructionLocation !FSRindex !expression}
  or
    $tblrd$
    let @location instructionLocation = @location.here
    @tableAccessOption accessOption
    select
      $*$
      accessOption = @tableAccessOption. simpleAccess
    or
      $*+$
      accessOption = @tableAccessOption. postIncrement
    or
      $*-$
      accessOption = @tableAccessOption. postDecrement
    or
      $+*$
      accessOption = @tableAccessOption. preIncrement
    end
    outInstruction = @pic18Instruction_TBLRD. new { !instructionLocation !accessOption}
  or
    $tblwt$
    let @location instructionLocation = @location.here
    @tableAccessOption accessOption
    select
      $*$
      accessOption = @tableAccessOption. simpleAccess
    or
      $*+$
      accessOption = @tableAccessOption. postIncrement
    or
      $*-$
      accessOption = @tableAccessOption. postDecrement
    or
      $+*$
      accessOption = @tableAccessOption. preIncrement
    end
    outInstruction = @pic18Instruction_TBLWT. new { !instructionLocation !accessOption}
  end
}

#----------------------------------------------------*

}