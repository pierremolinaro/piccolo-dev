syntax piccoloDevice_syntax (piccoloDevice_lexique) {

#----------------------------------------------------*

rule <start_symbol>
  !@piccoloDeviceModel outPiccoloModel

#----------------------------------------------------*

rule <start_symbol> !@piccoloDeviceModel outPiccoloDeviceModel {
  $controller$
  $identifier$ ?let @lstring deviceName
  ${$
  $processor$
  let @processorType processorType
  $string$ ?let @lstring processorName
  if processorName.string == "pic18_60" then
    processorType = @processorType. pic18_60
  elsif processorName.string == "pic18_80" then
    processorType = @processorType. pic18_80
  elsif processorName.string == "mid-range" then
    processorType = @processorType. midrange
  elsif processorName.string == "baseline" then
    processorType = @processorType. baseline
  else
    error @location.here : "The '" + processorName + "' processor is not handled" : processorType
  end
  $;$
  $romsize$
  $integer$ ?let @luint romSize
  $;$
  let @uint eepromSize
  let @uint eepromAddress
  select
    eepromSize = 0
    eepromAddress = 0
  or
    $eepromsize$
    $integer$ ?let @luint l_eepromSize
    eepromSize = l_eepromSize.uint
    $at$
    $integer$ ?let @luint l_eepromAddress
    eepromAddress = l_eepromAddress.uint
    $;$
  end
  $bank$
  $integer$ ?let @luint bankCount
  $;$
  repeat
  while
    $unusedregister$
    $integer$ ?* # let @luint unusedRegisterStart
    select
    or
     $to$
     $integer$ ?* # let @luint unusedRegisterEnd
    end
    $;$
  end
  var ramBankTable =@ramBankTable §[]
  repeat
  while
    $ram$
    $identifier$ ?let @lstring ramName
    $:$
    $integer$ ?let @luint ramStart
    $to$
    $integer$ ?let @luint ramEnd
    var mirrorOffsetList =@uintlist §[]
    select
    or
      $mirrorat$
      repeat
        $integer$ ?let @luint mirrorStartAddress
        mirrorOffsetList += !mirrorStartAddress.uint - ramStart.uint
      while
        $,$
      end
    end
    $;$
    [!?ramBankTable insertKey
      !ramName
      !ramStart.uint # Start
      !ramStart.uint # First free address (mutable)
      !ramEnd.uint + 1
      !mirrorOffsetList
    ]
  end
  var registerTable =@registerTable §[]
  repeat
  while
    $register$
    $identifier$ ?let @lstring registerName
    $at$
    var registerAddressList =@uintlist §[]
    repeat
      $integer$ ?let @luint registerAddress
      registerAddressList += !registerAddress.uint
    while
      $,$
    end
    $<$
    var @uint sliceIndex = 8
    var bitSliceTable =@bitSliceTable §[]
    var @string bitDefinitionString = " <"
    repeat
      select
        var @uint sliceWidth = 0 # Means undefined
        repeat
          var @uint sliceBase = sliceIndex
          $identifier$ ?let @lstring sliceName
          select
            if (sliceWidth != 0) & (sliceWidth != 1) then
              error sliceName:"a slice of 1 bit is expected for '" + sliceName + "'" 
            end
            sliceWidth = 1
            bitDefinitionString += sliceName.string 
          or
            $[$
            $integer$ ?let @luint sliceSize
            if (sliceWidth != 0) & (sliceWidth != sliceSize.uint) then
              error sliceName:"a slice of " + [sliceWidth string] + " bits is expected for '" + sliceName + "'" 
            end
            sliceWidth = sliceSize.uint
            sliceBase = sliceBase + 1 - sliceSize.uint
            $]$
            bitDefinitionString += sliceName.string + "[" + [sliceSize.uint string] + "]" 
          end
          [!?bitSliceTable insertKey !sliceName !sliceIndex-sliceWidth !sliceWidth]
        while
          $/$
          bitDefinitionString += "/"
        end
        sliceIndex = sliceIndex - sliceWidth
      or
        $-$
        bitDefinitionString += "-"
        sliceIndex -= 1
      end
    while
      $,$
      bitDefinitionString += ", " 
    end
    if sliceIndex != 0 then
      error registerName:"Incorrect bit definition for register '" + registerName + "'"
    end
    bitDefinitionString += ">"
    $>$
    [!?registerTable insertKey !registerName !registerAddressList !1 !bitSliceTable !bitDefinitionString !.publicRegister]
    $;$
  end
  var configRegisterMap =@configRegisterMap §[]
  repeat
  while
    $configuration$
    $identifier$ ?let @lstring configRegisterName
    $at$
    $integer$ ?let @luint configRegisterAddress
    $width$
    $integer$ ?let @luint configRegisterWidth
    var illegalMaskList =@illegalMaskList §[]
    repeat
    while
      $illegal$
      $integer$ ?let @luint illegalValue
      $mask$
      $integer$ ?let @luint illegalMask
      $message$
      $string$ ?let @lstring illegalDescriptionString
      illegalMaskList += !illegalValue !illegalMask !illegalDescriptionString
    end
    ${$
    var configRegisterMaskMap =@configRegisterMaskMap §[]
    var settingNameSet =@stringset §[]
    repeat
    while
      $identifier$ ?let @lstring maskName
      if [settingNameSet hasKey !maskName.string] then
        error maskName:"The '" + maskName + "' setting is already defined"
      end
      settingNameSet += !maskName.string
      $mask$
      $integer$ ?let @luint maskValue
      $description$
      $string$ ?let @lstring maskDescriptionString
      var fieldSettingMap =@fieldSettingMap §[]
      repeat
        $setting$
        $integer$ ?let @luint value
        $mask$
        $integer$ ?let @luint mask
        $description$
        $string$ ?let @lstring descriptionString
        [!?fieldSettingMap insertKey !descriptionString !value.uint !mask.uint]
      while
      end
      [!?configRegisterMaskMap insertKey !maskName !maskValue !maskDescriptionString !fieldSettingMap]
    end
    $}$
    [!?configRegisterMap insertKey
      !configRegisterName
      !configRegisterAddress
      !configRegisterWidth
      !configRegisterMaskMap
      !illegalMaskList
    ]
  end
  $}$
#--- Check some registers are actual defined
  if (processorName.string == "pic18_60") | (processorName.string == "pic18_80") then
    [registerTable searchKey !@lstring.init { !"BSR" !@location.here} ?5*]
    [registerTable searchKey !@lstring.init { !"TOSL" !@location.here} ?5*]
    [registerTable searchKey !@lstring.init { !"TOSH" !@location.here} ?5*]
    [registerTable searchKey !@lstring.init { !"TOSU" !@location.here} ?5*]
  end
#--- Check if controller has a shared bank
  var @string sharedBankName = "" # No shared bank by default
  if processorName.string == "mid-range" then
    for (lkey * mFirstFreeAddress * mMirrorOffsetList) in  ramBankTable while sharedBankName  == "" do
      var @uint bankAccessibility = 1 << (mFirstFreeAddress >> 7)
      for (mValue) in  mMirrorOffsetList do
        bankAccessibility = bankAccessibility  | (1 << ((mFirstFreeAddress + mValue) >> 7))
      end
      if (1 << bankCount.uint) == (bankAccessibility + 1) then
        sharedBankName = lkey.string
      end
    end
  end
#---
  outPiccoloDeviceModel = @piccoloDeviceModel.init {
    !deviceName
    !processorType
    !romSize
    !bankCount
    !registerTable
    !ramBankTable
    !eepromSize
    !eepromAddress
    !configRegisterMap
    !sharedBankName}
}

#----------------------------------------------------*

}