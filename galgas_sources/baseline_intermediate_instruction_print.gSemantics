semantics baseline_intermediate_instruction_print :
import "baseline_intermediate_classes.gSemantics" ;

#-------------------------------------------------------------------------------*

abstract method @baseline_intermediate_instruction print ?!@string ioListFileContents ;

#-------------------------------------------------------------------------------*

override method @baseline_intermediate_NULL print ?!@string ioListFileContents :
  ioListFileContents .= "-" ;
end method ;

#-------------------------------------------------------------------------------*

override method @baseline_intermediate_pseudo_PAGE print ?!@string ioListFileContents :
  ioListFileContents .= "ORG " . [mPage * 512 hexString] ;
end method ;

#-------------------------------------------------------------------------------*

override method @baseline_intermediate_pseudo_BEGIN_ROUTINE print ?!@string ioListFileContents :
  ioListFileContents .= "BEGIN OF ROUTINE " . mRoutineName ;
end method ;

#-------------------------------------------------------------------------------*

override method @baseline_intermediate_pseudo_END_ROUTINE print ?!@string ioListFileContents :
  ioListFileContents .= "END OF ROUTINE " . mRoutineName . " IN PAGE " . mPage ;
end method ;

#-------------------------------------------------------------------------------*

override method @baseline_intermediate_pseudo_LABEL print ?!@string ioListFileContents :
  ioListFileContents .= "LABEL " . mLabel ;
end method ;

#-------------------------------------------------------------------------------*

override method @baseline_intermediate_instruction_FD print ?!@string ioListFileContents :
  ioListFileContents .= [mInstruction mnemonic] . " " . [mRegisterDescription mAssemblyString] ;
  if m_W_isDestination then
    ioListFileContents .= ", W" ;
  end if ;
end method ;

#-------------------------------------------------------------------------------*

override method @baseline_intermediate_instruction_F print ?!@string ioListFileContents :
  ioListFileContents .= [mInstruction mnemonic] . " " . [mRegisterDescription mAssemblyString] ;
end method ;

#-------------------------------------------------------------------------------*

override method @baseline_intermediate_instruction_FB print ?!@string ioListFileContents :
  ioListFileContents .= [mInstruction mnemonic] . " " . [mRegisterDescription mAssemblyString] . ", " . [mBitNumber string] ;
end method ;

#-------------------------------------------------------------------------------*

override method @baseline_intermediate_instruction_BitTestSkip print ?!@string ioListFileContents :
  if mSkipIfSet then
    ioListFileContents .= "BTFSS" ;
  else
    ioListFileContents .= "BTFSC" ;
  end if ;
  ioListFileContents .= " " . [mRegisterDescription mAssemblyString] . ", " . [mBitNumber string] ;
end method ;

#-------------------------------------------------------------------------------*

override method @baseline_intermediate_GOTO print ?!@string ioListFileContents :
  ioListFileContents .= "GOTO " . mTargetLabel ;
end method ;

#-------------------------------------------------------------------------------*


override method @baseline_intermediate_CALL print ?!@string ioListFileContents :
  ioListFileContents .= "CALL " . mTargetLabel ;
end method ;

#-------------------------------------------------------------------------------*

override method @baseline_intermediate_instruction_literalOperation print ?!@string ioListFileContents :
  ioListFileContents .= [mInstruction mnemonic] . " " . [mLiteralValue hexString] ;
end method ;

#-------------------------------------------------------------------------------*

override method @baseline_intermediate_instruction_MNOP print ?!@string ioListFileContents :
  ioListFileContents .= "MNOP " . [[mOccurrenceFactor uint] string] ;
end method ;

#-------------------------------------------------------------------------------*

override method @baseline_intermediate_incDecRegisterInCondition print ?!@string ioListFileContents :
  ioListFileContents .= "JUMP " ;
  if mIncrement then
    ioListFileContents .= "INCF " ;
  else
    ioListFileContents .= "DECF " ;
  end if ;
  ioListFileContents .= [mRegisterDescription mAssemblyString] ;
  if m_W_isDestination then
    ioListFileContents .= ", W" ;
  end if ;
  if mBranchIfZero then
    ioListFileContents .= " Z" ;
  else
    ioListFileContents .= " NZ" ;
  end if ;
  ioListFileContents .= " " . mTargetLabel ;
end method ;

#-------------------------------------------------------------------------------*

override method @baseline_intermediate_WO_OPERAND print ?!@string ioListFileContents :
  ioListFileContents .= [mInstruction mnemonic] ;
end method ;

#-------------------------------------------------------------------------------*

override method @baseline_intermediate_TRIS print ?!@string ioListFileContents :
  ioListFileContents .= "TRIS " . mOperand ;
end method ;

#-------------------------------------------------------------------------------*

end semantics ;
