semantics ipic18_optimize_block_ordering :
import "ipic18_block_representation.gSemantics" ;
import "pic18_mnemonics.gSemantics" ;
import "ipic18_display_block_list.gSemantics" ;

#----------------------------------------------------------------------------*

#!--- Block invocation graph

#----------------------------------------------------------------------------*

graph @blockInvocationGraph (@stringlist) {
  insert addNode error message "the '%K' label is already declared at %L" ;
}

#----------------------------------------------------------------------------*

#!--- Build ordered blocks

#----------------------------------------------------------------------------*

override method @ipic18ComputedGotoTerminator buildOrderedBlocks
  ?!@blockInvocationGraph unused ioGraph
  ?!@stringset unused ioVisitedBlockSet
  ??@ipic18BlockList unused inBlockList
  ??@symbolTableForBlockOptimization unused inSymbolTable
  ??@lstring unused inLabelBlock
:
end method ;

#----------------------------------------------------------------------------*

override method @ipic18ComputedRETLWTerminator buildOrderedBlocks
  ?!@blockInvocationGraph unused ioGraph
  ?!@stringset unused ioVisitedBlockSet
  ??@ipic18BlockList unused inBlockList
  ??@symbolTableForBlockOptimization unused inSymbolTable
  ??@lstring unused inLabelBlock
:
end method ;

#----------------------------------------------------------------------------*

override method @ipic18ComputedBraTerminator buildOrderedBlocks
  ?!@blockInvocationGraph unused ioGraph
  ?!@stringset unused ioVisitedBlockSet
  ??@ipic18BlockList unused inBlockList
  ??@symbolTableForBlockOptimization unused inSymbolTable
  ??@lstring unused inLabelBlock
:
end method ;

#----------------------------------------------------------------------------*

abstract method @ipic18AbstractBlockTerminator buildOrderedBlocks
  ?!@blockInvocationGraph ioGraph
  ?!@stringset ioVisitedBlockSet
  ??@ipic18BlockList inBlockList
  ??@symbolTableForBlockOptimization inSymbolTable
  ??@lstring inLabelBlock
;

#----------------------------------------------------------------------------*

override method @ipic18ReturnTerminator buildOrderedBlocks
  ?!@blockInvocationGraph unused ioGraph
  ?!@stringset unused ioVisitedBlockSet
  ??@ipic18BlockList unused inBlockList
  ??@symbolTableForBlockOptimization unused inSymbolTable
  ??@lstring unused inLabelBlock
:
end method ;

#----------------------------------------------------------------------------*

override method @ipic18RetlwTerminator buildOrderedBlocks
  ?!@blockInvocationGraph unused ioGraph
  ?!@stringset unused ioVisitedBlockSet
  ??@ipic18BlockList unused inBlockList
  ??@symbolTableForBlockOptimization unused inSymbolTable
  ??@lstring unused inLabelBlock
:
end method ;

#----------------------------------------------------------------------------*

override method @ipic18RetfieTerminator buildOrderedBlocks
  ?!@blockInvocationGraph unused ioGraph
  ?!@stringset unused ioVisitedBlockSet
  ??@ipic18BlockList unused inBlockList
  ??@symbolTableForBlockOptimization unused inSymbolTable
  ??@lstring unused inLabelBlock
:
end method ;

#----------------------------------------------------------------------------*

override method @ipic18JumpTerminator buildOrderedBlocks
  ?!@blockInvocationGraph ioGraph
  ?!@stringset ioVisitedBlockSet
  ??@ipic18BlockList inBlockList
  ??@symbolTableForBlockOptimization inSymbolTable
  ??@lstring inLabelBlock
:
  if not [ioVisitedBlockSet hasKey !mLabel->string] then
    [!?ioGraph addArc !inLabelBlock !mLabel] ;
    ioVisitedBlockSet += !mLabel->string ;
    [inSymbolTable searchKey !mLabel ??@uint targetBlockIndex] ;
    const @ipic18Block targetBlock := [inBlockList mBlockAtIndex !targetBlockIndex] ;
    [!?ioGraph addNode !targetBlock->mLabel !targetBlock->mLabel->string] ;
    [targetBlock->mTerminator buildOrderedBlocks
      !?ioGraph
      !?ioVisitedBlockSet
      !inBlockList
      !inSymbolTable
      !targetBlock->mLabel
    ] ;
  end if ;
end method ;

#----------------------------------------------------------------------------*

override method @ipic18ConditionalJumpTerminator buildOrderedBlocks
  ?!@blockInvocationGraph ioGraph
  ?!@stringset ioVisitedBlockSet
  ??@ipic18BlockList inBlockList
  ??@symbolTableForBlockOptimization inSymbolTable
  ??@lstring inLabelBlock
:
  if not [ioVisitedBlockSet hasKey !mTargetLabelWhenTrue->string] then
    [!?ioGraph addArc !inLabelBlock !mTargetLabelWhenTrue] ;
    ioVisitedBlockSet += !mTargetLabelWhenTrue->string ;
    [inSymbolTable searchKey !mTargetLabelWhenTrue ??@uint targetBlockIndex] ;
    const @ipic18Block targetBlock := [inBlockList mBlockAtIndex !targetBlockIndex] ;
    [!?ioGraph addNode !targetBlock->mLabel !targetBlock->mLabel->string] ;
    [targetBlock->mTerminator buildOrderedBlocks
      !?ioGraph
      !?ioVisitedBlockSet
      !inBlockList
      !inSymbolTable
      !targetBlock->mLabel
    ] ;
  end if ;
  if not [ioVisitedBlockSet hasKey !mTargetLabelWhenFalse->string] then
    [!?ioGraph addArc !inLabelBlock !mTargetLabelWhenFalse] ;
    ioVisitedBlockSet += !mTargetLabelWhenFalse->string ;
    [inSymbolTable searchKey !mTargetLabelWhenFalse ??@uint targetBlockIndex] ;
    const @ipic18Block targetBlock := [inBlockList mBlockAtIndex !targetBlockIndex] ;
    [!?ioGraph addNode !targetBlock->mLabel !targetBlock->mLabel->string] ;
    [targetBlock->mTerminator buildOrderedBlocks
      !?ioGraph
      !?ioVisitedBlockSet
      !inBlockList
      !inSymbolTable
      !targetBlock->mLabel
    ] ;
  end if ;
end method ;

#----------------------------------------------------------------------------*

override method @ipic18AbstractConditionTerminator buildOrderedBlocks
  ?!@blockInvocationGraph ioGraph
  ?!@stringset ioVisitedBlockSet
  ??@ipic18BlockList inBlockList
  ??@symbolTableForBlockOptimization inSymbolTable
  ??@lstring inLabelBlock
:
  [mInstructionIfConditionTrue buildOrderedBlocks
    !?ioGraph
    !?ioVisitedBlockSet
    !inBlockList
    !inSymbolTable
    !inLabelBlock
  ] ;
  [mInstructionIfConditionFalse buildOrderedBlocks
    !?ioGraph
    !?ioVisitedBlockSet
    !inBlockList
    !inSymbolTable
    !inLabelBlock
  ] ;
end method ;

#----------------------------------------------------------------------------*

override method @pic18RegisterComparisonTerminator buildOrderedBlocks
  ?!@blockInvocationGraph ioGraph
  ?!@stringset ioVisitedBlockSet
  ??@ipic18BlockList inBlockList
  ??@symbolTableForBlockOptimization inSymbolTable
  ??@lstring inLabelBlock
:
  [mInstructionIfConditionTrue buildOrderedBlocks
    !?ioGraph
    !?ioVisitedBlockSet
    !inBlockList
    !inSymbolTable
    !inLabelBlock
  ] ;
end method ;

#----------------------------------------------------------------------------*

override method @pic18TestRegisterTerminator buildOrderedBlocks
  ?!@blockInvocationGraph ioGraph
  ?!@stringset ioVisitedBlockSet
  ??@ipic18BlockList inBlockList
  ??@symbolTableForBlockOptimization inSymbolTable
  ??@lstring inLabelBlock
:
  [mInstructionIfConditionTrue buildOrderedBlocks
    !?ioGraph
    !?ioVisitedBlockSet
    !inBlockList
    !inSymbolTable
    !inLabelBlock
  ] ;
end method ;

#----------------------------------------------------------------------------*

#!--- buildInvocationGraph

#----------------------------------------------------------------------------*

abstract method @ipic18AbstractBlockTerminator buildInvocationGraph
  ??@lstring inBlockLabel
  ?!@string ioGraphVizString
  ??@bool inDottedArrow
;

#----------------------------------------------------------------------------*

override method @ipic18ReturnTerminator buildInvocationGraph
  ??@lstring unused inBlockLabel
  ?!@string unused ioGraphVizString
  ??@bool unused inDottedArrow
:
end method ;

#----------------------------------------------------------------------------*

override method @ipic18RetlwTerminator buildInvocationGraph
  ??@lstring unused inBlockLabel
  ?!@string unused ioGraphVizString
  ??@bool unused inDottedArrow
:
end method ;

#----------------------------------------------------------------------------*

override method @ipic18RetfieTerminator buildInvocationGraph
  ??@lstring unused inBlockLabel
  ?!@string unused ioGraphVizString
  ??@bool unused inDottedArrow
:
end method ;

#----------------------------------------------------------------------------*

override method @ipic18ComputedGotoTerminator buildInvocationGraph
  ??@lstring unused inBlockLabel
  ?!@string unused ioGraphVizString
  ??@bool unused inDottedArrow
:
end method ;

#----------------------------------------------------------------------------*

override method @ipic18ComputedRETLWTerminator buildInvocationGraph
  ??@lstring unused inBlockLabel
  ?!@string unused ioGraphVizString
  ??@bool unused inDottedArrow
:
end method ;

#----------------------------------------------------------------------------*

override method @ipic18ComputedBraTerminator buildInvocationGraph
  ??@lstring unused inBlockLabel
  ?!@string unused ioGraphVizString
  ??@bool unused inDottedArrow
:
end method ;

#----------------------------------------------------------------------------*

override method @ipic18AbstractConditionTerminator buildInvocationGraph
  ??@lstring inBlockLabel
  ?!@string ioGraphVizString
  ??@bool unused inDottedArrow
:
  [mInstructionIfConditionTrue buildInvocationGraph !inBlockLabel !?ioGraphVizString !false] ;
  [mInstructionIfConditionFalse buildInvocationGraph !inBlockLabel !?ioGraphVizString !false] ;
end method ;

#----------------------------------------------------------------------------*

override method @pic18RegisterComparisonTerminator buildInvocationGraph
  ??@lstring inBlockLabel
  ?!@string ioGraphVizString
  ??@bool unused inDottedArrow
:
  [mInstructionIfConditionTrue buildInvocationGraph !inBlockLabel !?ioGraphVizString !false] ;
  [mInstructionIfConditionFalse buildInvocationGraph !inBlockLabel !?ioGraphVizString !true] ;
end method ;

#----------------------------------------------------------------------------*

override method @pic18TestRegisterTerminator buildInvocationGraph
  ??@lstring inBlockLabel
  ?!@string ioGraphVizString
  ??@bool unused inDottedArrow
:
  [mInstructionIfConditionTrue buildInvocationGraph !inBlockLabel !?ioGraphVizString !false] ;
  [mInstructionIfConditionFalse buildInvocationGraph !inBlockLabel !?ioGraphVizString !true] ;
end method ;

#----------------------------------------------------------------------------*

override method @ipic18JumpTerminator buildInvocationGraph
  ??@lstring inBlockLabel
  ?!@string ioGraphVizString
  ??@bool inDottedArrow
:
  ioGraphVizString .= "  \"" . inBlockLabel . "\" -> \"" . mLabel . "\"" ;
  if inDottedArrow then
    ioGraphVizString .= " [style=dotted]" ;
  end if ;
  ioGraphVizString .= " ;\n" ;
end method ;

#----------------------------------------------------------------------------*

override method @ipic18ConditionalJumpTerminator buildInvocationGraph
  ??@lstring inBlockLabel
  ?!@string ioGraphVizString
  ??@bool inDottedArrow
:
  ioGraphVizString .= "  \"" . inBlockLabel . "\" -> \"" . mTargetLabelWhenTrue . "\"" ;
  if inDottedArrow then
    ioGraphVizString .= " [style=dotted]" ;
  end if ;
  ioGraphVizString .= " ;\n" ;
  ioGraphVizString .= "  \"" . inBlockLabel . "\" -> \"" . mTargetLabelWhenFalse . "\"" ;
  if inDottedArrow then
    ioGraphVizString .= " [style=dotted]" ;
  end if ;
  ioGraphVizString .= " ;\n" ;
end method ;

#----------------------------------------------------------------------------*

#!--- Block build ordering constraint

#----------------------------------------------------------------------------*

method @ipic18AbstractBlockTerminator buildOrderingConstraintGraph
  ??@ipic18BlockList unused inBlockList
  ??@symbolTableForBlockOptimization unused inSymbolTable
  ??@lstring unused inBlockLabel
  ??@lstring unused inDominatorLabel
  ?!@blockInvocationGraph unused ioGraph
  ?!@stringset unused ioVisitedBlockSet
:
end method ;

#----------------------------------------------------------------------------*

override method @ipic18JumpTerminator buildOrderingConstraintGraph
  ??@ipic18BlockList inBlockList
  ??@symbolTableForBlockOptimization inSymbolTable
  ??@lstring inBlockLabel
  ??@lstring inDominatorLabel
  ?!@blockInvocationGraph ioGraph
  ?!@stringset ioVisitedBlockSet
:
  if not [ioVisitedBlockSet hasKey !mLabel->string] then
    [!?ioGraph addArc !inBlockLabel !mLabel] ;
    if inDominatorLabel->string != "" then
      [!?ioGraph addArc !inDominatorLabel !mLabel] ;
    end if ;
  #--- Explore next block
    [!?ioGraph addNode !mLabel !mLabel->string] ;
    ioVisitedBlockSet += !mLabel->string ;
    [inSymbolTable searchKey !mLabel ??@uint blockIndex] ;
    const @ipic18Block nextBlock := [inBlockList mBlockAtIndex !blockIndex] ;
    [nextBlock->mTerminator buildOrderingConstraintGraph
      !inBlockList
      !inSymbolTable
      !mLabel
      !inDominatorLabel
      !?ioGraph
      !?ioVisitedBlockSet
    ] ;
  end if ;
end method ;

#----------------------------------------------------------------------------*

override method @ipic18ConditionalJumpTerminator buildOrderingConstraintGraph
  ??@ipic18BlockList inBlockList
  ??@symbolTableForBlockOptimization inSymbolTable
  ??@lstring inBlockLabel
  ??@lstring inDominatorLabel
  ?!@blockInvocationGraph ioGraph
  ?!@stringset ioVisitedBlockSet
:
  if not [ioVisitedBlockSet hasKey !mTargetLabelWhenTrue->string] then
    [!?ioGraph addArc !inBlockLabel !mTargetLabelWhenTrue] ;
    if inDominatorLabel->string != "" then
      [!?ioGraph addArc !inDominatorLabel !mTargetLabelWhenTrue] ;
    end if ;
  #--- Explore next block
    [!?ioGraph addNode !mTargetLabelWhenTrue !mTargetLabelWhenTrue->string] ;
    ioVisitedBlockSet += !mTargetLabelWhenTrue->string ;
    [inSymbolTable searchKey !mTargetLabelWhenTrue ??@uint blockIndex] ;
    const @ipic18Block nextBlock := [inBlockList mBlockAtIndex !blockIndex] ;
    [nextBlock->mTerminator buildOrderingConstraintGraph
      !inBlockList
      !inSymbolTable
      !mTargetLabelWhenTrue
      !["" nowhere]
      !?ioGraph
      !?ioVisitedBlockSet
    ] ;
  end if ;
  if not [ioVisitedBlockSet hasKey !mTargetLabelWhenFalse->string] then
    [!?ioGraph addArc !inBlockLabel !mTargetLabelWhenFalse] ;
    if inDominatorLabel->string != "" then
      [!?ioGraph addArc !inDominatorLabel !mTargetLabelWhenFalse] ;
    end if ;
  #--- Explore next block
    [!?ioGraph addNode !mTargetLabelWhenFalse !mTargetLabelWhenFalse->string] ;
    ioVisitedBlockSet += !mTargetLabelWhenFalse->string ;
    [inSymbolTable searchKey !mTargetLabelWhenFalse ??@uint blockIndex] ;
    const @ipic18Block nextBlock := [inBlockList mBlockAtIndex !blockIndex] ;
    [nextBlock->mTerminator buildOrderingConstraintGraph
      !inBlockList
      !inSymbolTable
      !mTargetLabelWhenFalse
      !["" nowhere]
      !?ioGraph
      !?ioVisitedBlockSet
    ] ;
  end if ;
end method ;

#----------------------------------------------------------------------------*

override method @ipic18AbstractConditionTerminator buildOrderingConstraintGraph
  ??@ipic18BlockList inBlockList
  ??@symbolTableForBlockOptimization inSymbolTable
  ??@lstring inBlockLabel
  ??@lstring unused inDominatorLabel
  ?!@blockInvocationGraph ioGraph
  ?!@stringset ioVisitedBlockSet
:
  [mInstructionIfConditionTrue buildOrderingConstraintGraph
    !inBlockList
    !inSymbolTable
    !inBlockLabel
    !["" nowhere]
    !?ioGraph
    !?ioVisitedBlockSet
  ] ;
  [mInstructionIfConditionFalse buildOrderingConstraintGraph
    !inBlockList
    !inSymbolTable
    !inBlockLabel
    !["" nowhere]
    !?ioGraph
    !?ioVisitedBlockSet
  ] ;
end method ;

#----------------------------------------------------------------------------*

override method @pic18RegisterComparisonTerminator buildOrderingConstraintGraph
  ??@ipic18BlockList inBlockList
  ??@symbolTableForBlockOptimization inSymbolTable
  ??@lstring inBlockLabel
  ??@lstring unused inDominatorLabel
  ?!@blockInvocationGraph ioGraph
  ?!@stringset ioVisitedBlockSet
:
#--- Only true jump can be ommitted
  [mInstructionIfConditionTrue buildOrderingConstraintGraph
    !inBlockList
    !inSymbolTable
    !inBlockLabel
    !["" nowhere]
    !?ioGraph
    !?ioVisitedBlockSet
  ] ;
#--- Weak precedence
  if (mInstructionIfConditionTrue is @ipic18JumpTerminator) then
    const @ipic18JumpTerminator jp := (cast mInstructionIfConditionTrue : @ipic18JumpTerminator) ;
    [mInstructionIfConditionFalse buildOrderingConstraintGraph
      !inBlockList
      !inSymbolTable
      !inBlockLabel
      ![jp mLabel]
      !?ioGraph
      !?ioVisitedBlockSet
    ] ;
  else
    [mInstructionIfConditionFalse buildOrderingConstraintGraph
      !inBlockList
      !inSymbolTable
      !inBlockLabel
      !["" nowhere]
      !?ioGraph
      !?ioVisitedBlockSet
    ] ;
  end if ;
end method ;

#----------------------------------------------------------------------------*

override method @pic18TestRegisterTerminator buildOrderingConstraintGraph
  ??@ipic18BlockList inBlockList
  ??@symbolTableForBlockOptimization inSymbolTable
  ??@lstring inBlockLabel
  ??@lstring unused inDominatorLabel
  ?!@blockInvocationGraph ioGraph
  ?!@stringset ioVisitedBlockSet
:
#--- Only true jump can be ommitted
  [mInstructionIfConditionTrue buildOrderingConstraintGraph
    !inBlockList
    !inSymbolTable
    !inBlockLabel
    !["" nowhere]
    !?ioGraph
    !?ioVisitedBlockSet
  ] ;
#--- Weak precedence
  if (mInstructionIfConditionTrue is @ipic18JumpTerminator) then
    const @ipic18JumpTerminator jp := (cast mInstructionIfConditionTrue : @ipic18JumpTerminator) ;
    [mInstructionIfConditionFalse buildOrderingConstraintGraph
      !inBlockList
      !inSymbolTable
      !inBlockLabel
      ![jp mLabel]
      !?ioGraph
      !?ioVisitedBlockSet
    ] ;
  else
    [mInstructionIfConditionFalse buildOrderingConstraintGraph
      !inBlockList
      !inSymbolTable
      !inBlockLabel
      !["" nowhere]
      !?ioGraph
      !?ioVisitedBlockSet
    ] ;
  end if ;
end method ;

#----------------------------------------------------------------------------*

#!--- Terminator noNeedToInsertJumpInstruction

#----------------------------------------------------------------------------*

abstract reader @ipic18AbstractBlockTerminator needToInsertJumpInstruction
  ??@string inNextBlockLabel
  ->@bool outResult
;

#----------------------------------------------------------------------------*

override reader @ipic18ReturnTerminator needToInsertJumpInstruction
  ??@string unused inNextBlockLabel
  ->@bool outResult
:
  outResult := false ;
end reader ;

#----------------------------------------------------------------------------*

override reader @ipic18RetlwTerminator needToInsertJumpInstruction
  ??@string unused inNextBlockLabel
  ->@bool outResult
:
  outResult := false ;
end reader ;

#----------------------------------------------------------------------------*

override reader @ipic18RetfieTerminator needToInsertJumpInstruction
  ??@string unused inNextBlockLabel
  ->@bool outResult
:
  outResult := false ;
end reader ;

#----------------------------------------------------------------------------*

override reader @ipic18JumpTerminator needToInsertJumpInstruction
  ??@string inNextBlockLabel
  ->@bool outResult
:
  outResult := mLabel->string != inNextBlockLabel ;
end reader ;

#----------------------------------------------------------------------------*

override reader @ipic18ComputedGotoTerminator needToInsertJumpInstruction
  ??@string unused inNextBlockLabel
  ->@bool outResult
:
  outResult := false ;
end reader ;

#----------------------------------------------------------------------------*

override reader @ipic18ComputedRETLWTerminator needToInsertJumpInstruction
  ??@string unused inNextBlockLabel
  ->@bool outResult
:
  outResult := false ;
end reader ;

#----------------------------------------------------------------------------*

override reader @ipic18ConditionalJumpTerminator needToInsertJumpInstruction
  ??@string unused inNextBlockLabel
  ->@bool outResult
:
  outResult := false ;
end reader ;

#----------------------------------------------------------------------------*

override reader @ipic18ComputedBraTerminator needToInsertJumpInstruction
  ??@string unused inNextBlockLabel
  ->@bool outResult
:
  outResult := false ;
end reader ;

#----------------------------------------------------------------------------*

override reader @ipic18AbstractConditionTerminator needToInsertJumpInstruction
  ??@string inNextBlockLabel
  ->@bool outResult
:
  outResult :=
    [mInstructionIfConditionTrue needToInsertJumpInstruction !inNextBlockLabel]
    &
    [mInstructionIfConditionFalse needToInsertJumpInstruction !inNextBlockLabel]
  ;
end reader ;

#----------------------------------------------------------------------------*

override reader @pic18RegisterComparisonTerminator needToInsertJumpInstruction
  ??@string inNextBlockLabel
  ->@bool outResult
:
  outResult :=
    [mInstructionIfConditionTrue needToInsertJumpInstruction !inNextBlockLabel]
  ;
end reader ;

#----------------------------------------------------------------------------*

override reader @pic18TestRegisterTerminator needToInsertJumpInstruction
  ??@string inNextBlockLabel
  ->@bool outResult
:
  outResult :=
    [mInstructionIfConditionTrue needToInsertJumpInstruction !inNextBlockLabel]
  ;
end reader ;

#----------------------------------------------------------------------------*

#!--- Terminator buildTerminatorCalledRoutineSet

#----------------------------------------------------------------------------*

method @ipic18AbstractBlockTerminator buildTerminatorCalledRoutineSet
  ?!@stringset unused ioReferencedBlockSet
:
end method ;

#----------------------------------------------------------------------------*

override method @ipic18ComputedGotoTerminator buildTerminatorCalledRoutineSet
  ?!@stringset ioReferencedBlockSet
:
  ioReferencedBlockSet += !"_computed_goto_4" ;
  foreach mTargetLabels do
    ioReferencedBlockSet += !mValue->string ;
  end foreach ;
end method ;

#----------------------------------------------------------------------------*

override method @ipic18ComputedBraTerminator buildTerminatorCalledRoutineSet
  ?!@stringset ioReferencedBlockSet
:
  ioReferencedBlockSet += !"_computed_goto_2" ;
end method ;

#----------------------------------------------------------------------------*

override method @ipic18ComputedRETLWTerminator buildTerminatorCalledRoutineSet
  ?!@stringset ioReferencedBlockSet
:
  ioReferencedBlockSet += !"_computed_goto_2" ;
end method ;

#----------------------------------------------------------------------------*

#!--- Terminator rootRoutines

#----------------------------------------------------------------------------*

method @ipic18AbstractBlockTerminator buildRootRoutineSet
  ?!@stringset unused ioReferencedBlockSet
:
end method ;

#----------------------------------------------------------------------------*

override method @ipic18JumpTerminator buildRootRoutineSet
  ?!@stringset ioReferencedBlockSet
:
  ioReferencedBlockSet += !mLabel->string ;
end method ;

#----------------------------------------------------------------------------*

override method @ipic18ComputedGotoTerminator buildRootRoutineSet
  ?!@stringset ioReferencedBlockSet
:
  foreach mTargetLabels do
    ioReferencedBlockSet += !mValue->string ;
  end foreach ;
end method ;

#----------------------------------------------------------------------------*

override method @ipic18ComputedBraTerminator buildRootRoutineSet
  ?!@stringset ioReferencedBlockSet
:
  foreach mTargetLabels do
    ioReferencedBlockSet += !mValue->string ;
  end foreach ;
end method ;

#----------------------------------------------------------------------------*

override method @ipic18AbstractConditionTerminator buildRootRoutineSet
  ?!@stringset ioReferencedBlockSet
:
  [mInstructionIfConditionTrue buildRootRoutineSet !?ioReferencedBlockSet] ;
  [mInstructionIfConditionFalse buildRootRoutineSet !?ioReferencedBlockSet] ;
end method ;

#----------------------------------------------------------------------------*

#!--- Instruction buildInstructionCalledRoutineSet

#----------------------------------------------------------------------------*

method @ipic18SequentialInstruction buildInstructionCalledRoutineSet
  ?!@stringset unused ioReferencedBlockSet
:
end method ;

#----------------------------------------------------------------------------*

override method @ipic18_intermediate_JSR buildInstructionCalledRoutineSet
  ?!@stringset ioReferencedBlockSet
:
  ioReferencedBlockSet += !mTargetLabel->string ;
end method ;

#----------------------------------------------------------------------------*

override method @ipic18_intermediate_instruction_computed_rcall buildInstructionCalledRoutineSet
  ?!@stringset ioReferencedBlockSet
:
  ioReferencedBlockSet += !"_computed_goto_4" ;
  foreach mTargetLabels do
    ioReferencedBlockSet += !mValue->string ;
  end foreach ;
end method ;

#----------------------------------------------------------------------------*

override method @ipic18_condition_skip_instruction buildInstructionCalledRoutineSet
  ?!@stringset ioReferencedBlockSet
:
  [mEmbeddedInstruction buildInstructionCalledRoutineSet !?ioReferencedBlockSet] ;
end method ;

#----------------------------------------------------------------------------*

#!--- Block optimization

#----------------------------------------------------------------------------*

method @ipic18SequentialInstructionList buildInstructionCalledRoutineSet
  ?!@stringset ioReferencedBlockSet
:
  foreach selfcopy do
    [mInstruction buildInstructionCalledRoutineSet !?ioReferencedBlockSet] ;  
  end foreach ;
end method ;

#----------------------------------------------------------------------------*

method @ipic18Block buildCalledRoutineSet
  ?!@stringset ioReferencedBlockSet
:
  [mInstructionList buildInstructionCalledRoutineSet !?ioReferencedBlockSet] ;
  [mTerminator buildTerminatorCalledRoutineSet !?ioReferencedBlockSet] ;
end method ;

#----------------------------------------------------------------------------*

#!--- Routines

#----------------------------------------------------------------------------*

local routine buildInvocationGraph
  ??@ipic18BlockList inBlockList
  !@string outGraphVizString
:
  outGraphVizString := "digraph G{\n" ;
  foreach inBlockList do
    if mBlock->mStartAddress == [@uint max] then # Only relocatable block
      [mBlock->mTerminator buildInvocationGraph !mBlock->mLabel !?outGraphVizString !false] ;
    end if ;
  end foreach ;
  outGraphVizString .= "}\n" ;
end routine ;

#----------------------------------------------------------------------------*

local routine performBlockOrdering
  ??@string unused inSourceFileName
  ?!@string ioListFileContents
  ?!@ipic18BlockList ioBlockList
:
  ioListFileContents .= ["" stringByLeftAndRightPadding !79 !'*'] . "\n" ;
  ioListFileContents .= "*" . ["BLOCK REPRESENTATION ORDERING OPTIMIZATION" stringByLeftAndRightPadding !77 !' '] . "*\n" ;
  ioListFileContents .= ["" stringByLeftAndRightPadding !79 !'*'] . "\n\n" ;
  ioListFileContents .= "Block ordering optimization:\n" ;
  const @uint n := blockDiscontinuityCount [!ioBlockList] ;
  ioListFileContents .= "  discontinuity count before optimization: " . n . "\n" ;
#---
  if [option.verbose_output value] then
    message "Block ordering optimization:\n" ;
    message "  discontinuity count before optimization: " . n . "\n" ;
  end if ;
#-------------------------------- Build label map
  @symbolTableForBlockOptimization symbolTable [emptyMap] ;
  foreach ioBlockList index blockIndex do
    [!?symbolTable insertKey !mBlock->mLabel !blockIndex] ;
  end foreach ;
#-------------------------------- Build actual routine set
  @stringset actualRoutineSet [emptySet] ;
  #--- Find root routines
  foreach ioBlockList do
    if mBlock->mStartAddress != [@uint max] then # Absolute block
      [mBlock->mTerminator buildRootRoutineSet !?actualRoutineSet] ;
    end if ;
  end foreach ;
  foreach ioBlockList do
    [mBlock buildCalledRoutineSet !?actualRoutineSet] ;
  end foreach ;
#---
  @ipic18BlockList orderedBlockList [emptyList] ;
  @stringset generatedBlockSet [emptySet] ;
  foreach ioBlockList do
    if (mBlock->mStartAddress != [@uint max]) then # Absolute block
      orderedBlockList += !mBlock ;    
    elsif [actualRoutineSet hasKey !mBlock->mLabel->string] then
      @stringset visitedBlockSet [setWithString !mBlock->mLabel->string] ;
      @blockInvocationGraph g [emptyGraph] ;
      [!?g addNode !mBlock->mLabel !mBlock->mLabel->string] ;
      [mBlock->mTerminator buildOrderingConstraintGraph
        !ioBlockList
        !symbolTable
        !mBlock->mLabel
        !["" nowhere]
        !?g
        !?visitedBlockSet
      ] ;
      [g topologicalSort
        ??@stringlist orderedLabelList
        ?3*
      ] ;
      foreach \down orderedLabelList do
        if not [generatedBlockSet hasKey !mValue] then
          generatedBlockSet += !mValue ;
          [symbolTable searchKey ![mValue nowhere] ??@uint blockIndex] ;
          const @ipic18Block b := [ioBlockList mBlockAtIndex !blockIndex] ;
          if b->mStartAddress == [@uint max] then # Only relocatable blocks
            orderedBlockList += !b ;
          end if ;
        end if ;
      end foreach ;
    end if ;
  end foreach ;
  const @uint p := blockDiscontinuityCount [!orderedBlockList] ;
  ioListFileContents .= "  discontinuity count after optimization: " . p . "\n\n" ;
  if [option.verbose_output value] then
    message "  discontinuity count after optimization: " . p . "\n\n" ;
  end if ;
#--- Check length
  @bool ok := [orderedBlockList length] == [ioBlockList length] ;
  if not ok then
    const @string s := "BLOCK REORDERING INTERNAL ERROR: origin block list is "
    . [ioBlockList length] . " blocks long, but reordered list is "
    . [orderedBlockList length] . " blocks long"
    ;
    warning [@location nowhere]: s ;
    ioListFileContents .= s . ".\n\n" ;
  end if ;
#--- Check block definition
  @stringset newSymbolSet [emptySet] ;
  foreach orderedBlockList do
    if [newSymbolSet hasKey !mBlock->mLabel->string] then
      const @string s := "BLOCK REORDERING INTERNAL ERROR: block \""
      . mBlock->mLabel . "\" is duplicated"
      ;
      warning [@location nowhere]: s ;
      ioListFileContents .= s . ".\n\n" ;
      ok := false ;
    else
      newSymbolSet += !mBlock->mLabel->string ;
    end if ;
  end foreach ;
  foreach symbolTable do
    if not [newSymbolSet hasKey !lkey->string] then
      const @string s := "BLOCK REORDERING INTERNAL ERROR: block \""
      . lkey . "\" is missing"
      ;
      warning [@location nowhere]: s ;
      ioListFileContents .= s . ".\n\n" ;
      ok := false ;
    end if ;
  end foreach ;
#--- Perform list remplacement if no detected error
  if ok then
    ioBlockList := orderedBlockList ;
    displayBlockList !"ORDERED OPTIMIZED INTERMEDIATE BLOCK REPRESENTATION" !?ioListFileContents !orderedBlockList ;
  else
    const @string s := "BLOCK REORDERING INTERNAL ERROR: reordered block list is inconsistent, it is not used anymore" ;
    warning [@location nowhere]: s ;
    ioListFileContents .= s . ".\n\n" ;
    displayBlockList !"INCONSISTENT ORDERED OPTIMIZED INTERMEDIATE BLOCK REPRESENTATION (not used)" !?ioListFileContents !orderedBlockList ;
  end if ;
end routine ;

#----------------------------------------------------------------------------*

local function blockDiscontinuityCount
  ?@ipic18BlockList inBlockList
  ->@uint outResult
:
  outResult := 0 ;
  foreach inBlockList index blockIndex do
    if mBlock->mStartAddress == [@uint max] then # Only relocatable block
      @string nextBlockLabel ;
      if (blockIndex+1) < [inBlockList length] then
        nextBlockLabel := [inBlockList mBlockAtIndex !blockIndex+1]->mLabel->string ;
      else
        nextBlockLabel := "" ;
      end if ;
      if [mBlock->mTerminator needToInsertJumpInstruction !nextBlockLabel] then
        outResult := outResult + 1 ;
      end if ;
    end if ;
  end foreach ;
end function ;

#----------------------------------------------------------------------------*

routine ipic18OptimizeBlockOrdering
  ??@string inSourceFileName
  ?!@string ioListFileContents
  ?!@ipic18BlockList ioGeneratedBlockList
:
#---
  buildInvocationGraph !ioGeneratedBlockList ??@string invocationGraph ;
  [invocationGraph writeToFileWhenDifferentContents !inSourceFileName. ".blockInvocation.dot" ?*] ;
##---
#  buildDirectInvocationGraph !ioGeneratedBlockList ??@blockInvocationGraph directInvocationGraph ;
#  [[directInvocationGraph graphviz] writeToFile !inSourceFileName. ".orderedBlocks.dot"] ;
#---
  performBlockOrdering !inSourceFileName !?ioListFileContents !?ioGeneratedBlockList ;
end routine ;

#----------------------------------------------------------------------------*

end semantics ;
