semantics midrange_optimizations :
import semantics midrange_ipic_metamodel in "midrange_ipic_metamodel.gSemantics" ;
import "midrange_ipic_metamodel.gSemantics" ;

#----------------------------------------------------------------------------*
#                                                                            *
#                          defineLabel                                       *
#                                                                            *
#----------------------------------------------------------------------------*

method @midrange_ipic_instruction defineLabel
  ?!@symbolTableForOptimizations unused ioRoutineSymbolTable
  ??@uint unused inLineIndex
:
end method ;

#----------------------------------------------------------------------------*

override
method @midrange_ipic_pseudo_LABEL defineLabel
  ?!@symbolTableForOptimizations ioRoutineSymbolTable
  ??@uint inLineIndex
:
  [!?ioRoutineSymbolTable insertKey !mLabel !inLineIndex] ;
end method ;

#----------------------------------------------------------------------------*

routine findMidRangeFirstInstructionFromAddress
  ??@uint inLineIndex
  ??@midrange_ipic_instructionList inGeneratedInstructionList
  !@midrange_ipic_instruction outFirstInstruction
:
#--- Line Index
  @uint currentAddress := inLineIndex ;
#--- Find first executable instruction
  outFirstInstruction := [@midrange_ipic_NULL new] ; # By default, no instruction
  @bool found := false ;
  loop [inGeneratedInstructionList length] :
  while (currentAddress < [inGeneratedInstructionList length]) & not found
  do
    cast [inGeneratedInstructionList mInstructionAtIndex !currentAddress] :
    when >= @midrange_ipic_actualInstruction instr do
      outFirstInstruction := instr ;
      found := true ;
    else
      currentAddress ++ ;
    end cast ;
  end loop ;
end routine ;

#----------------------------------------------------------------------------*

routine findMidRangeFirstInstructionOrLabelFromAddress
  ??@uint inLineIndex
  ??@midrange_ipic_instructionList inGeneratedInstructionList
  !@midrange_ipic_instruction outFirstInstruction
  !@stringset outReachedLabelSet
:
  outReachedLabelSet := [@stringset emptySet] ;
#--- Line Index
  @uint currentAddress := inLineIndex ;
#--- Find first executable instruction
  outFirstInstruction := [@midrange_ipic_NULL new] ; # By default, no instruction
  @bool found := false ;
  loop [inGeneratedInstructionList length] :
  while (currentAddress < [inGeneratedInstructionList length]) & not found
  do
    cast [inGeneratedInstructionList mInstructionAtIndex !currentAddress] :
    when >= @midrange_ipic_actualInstruction instr do
      outFirstInstruction := instr ;
      found := true ;
    when >= @midrange_ipic_pseudo_ORG instr do
      outFirstInstruction := instr ;
      found := true ;
    when == @midrange_ipic_pseudo_LABEL instr do
      outFirstInstruction := instr ;
      outReachedLabelSet += ![[instr mLabel] string] ;
      currentAddress ++ ;
    else
      currentAddress ++ ;
    end cast ;
  end loop ;
end routine ;

#----------------------------------------------------------------------------*
#                                                                            *
#                           O P T I M I Z E                                  *
#                                                                            *
#----------------------------------------------------------------------------*

method @midrange_ipic_JUMP optimizeJUMP
  ??@symbolTableForOptimizations inSymbolTable
  ??@uint inLineIndex
  ?!@midrange_ipic_instructionList ioGeneratedInstructionList
  ?!@string ioListFileContents
  ?!@bool ioOptimizationsDone
:
#--- The JUMP goes to the next instruction ?
  @stringset reachedLabelSet ;
  findMidRangeFirstInstructionOrLabelFromAddress !inLineIndex+1 !ioGeneratedInstructionList ?* ?reachedLabelSet ;
#  log reachedLabelSet ; log inLineIndex ;
  if [reachedLabelSet hasKey ![mTargetLabel string]] then
    [!?ioGeneratedInstructionList setMInstructionAtIndex ![@midrange_ipic_NULL new] !inLineIndex] ;
    ioListFileContents .= "  line " . [inLineIndex string] . ": JUMP branching to next instruction deleted\n" ;
  else      
  #--- Find destination label definition line
    @uint line ; [inSymbolTable searchKey !mTargetLabel ?line] ;
  #--- Find first executable instruction
    @midrange_ipic_instruction firstInstruction ;
    findMidRangeFirstInstructionFromAddress !line + 1 !ioGeneratedInstructionList ?firstInstruction ;
  #--- Optimization from found first instruction
    cast firstInstruction :
    when == @midrange_ipic_instruction_RETURN do
      [!?ioGeneratedInstructionList setMInstructionAtIndex
        ![@midrange_ipic_instruction_RETURN new !mInstructionLocation]
        !inLineIndex
      ] ;
      ioOptimizationsDone := true ;
      ioListFileContents .= "  line " . [inLineIndex string] . ": JUMP branching to RETURN replaced by RETURN\n" ;
    when == @midrange_ipic_instruction_literalOperation literalOp do
      if [literalOp mLiteralInstruction] == [@midrange_literal_instruction_opcode RETLW] then
        [!?ioGeneratedInstructionList setMInstructionAtIndex
          !literalOp
          !inLineIndex
        ] ;
        ioOptimizationsDone := true ;
        ioListFileContents .= "  line " . [inLineIndex string] . ": JUMP branching to RETLW replaced by RETLW\n" ;
      end if ;
    when == @midrange_ipic_JUMP jump do
      if mTargetLabel != [jump mTargetLabel] then
        [!?ioGeneratedInstructionList setMInstructionAtIndex
          ![@midrange_ipic_JUMP new !mInstructionLocation ![jump mTargetLabel] ![@midrange_call_goto_bit noChange] ![@midrange_call_goto_bit noChange]]
          !inLineIndex
        ] ;
        ioOptimizationsDone := true ;
        ioListFileContents .= "  line " . [inLineIndex string] . ": JUMP branching to JUMP replaced by JUMP\n" ;
      end if ;
    else # No Optimization
    end cast ;
  end if ;      
end method ;

#----------------------------------------------------------------------------*

method @midrange_ipic_GOTO optimizeGOTO
  ??@symbolTableForOptimizations inSymbolTable
  ??@uint inLineIndex
  ?!@midrange_ipic_instructionList ioGeneratedInstructionList
  ?!@string ioListFileContents
  ?!@bool ioOptimizationsDone
:
#--- The GOTO goes to the next instruction ?
  @stringset reachedLabelSet ;
  findMidRangeFirstInstructionOrLabelFromAddress !inLineIndex+1 !ioGeneratedInstructionList ?* ?reachedLabelSet ;
#  log reachedLabelSet ; log inLineIndex ;
  if [reachedLabelSet hasKey ![mTargetLabel string]] then
    [!?ioGeneratedInstructionList setMInstructionAtIndex ![@midrange_ipic_NULL new] !inLineIndex] ;
    ioListFileContents .= "  line " . [inLineIndex string] . ": GOTO branching to next instruction deleted\n" ;
  else      
  #--- Find destination label definition line
    @uint line ; [inSymbolTable searchKey !mTargetLabel ?line] ;
  #--- Find first executable instruction
    @midrange_ipic_instruction firstInstruction ;
    findMidRangeFirstInstructionFromAddress !line + 1 !ioGeneratedInstructionList ?firstInstruction ;
  #--- Optimization from found first instruction
    cast firstInstruction :
    when == @midrange_ipic_instruction_RETURN do
      [!?ioGeneratedInstructionList setMInstructionAtIndex
        ![@midrange_ipic_instruction_RETURN new !mInstructionLocation]
        !inLineIndex
      ] ;
      ioOptimizationsDone := true ;
      ioListFileContents .= "  line " . [inLineIndex string] . ": GOTO branching to RETURN replaced by RETURN\n" ;
    when == @midrange_ipic_instruction_literalOperation literalOp do
      if [literalOp mLiteralInstruction] == [@midrange_literal_instruction_opcode RETLW] then
        [!?ioGeneratedInstructionList setMInstructionAtIndex
          !literalOp
          !inLineIndex
        ] ;
        ioOptimizationsDone := true ;
        ioListFileContents .= "  line " . [inLineIndex string] . ": GOTO branching to RETLW replaced by RETLW\n" ;
      end if ;
    when == @midrange_ipic_JUMP jump do
      if mTargetLabel != [jump mTargetLabel] then
        [!?ioGeneratedInstructionList setMInstructionAtIndex
          ![@midrange_ipic_JUMP new !mInstructionLocation ![jump mTargetLabel] ![@midrange_call_goto_bit noChange] ![@midrange_call_goto_bit noChange]]
          !inLineIndex
        ] ;
        ioOptimizationsDone := true ;
        ioListFileContents .= "  line " . [inLineIndex string] . ": GOTO branching to JUMP replaced by JUMP\n" ;
      end if ;
    when == @midrange_ipic_GOTO goto do
      if mTargetLabel != [goto mTargetLabel] then
        [!?ioGeneratedInstructionList setMInstructionAtIndex
          ![@midrange_ipic_GOTO new !mInstructionLocation ![goto mTargetLabel]]
          !inLineIndex
        ] ;
        ioOptimizationsDone := true ;
        ioListFileContents .= "  line " . [inLineIndex string] . ": GOTO branching to JUMP replaced by JUMP\n" ;
      end if ;
    else # No Optimization
    end cast ;
  end if ;      
end method ;

#----------------------------------------------------------------------------*

#method @midrange_ipic_CALL.optimizeCALL
#  ??@uint inLineIndex
#  ?!@midrange_ipic_instructionList ioGeneratedInstructionList
#  ?!@string ioListFileContents
#  ?!@bool ioOptimizationsDone
#:
##--- Find first executable instruction
#  @midrange_ipic_instruction firstInstruction ;
#  findFirstInstructionFromAddress !inLineIndex + 1 !ioGeneratedInstructionList ?firstInstruction ;
##--- Optimization from found first instruction
#  cast firstInstruction :
#  when == @midrange_ipic_instruction_RETURN do
#    [!?ioGeneratedInstructionList setMInstructionAtIndex
#      ![@midrange_ipic_GOTO new !mInstructionLocation !mTargetLabel]
#      !inLineIndex
#    ] ;
#    ioOptimizationsDone := true ;
#    ioListFileContents .= "  line " . [inLineIndex string] . ": JSR followed by RETURN replaced by JUMP\n" ;
#  else
#  end cast ;
#end method ;

#----------------------------------------------------------------------------*

method @midrange_ipic_instruction_literalOperation optimizeMOVLW
  ??@uint inLineIndex
  ?!@midrange_ipic_instructionList ioGeneratedInstructionList
  ?!@string ioListFileContents
  ?!@bool ioOptimizationsDone
:
  if mLiteralInstruction == [@midrange_literal_instruction_opcode MOVLW] then
  #--- Find first executable instruction
    @midrange_ipic_instruction firstInstruction ;
    findMidRangeFirstInstructionFromAddress !inLineIndex + 1 !ioGeneratedInstructionList ?firstInstruction ;
  #--- Optimization from found first instruction
    cast firstInstruction :
    when == @midrange_ipic_instruction_RETURN do
      [!?ioGeneratedInstructionList setMInstructionAtIndex
        ![@midrange_ipic_instruction_literalOperation new !mInstructionLocation ![@midrange_literal_instruction_opcode RETLW] !mLiteralValue]
        !inLineIndex
      ] ;
      ioOptimizationsDone := true ;
      ioListFileContents .= "  line " . [inLineIndex string] . ": MOVLW followed by RETURN replaced by RETLW\n" ;
    else
    end cast ;
  end if ;
end method ;

#----------------------------------------------------------------------------*

method @midrange_ipic_incDecRegisterInCondition optimizeTestDecInc
  ??@symbolTableForOptimizations inSymbolTable
  ??@uint inLineIndex
  ?!@midrange_ipic_instructionList ioGeneratedInstructionList
  ?!@string ioListFileContents
  ?!@bool ioOptimizationsDone
:
#--- Find destination label definition line
  @uint line ; [inSymbolTable searchKey ![@lstring new !mTargetLabel !here] ?line] ;
#--- Find first executable instruction
  @midrange_ipic_instruction firstInstruction ;
  findMidRangeFirstInstructionFromAddress !line + 1 !ioGeneratedInstructionList ?firstInstruction ;
#--- Optimization from found first instruction
  cast firstInstruction :
  when == @midrange_ipic_JUMP jump do
    [!?ioGeneratedInstructionList setMInstructionAtIndex
      ![@midrange_ipic_incDecRegisterInCondition new
        !mInstructionLocation
        !mRegisterDescription
        ![[jump mTargetLabel] string]
        !mIncrement
        !m_W_isDestination
        !mBranchIfZero
      ]
      !inLineIndex
    ] ;
    ioOptimizationsDone := true ;
    ioListFileContents .= "  line " . [inLineIndex string] . ": label of " ;
    ioListFileContents .= "JUMP " ;
    if mIncrement then
      ioListFileContents .= "INCF " ;
    else
      ioListFileContents .= "DECF " ;
    end if ;
    ioListFileContents .= [mRegisterDescription mAssemblyString] ;
    if m_W_isDestination then
      ioListFileContents .= ", W " ;
    end if ;
    if mBranchIfZero then
      ioListFileContents .= "Z" ;
    else
      ioListFileContents .= "NZ" ;
    end if ;
    ioListFileContents .= " branching to JUMP replaced by second JUMP label\n" ;
  else
  end cast ;
end method ;

#----------------------------------------------------------------------------*

method @midrange_ipic_instruction_BitTestSkip optimizeBitTestSkip
  ??@uint inLineIndex
  ?!@midrange_ipic_instructionList ioGeneratedInstructionList
  ?!@string ioListFileContents
  ?!@bool ioOptimizationsDone
:
#--- First following instruction is a JUMP ?
  @string jumpLabel := "" ; # No By default
  @bool ok := false ;
  cast [ioGeneratedInstructionList mInstructionAtIndex !inLineIndex + 1] :
  when == @midrange_ipic_JUMP jump do
    jumpLabel := [[jump mTargetLabel] string] ;
    ok := true ;
  else
  end cast ;
#--- Get second following instruction
  if (ok) then
    cast [ioGeneratedInstructionList mInstructionAtIndex !inLineIndex + 2] :
    when >= @midrange_ipic_actualInstruction instruction do
      @uint instructionLength ; [instruction instructionLength ?instructionLength] ;
      ok := instructionLength == 1 ;
    else
      ok := false ;
    end cast ;
  end if ;
#--- Get third following instruction
  if (ok) then
    @midrange_ipic_instruction thirdInstruction := [@midrange_ipic_NULL new] ;
    @stringset reachedLabelSet ;
    findMidRangeFirstInstructionOrLabelFromAddress !inLineIndex+3 !ioGeneratedInstructionList ?* ?reachedLabelSet ;
    ok := [reachedLabelSet hasKey !jumpLabel] ;
  end if ;
#--- If Ok, perform optimization
  if ok then
  #--- Complement Bit test instruction (at inLineIndex)
    [!?ioGeneratedInstructionList setMInstructionAtIndex
      ![@midrange_ipic_instruction_BitTestSkip new
        !mInstructionLocation
        !not mSkipIfSet
        !mRegisterDescription
        !mBitNumber
      ]
      !inLineIndex
    ] ;
  #--- Copy instruction at (inLineIndex + 2) to (inLineIndex + 1)
    [!?ioGeneratedInstructionList setMInstructionAtIndex
      ![ioGeneratedInstructionList mInstructionAtIndex !inLineIndex + 2]
      !inLineIndex + 1
    ] ;
  #--- Delete instruction at (inLineIndex + 2)
    [!?ioGeneratedInstructionList setMInstructionAtIndex
      ![@midrange_ipic_NULL new]
      !inLineIndex + 2
    ] ;
  #--- 
    ioOptimizationsDone := true ;
  #--- Log
    ioListFileContents .= "  lines " . [inLineIndex string] . " to " . [inLineIndex + 2 string] ;
    ioListFileContents .= " bit test skip, following by a JUMP skipping next instruction, is complemented\n" ;
  end if ;
end method ;

#----------------------------------------------------------------------------*
#                                                                            *
#               S E M A N T I C    A N A L Y S I S                           *
#                                                                            *
#----------------------------------------------------------------------------*

routine perform_midrange_optimizations
  ?!@midrange_ipic_instructionList ioGeneratedInstructionList
  ?!@string ioListFileContents
:
#-------------------------------- List file
  ioListFileContents .= ["" stringByLeftAndRightPadding !79 !'*'] . "\n" ;
  ioListFileContents .= "*" . ["CODE OPTIMIZATION" stringByLeftAndRightPadding !77 !' '] . "*\n" ;
  ioListFileContents .= ["" stringByLeftAndRightPadding !79 !'*'] . "\n\n" ;
#-------------------------------- Build label map
  @symbolTableForOptimizations symbolTable [emptyMap] ;
  foreach ioGeneratedInstructionList:lineIndex do
    [mInstruction defineLabel !?symbolTable !lineIndex] ;
  end foreach ;
#-------------------------------- Iterative optimizations
  @bool optimizationsDone := true ;
  @uint iteration := 0 ;
  loop [ioGeneratedInstructionList length] :
  while optimizationsDone
  do
    optimizationsDone := false ;
    iteration ++ ;
    ioListFileContents .= "Pass " . [iteration string] . ":\n" ;
  #--- Optimization loop
    foreach ioGeneratedInstructionList :idx do
      cast mInstruction :
      when == @midrange_ipic_GOTO ipic_GOTO do
        [ipic_GOTO optimizeGOTO !symbolTable !idx !?ioGeneratedInstructionList !?ioListFileContents !?optimizationsDone] ;
      when == @midrange_ipic_JUMP ipic_JUMP do
        [ipic_JUMP optimizeJUMP !symbolTable !idx !?ioGeneratedInstructionList !?ioListFileContents !?optimizationsDone] ;
#      when == @midrange_ipic_CALL ipic_CALL do
#        [ipic_CALL optimizeCALL !idx !?ioGeneratedInstructionList !?ioListFileContents !?optimizationsDone] ;
      when == @midrange_ipic_instruction_literalOperation ipic_literal do
        [ipic_literal optimizeMOVLW !idx !?ioGeneratedInstructionList !?ioListFileContents !?optimizationsDone] ;
      when == @midrange_ipic_incDecRegisterInCondition ipic_decInc do
        [ipic_decInc optimizeTestDecInc !symbolTable !idx !?ioGeneratedInstructionList !?ioListFileContents !?optimizationsDone] ;
      when == @midrange_ipic_instruction_BitTestSkip ipic_bitTestSkip do
        [ipic_bitTestSkip optimizeBitTestSkip !idx !?ioGeneratedInstructionList !?ioListFileContents !?optimizationsDone] ;
      else
      end cast ;
    end foreach ;
  #--- Delete non referenced labels
    @stringset referencedLabels [emptySet] ;
    foreach ioGeneratedInstructionList do
      [mInstruction enterReferencedLabel !?referencedLabels] ;
    end foreach ;
    foreach symbolTable do
      if not [referencedLabels hasKey ![key string]] then
        @uint definitionLine ; [symbolTable searchKey !key ?definitionLine] ;
        ioListFileContents .= "  line " . [definitionLine  string] . ": unreferenced label '" . key . "' is deleted\n" ;
        [!?ioGeneratedInstructionList setMInstructionAtIndex ![@midrange_ipic_NULL new] !definitionLine] ;
        [!?symbolTable removeKey !key ?*] ;
      end if ;
    end foreach ;
  #--- Dead code elimination
    @bool reachable := true ;
    @bool skippingInstruction := false ;
    @bool previousWasSkippingInstruction := false ;
    foreach ioGeneratedInstructionList :idx do
       if reachable | previousWasSkippingInstruction then
         [mInstruction nextInstructionIsReachable ?reachable] ;
         previousWasSkippingInstruction := skippingInstruction ;
         [mInstruction isSkippingInstruction ?skippingInstruction] ;
       else
         @bool isLABELorORG ; [mInstruction isLABELorORG ?isLABELorORG] ;
         @bool isNULL ; [mInstruction isNULL ?isNULL] ;
         if (not isLABELorORG) & (not isNULL) then
           ioListFileContents .= "  line " . [idx string] . ": unreachable instruction is deleted\n" ;
           [!?ioGeneratedInstructionList setMInstructionAtIndex ![@midrange_ipic_NULL new] !idx] ;
         elsif isLABELorORG then
           reachable := true ;
           previousWasSkippingInstruction := false ;
           skippingInstruction := false ;
         end if ;
       end if ;
    end foreach ;
    if not optimizationsDone then
      ioListFileContents .= "  No optimization\n\n" ;
    #--- Compute code size
      @uint currentAddress := 0 ;
      foreach ioGeneratedInstructionList do
        [mInstruction setCurrentAddress !?currentAddress] ;
      end foreach ;
      ioListFileContents .= "Minimum code size: " . [currentAddress string] . " instructions\n\n" ;
    end if ;
  end loop ;
end routine ;

#----------------------------------------------------------------------------*

end semantics ;
