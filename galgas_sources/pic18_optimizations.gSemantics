semantics pic18_optimizations :
import "pic18_intermediate_next_intruction_is_reachable.gSemantics" ;
import "pic18_intermediate_classes_set_current_address.gSemantics" ;

#----------------------------------------------------------------------------*
#                                                                            *
#                          defineLabel                                       *
#                                                                            *
#----------------------------------------------------------------------------*

method @pic18_intermediate_instruction defineLabel
  ?!@symbolTableForOptimizations unused ioRoutineSymbolTable
  ??@uint unused inLineIndex
:
end method ;

#----------------------------------------------------------------------------*

override method @pic18_intermediate_pseudo_LABEL defineLabel
  ?!@symbolTableForOptimizations ioRoutineSymbolTable
  ??@uint inLineIndex
:
  [!?ioRoutineSymbolTable insertKey !mLabel !inLineIndex !mIsDeletable] ;
end method ;

#----------------------------------------------------------------------------*

routine findFirstInstructionFromAddress
  ??@uint inLineIndex
  ??@pic18_intermediate_instructionList inGeneratedInstructionList
  !@pic18_intermediate_instruction outFirstInstruction
  !@uint outNextInstructionLineIndex
:
#--- Line Index
  outNextInstructionLineIndex := inLineIndex ;
#--- Find first executable instruction
  outFirstInstruction := [@pic18_intermediate_NULL new] ; # By default, no instruction
  @bool found := false ;
  loop [inGeneratedInstructionList length] :
  while (outNextInstructionLineIndex < [inGeneratedInstructionList length]) & not found
  do
    cast [inGeneratedInstructionList mInstructionAtIndex !outNextInstructionLineIndex] :
    when >= @pic18_intermediate_actualInstruction instr do
      outFirstInstruction := instr ;
      found := true ;
    when >= @pic18_intermediate_pseudo_ORG  do
      found := true ;
    else
      outNextInstructionLineIndex ++ ;
    end cast ;
  end loop ;
  outNextInstructionLineIndex ++ ;
end routine ;

#----------------------------------------------------------------------------*

routine findFirstInstructionOrLabelFromAddress
  ??@uint inLineIndex
  ??@pic18_intermediate_instructionList inGeneratedInstructionList
  !@pic18_intermediate_instruction outFirstInstruction
  !@stringset outReachedLabelSet
:
  outReachedLabelSet := [@stringset emptySet] ;
#--- Line Index
  @uint currentAddress := inLineIndex ;
#--- Find first executable instruction
  outFirstInstruction := [@pic18_intermediate_NULL new] ; # By default, no instruction
  @bool found := false ;
  loop [inGeneratedInstructionList length] :
  while (currentAddress < [inGeneratedInstructionList length]) & not found
  do
    cast [inGeneratedInstructionList mInstructionAtIndex !currentAddress] :
    when >= @pic18_intermediate_actualInstruction instr do
      outFirstInstruction := instr ;
      found := true ;
    when >= @pic18_intermediate_pseudo_ORG instr do
      outFirstInstruction := instr ;
      found := true ;
    when == @pic18_intermediate_pseudo_LABEL instr do
      outFirstInstruction := instr ;
      outReachedLabelSet += ![[instr mLabel] string] ;
      currentAddress ++ ;
    else
      currentAddress ++ ;
    end cast ;
  end loop ;
end routine ;

#----------------------------------------------------------------------------*
#                                                                            *
#                           O P T I M I Z E                                  *
#                                                                            *
#----------------------------------------------------------------------------*

method @pic18_intermediate_JUMP optimizeJUMP
  ??@symbolTableForOptimizations inSymbolTable
  ??@uint inLineIndex
  ?!@pic18_intermediate_instructionList ioGeneratedInstructionList
  ?!@string ioListFileContents
  ?!@bool ioOptimizationsDone
:
#--- The JUMP goes to the next instruction ?
  @stringset reachedLabelSet ;
  findFirstInstructionOrLabelFromAddress !inLineIndex+1 !ioGeneratedInstructionList ?* ?reachedLabelSet ;
#  log reachedLabelSet ; log inLineIndex ;
  if [reachedLabelSet hasKey ![mTargetLabel string]] then
    [!?ioGeneratedInstructionList setMInstructionAtIndex ![@pic18_intermediate_NULL new] !inLineIndex] ;
    ioListFileContents .= "  line " . [inLineIndex string] . ": JUMP '" . mTargetLabel . "' branching to next instruction deleted\n" ;
  else      
  #--- Find destination label definition line
    [inSymbolTable searchKey !mTargetLabel ?@uint line ?*] ;
  #--- Find first executable instruction
    @pic18_intermediate_instruction firstInstruction ;
    findFirstInstructionFromAddress !line + 1 !ioGeneratedInstructionList ?firstInstruction ?* ;
  #--- Optimization from found first instruction
    cast firstInstruction :
    when == @pic18_intermediate_instruction_RETURN do
      [!?ioGeneratedInstructionList setMInstructionAtIndex
        ![@pic18_intermediate_instruction_RETURN new !mInstructionLocation]
        !inLineIndex
      ] ;
      ioOptimizationsDone := true ;
      ioListFileContents .= "  line " . [inLineIndex string] . ": JUMP '" . mTargetLabel . "' branching to RETURN replaced by RETURN\n" ;
    when == @pic18_intermediate_instruction_RETLW retlwInstruction do
      [!?ioGeneratedInstructionList setMInstructionAtIndex
        !retlwInstruction
        !inLineIndex
      ] ;
      ioOptimizationsDone := true ;
      ioListFileContents .= "  line " . [inLineIndex string] . ": JUMP '" . mTargetLabel . "'branching to RETLW " . [retlwInstruction mLiteralValue] . "replaced by RETLW " . [retlwInstruction mLiteralValue] . "\n" ;
    when == @pic18_intermediate_JUMP jump do
      if mTargetLabel != [jump mTargetLabel] then
        [!?ioGeneratedInstructionList setMInstructionAtIndex
          ![@pic18_intermediate_JUMP new !mInstructionLocation ![jump mTargetLabel] !mKind]
          !inLineIndex
        ] ;
        ioOptimizationsDone := true ;
        ioListFileContents .= "  line " . [inLineIndex string] . ": JUMP '" . mTargetLabel . "' branching to JUMP '" . [jump mTargetLabel] . "' replaced by JUMP '" . [jump mTargetLabel] . "'\n" ;
      end if ;
    else # No Optimization
    end cast ;
  end if ;      
end method ;

#----------------------------------------------------------------------------*

method @pic18_intermediate_JSR optimizeJSR
  ??@symbolTableForOptimizations inSymbolTable
  ??@uint inLineIndex
  ?!@pic18_intermediate_instructionList ioGeneratedInstructionList
  ?!@string ioListFileContents
  ?!@bool ioOptimizationsDone
:
#--- Find first executable instruction
  @pic18_intermediate_instruction firstInstruction ;
  findFirstInstructionFromAddress !inLineIndex + 1 !ioGeneratedInstructionList ?firstInstruction ?* ;
#--- Optimization from found first instruction
  cast firstInstruction :
  when == @pic18_intermediate_instruction_RETURN do # JSR followed by RETURN
    [!?ioGeneratedInstructionList setMInstructionAtIndex
      ![@pic18_intermediate_JUMP new !mInstructionLocation !mTargetLabel !mKind]
      !inLineIndex
    ] ;
    ioOptimizationsDone := true ;
    ioListFileContents .= "  line " . [inLineIndex string] . ": JSR '" . mTargetLabel . "' followed by RETURN replaced by JUMP\n" ;
  else
    #--- Find destination label definition line
      [inSymbolTable searchKey !mTargetLabel ?@uint line ?*] ;
    #--- Find first executable instruction
      @pic18_intermediate_instruction firstSubroutineInstruction ;
      @uint followingInstructionLineIndex ;
      findFirstInstructionFromAddress
        !line + 1
        !ioGeneratedInstructionList
        ?firstSubroutineInstruction
        ?followingInstructionLineIndex
      ;
      cast firstSubroutineInstruction :
      when == @pic18_intermediate_instruction_RETLW RETLWinstruction do # JSR to RETLW replaced by MOVLW
        [!?ioGeneratedInstructionList setMInstructionAtIndex
          ![@pic18_intermediate_instruction_literalOperation new
            !mInstructionLocation
            ![@literal_instruction_opcode MOVLW]
            ![RETLWinstruction mLiteralValue]
          ]
          !inLineIndex
        ] ;
        ioOptimizationsDone := true ;
        ioListFileContents .= "  line " . [inLineIndex string] . ": JSR '" . mTargetLabel
        . "' to RETLW " . [RETLWinstruction mLiteralValue]
        . " replaced by MOVLW " . [RETLWinstruction mLiteralValue] . "\n" ;
      when >= @pic18_intermediate_actualInstruction actualInstruction do
        if ([actualInstruction instructionLength] == 2)
          & [actualInstruction nextInstructionIsReachable]
          & not [actualInstruction isSkippingInstruction] then
          @pic18_intermediate_instruction followingInstruction ;
          findFirstInstructionFromAddress !followingInstructionLineIndex !ioGeneratedInstructionList ?followingInstruction ?* ;
          cast followingInstruction :
          when == @pic18_intermediate_instruction_RETURN do # JSR followed by RETURN
            [!?ioGeneratedInstructionList setMInstructionAtIndex
              !actualInstruction
              !inLineIndex
            ] ;
            ioOptimizationsDone := true ;
            ioListFileContents .= "  line " . [inLineIndex string] . ": JSR '" . mTargetLabel
            . "' to one instruction routine replaced by instruction\n" ;
        
          end cast ;
        end if ;
      end cast ;
  end cast ;
end method ;

#----------------------------------------------------------------------------*

method @pic18_intermediate_instruction_literalOperation optimizeMOVLW
  ??@uint inLineIndex
  ?!@pic18_intermediate_instructionList ioGeneratedInstructionList
  ?!@string ioListFileContents
  ?!@bool ioOptimizationsDone
:
  if mLiteralInstruction == [@literal_instruction_opcode MOVLW] then
  #--- Find first executable instruction
    @pic18_intermediate_instruction firstInstruction ;
    findFirstInstructionFromAddress !inLineIndex + 1 !ioGeneratedInstructionList ?firstInstruction ?* ;
  #--- Optimization from found first instruction
    cast firstInstruction :
    when == @pic18_intermediate_instruction_RETURN do
      [!?ioGeneratedInstructionList setMInstructionAtIndex
        ![@pic18_intermediate_instruction_RETLW new !mInstructionLocation !mLiteralValue]
        !inLineIndex
      ] ;
      ioOptimizationsDone := true ;
      ioListFileContents .= "  line " . [inLineIndex string] . ": MOVLW " . mLiteralValue . " followed by RETURN replaced by RETLW " . mLiteralValue . "\n" ;
    else
    end cast ;
  end if ;
end method ;

#----------------------------------------------------------------------------*

method @pic18_intermediate_instruction_conditionalBranch optimizeBCC
  ??@symbolTableForOptimizations inSymbolTable
  ??@uint inLineIndex
  ?!@pic18_intermediate_instructionList ioGeneratedInstructionList
  ?!@string ioListFileContents
  ?!@bool ioOptimizationsDone
:
#--- Find destination label definition line
  [inSymbolTable searchKey !mTargetLabel ?@uint line ?*] ;
#--- Find first executable instruction
  @pic18_intermediate_instruction firstInstruction ;
  findFirstInstructionFromAddress !line + 1 !ioGeneratedInstructionList ?firstInstruction ?* ;
#--- Optimization from found first instruction
  cast firstInstruction :
  when == @pic18_intermediate_JUMP jump do
    if [mTargetLabel string] != [[jump mTargetLabel] string] then
      [!?ioGeneratedInstructionList setMInstructionAtIndex
        ![@pic18_intermediate_instruction_conditionalBranch new !mInstructionLocation !mBranchMode !mConditionalBranch ![jump mTargetLabel]]
        !inLineIndex
      ] ;
      ioOptimizationsDone := true ;
      ioListFileContents .= "  line " . [inLineIndex string] . ": label of JUMP " . [mConditionalBranch condition] . " branching to JUMP '" . [jump mTargetLabel] . "' replaced by second JUMP '" . [jump mTargetLabel] . "'\n" ;
    end if ;
  else
  end cast ;
end method ;

#----------------------------------------------------------------------------*

#method @pic18_intermediate_jump_test_register optimizeTestReg
#  ??@symbolTableForOptimizations inSymbolTable
#  ??@uint inLineIndex
#  ?!@pic18_intermediate_instructionList ioGeneratedInstructionList
#  ?!@string ioListFileContents
#  ?!@bool ioOptimizationsDone
#:
##--- Find destination label definition line
#  [inSymbolTable searchKey !mTargetLabel ?@uint line ?*] ;
##--- Find first executable instruction
#  @pic18_intermediate_instruction firstInstruction ;
#  findFirstInstructionFromAddress !line + 1 !ioGeneratedInstructionList ?firstInstruction ?* ;
##--- Optimization from found first instruction
#  cast firstInstruction :
#  when == @pic18_intermediate_JUMP jump do
#    if mTargetLabel->string != [[jump mTargetLabel] string] then
#      [!?ioGeneratedInstructionList setMInstructionAtIndex
#        ![@pic18_intermediate_jump_test_register new !mInstructionLocation !mRegisterDescription ![jump mTargetLabel] !mBranchIfZero !mUsesBra]
#        !inLineIndex
#      ] ;
#      ioOptimizationsDone := true ;
#      ioListFileContents .= "  line " . [inLineIndex string] . ": label of JUMP " . [mRegisterDescription mAssemblyString] ;
#      if mBranchIfZero then
#        ioListFileContents .= "Z" ;
#      else
#        ioListFileContents .= "NZ" ;
#      end if ;
#      ioListFileContents .= " branching to JUMP replaced by second JUMP label\n" ;
#    end if ;
#  else
#  end cast ;
#end method ;

#----------------------------------------------------------------------------*

method @pic18_intermediate_incDecRegisterInCondition optimizeTestDecInc
  ??@symbolTableForOptimizations inSymbolTable
  ??@uint inLineIndex
  ?!@pic18_intermediate_instructionList ioGeneratedInstructionList
  ?!@string ioListFileContents
  ?!@bool ioOptimizationsDone
:
#--- Find destination label definition line
  [inSymbolTable searchKey !mTargetLabel ?@uint line ?*] ;
#--- Find first executable instruction
  @pic18_intermediate_instruction firstInstruction ;
  findFirstInstructionFromAddress !line + 1 !ioGeneratedInstructionList ?firstInstruction ?* ;
#--- Optimization from found first instruction
  cast firstInstruction :
  when == @pic18_intermediate_JUMP jump do
    [!?ioGeneratedInstructionList setMInstructionAtIndex
      ![@pic18_intermediate_incDecRegisterInCondition new
        !mInstructionLocation
        !mRegisterDescription
        ![jump mTargetLabel]
        !mIncrement
        !m_W_isDestination
        !mBranchIfZero
        !mUsesBra
      ]
      !inLineIndex
    ] ;
    ioOptimizationsDone := true ;
    ioListFileContents .= "  line " . [inLineIndex string] . ": label of " ;
    ioListFileContents .= "JUMP " ;
    if mIncrement then
      ioListFileContents .= "INCF " ;
    else
      ioListFileContents .= "DECF " ;
    end if ;
    ioListFileContents .= [mRegisterDescription mAssemblyString] ;
    if m_W_isDestination then
      ioListFileContents .= ", W " ;
    end if ;
    if mBranchIfZero then
      ioListFileContents .= "Z" ;
    else
      ioListFileContents .= "NZ" ;
    end if ;
    ioListFileContents .= " branching to JUMP replaced by second JUMP label\n" ;
  else
  end cast ;
end method ;

#----------------------------------------------------------------------------*

method @pic18_intermediate_registerComparisonCondition optimizeCMP
  ??@symbolTableForOptimizations inSymbolTable
  ??@uint inLineIndex
  ?!@pic18_intermediate_instructionList ioGeneratedInstructionList
  ?!@string ioListFileContents
  ?!@bool ioOptimizationsDone
:
#--- Find destination label definition line
  [inSymbolTable searchKey !mTargetLabel ?@uint line ?*] ;
#--- Find first executable instruction
  @pic18_intermediate_instruction firstInstruction ;
  findFirstInstructionFromAddress !line + 1 !ioGeneratedInstructionList ?firstInstruction ?* ;
#--- Optimization from found first instruction
  cast firstInstruction :
  when == @pic18_intermediate_JUMP jump do
    [!?ioGeneratedInstructionList setMInstructionAtIndex
      ![@pic18_intermediate_registerComparisonCondition new
        !mInstructionLocation
        !mRegisterDescription
        ![jump mTargetLabel]
        !mComparison
        !mUsesBra
      ]
      !inLineIndex
    ] ;
    ioOptimizationsDone := true ;
    ioListFileContents .= "  line " . [inLineIndex string] . ": label of " ;
    ioListFileContents .= "JUMP " ;
    ioListFileContents .= [mRegisterDescription mAssemblyString] ;
    ioListFileContents .= " " . [mComparison mnemonic] ;
    ioListFileContents .= " branching to JUMP replaced by second JUMP label\n" ;
  else
  end cast ;
end method ;

#----------------------------------------------------------------------------*

method @pic18_intermediate_instruction_BitTestSkip optimizeBitTestSkip
  ??@uint inLineIndex
  ?!@pic18_intermediate_instructionList ioGeneratedInstructionList
  ?!@string ioListFileContents
  ?!@bool ioOptimizationsDone
:
#--- First following instruction is a JUMP ?
  @string jumpLabel := "" ; # No By default
  @bool ok := false ;
  cast [ioGeneratedInstructionList mInstructionAtIndex !inLineIndex + 1] :
  when == @pic18_intermediate_JUMP jump do
    jumpLabel := [[jump mTargetLabel] string] ;
    ok := true ;
  else
  end cast ;
#--- Get second following instruction
  if (ok) then
    cast [ioGeneratedInstructionList mInstructionAtIndex !inLineIndex + 2] :
    when >= @pic18_intermediate_actualInstruction instruction do
      ok := [instruction instructionLength] == 1 ;
    else
      ok := false ;
    end cast ;
  end if ;
#--- Get third following instruction
  if (ok) then
    @pic18_intermediate_instruction thirdInstruction := [@pic18_intermediate_NULL new] ;
    @stringset reachedLabelSet ;
    findFirstInstructionOrLabelFromAddress !inLineIndex+3 !ioGeneratedInstructionList ?* ?reachedLabelSet ;
    ok := [reachedLabelSet hasKey !jumpLabel] ;
  end if ;
#--- If Ok, perform optimization
  if ok then
  #--- Complement Bit test instruction (at inLineIndex)
    [!?ioGeneratedInstructionList setMInstructionAtIndex
      ![@pic18_intermediate_instruction_BitTestSkip new
        !mInstructionLocation
        !not mSkipIfSet
        !mRegisterDescription
        !mBitNumber
      ]
      !inLineIndex
    ] ;
  #--- Copy instruction at (inLineIndex + 2) to (inLineIndex + 1)
    [!?ioGeneratedInstructionList setMInstructionAtIndex
      ![ioGeneratedInstructionList mInstructionAtIndex !inLineIndex + 2]
      !inLineIndex + 1
    ] ;
  #--- Delete instruction at (inLineIndex + 2)
    [!?ioGeneratedInstructionList setMInstructionAtIndex
      ![@pic18_intermediate_NULL new]
      !inLineIndex + 2
    ] ;
  #--- 
    ioOptimizationsDone := true ;
  #--- Log
    ioListFileContents .= "  lines " . [inLineIndex string] . " to " . [inLineIndex + 2 string] ;
    ioListFileContents .= " bit test skip, following by a JUMP skipping next instruction, is complemented\n" ;
  end if ;
end method ;

#----------------------------------------------------------------------------*
#                                                                            *
#               S E M A N T I C    A N A L Y S I S                           *
#                                                                            *
#----------------------------------------------------------------------------*

routine performOptimizations
  ?!@pic18_intermediate_instructionList ioGeneratedInstructionList
  ?!@string ioListFileContents
:
#-------------------------------- List file
  ioListFileContents .= ["" stringByLeftAndRightPadding !79 !'*'] . "\n" ;
  ioListFileContents .= "*" . ["CODE OPTIMIZATION" stringByLeftAndRightPadding !77 !' '] . "*\n" ;
  ioListFileContents .= ["" stringByLeftAndRightPadding !79 !'*'] . "\n\n" ;
#-------------------------------- Build label map
  @symbolTableForOptimizations symbolTable [emptyMap] ;
  foreach ioGeneratedInstructionList index lineIndex do
    [mInstruction defineLabel !?symbolTable !lineIndex] ;
  end foreach ;
#-------------------------------- Iterative optimizations
  @bool optimizationsDone := true ;
  @uint iteration := 0 ;
  loop [ioGeneratedInstructionList length] :
  while optimizationsDone
  do
    optimizationsDone := false ;
    iteration ++ ;
    ioListFileContents .= "Pass " . [iteration string] . ":\n" ;
  #--- Optimization loop
    foreach ioGeneratedInstructionList index idx do
      cast mInstruction :
      when == @pic18_intermediate_JUMP pic18_intermediate_JUMP do
        [pic18_intermediate_JUMP optimizeJUMP !symbolTable !idx !?ioGeneratedInstructionList !?ioListFileContents !?optimizationsDone] ;
      when == @pic18_intermediate_JSR pic18_intermediate_JSR do
        [pic18_intermediate_JSR optimizeJSR !symbolTable !idx !?ioGeneratedInstructionList !?ioListFileContents !?optimizationsDone] ;
      when == @pic18_intermediate_instruction_literalOperation pic18_intermediate_literal do
        [pic18_intermediate_literal optimizeMOVLW !idx !?ioGeneratedInstructionList !?ioListFileContents !?optimizationsDone] ;
      when == @pic18_intermediate_instruction_conditionalBranch pic18_intermediate_cc do
        [pic18_intermediate_cc optimizeBCC !symbolTable !idx !?ioGeneratedInstructionList !?ioListFileContents !?optimizationsDone] ;
#      when == @pic18_intermediate_jump_test_register pic18_intermediate_testReg do
#        [pic18_intermediate_testReg optimizeTestReg !symbolTable !idx !?ioGeneratedInstructionList !?ioListFileContents !?optimizationsDone] ;
      when == @pic18_intermediate_incDecRegisterInCondition pic18_intermediate_decInc do
        [pic18_intermediate_decInc optimizeTestDecInc !symbolTable !idx !?ioGeneratedInstructionList !?ioListFileContents !?optimizationsDone] ;
      when == @pic18_intermediate_registerComparisonCondition pic18_intermediate_CMP do
        [pic18_intermediate_CMP optimizeCMP !symbolTable !idx !?ioGeneratedInstructionList !?ioListFileContents !?optimizationsDone] ;
      when == @pic18_intermediate_instruction_BitTestSkip pic18_intermediate_bitTestSkip do
        [pic18_intermediate_bitTestSkip optimizeBitTestSkip !idx !?ioGeneratedInstructionList !?ioListFileContents !?optimizationsDone] ;
      else
      end cast ;
    end foreach ;
  #--- Delete non referenced labels
    @stringset referencedLabels [emptySet] ;
    foreach ioGeneratedInstructionList do
      [mInstruction enterReferencedLabel !?referencedLabels] ;
    end foreach ;
    foreach symbolTable do
      if not [referencedLabels hasKey ![lkey string]] then
        [symbolTable searchKey !lkey ?@uint definitionLine ?@bool isDeletable] ;
        if isDeletable then
          ioListFileContents .= "  line " . [definitionLine  string] . ": unreferenced label '" . lkey . "' is deleted\n" ;
          [!?ioGeneratedInstructionList setMInstructionAtIndex ![@pic18_intermediate_NULL new] !definitionLine] ;
          [!?symbolTable removeKey !lkey ?* ?*] ;
        end if ;
      end if ;
    end foreach ;
  #--- Dead code elimination
    @bool reachable := true ;
    @bool skippingInstruction := false ;
    @bool previousWasSkippingInstruction := false ;
    foreach ioGeneratedInstructionList index idx do
       if reachable | previousWasSkippingInstruction then
         reachable := [mInstruction nextInstructionIsReachable] ;
         previousWasSkippingInstruction := skippingInstruction ;
         skippingInstruction := [mInstruction isSkippingInstruction] ;
       else
         @bool isLABELorORG ; [mInstruction isLABELorORG ?isLABELorORG] ;
         @bool isNULL ; [mInstruction isNULL ?isNULL] ;
         if (not isLABELorORG) & (not isNULL) then
           ioListFileContents .= "  line " . [idx string] . ": unreachable instruction is deleted\n" ;
           [!?ioGeneratedInstructionList setMInstructionAtIndex ![@pic18_intermediate_NULL new] !idx] ;
         elsif isLABELorORG then
           reachable := true ;
           previousWasSkippingInstruction := false ;
           skippingInstruction := false ;
         end if ;
       end if ;
    end foreach ;
    if not optimizationsDone then
      ioListFileContents .= "  No optimization\n\n" ;
    #--- Compute code size
      @uint currentAddress := 0 ;
      foreach ioGeneratedInstructionList do
        [mInstruction setCurrentAddress !?currentAddress] ;
      end foreach ;
      ioListFileContents .= "Minimum code size: " . [currentAddress string] . " bytes\n\n" ;
    end if ;
  end loop ;
end routine ;

#----------------------------------------------------------------------------*

end semantics ;
