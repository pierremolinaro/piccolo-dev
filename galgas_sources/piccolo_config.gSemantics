semantics piccolo_config :
import "piccoloDevice_semantics.gSemantics" ;

#----------------------------------------------------------------------------*

map @configFieldMap {
  @lstring mRegisterName ;
  @luint mMaskValue ;
  @lstring mDescription ;
  @fieldSettingMap mFieldSettingMap ;
  insert insertKey error message "the '%K' configuration field is already declared in %L" ;
  search searchKey error message "the '%K' configuration field is not declared" ;
}

#----------------------------------------------------*

routine actualBuildConfig
 ??@configRegisterMap inConfigRegisterMap
 ??@configDefinitionList inConfigDefinitionList
 ?!@string ioListFileContents
 !@actualConfigurationMap outActualConfigurationMap
:
#--- From config description of device, build dictionary of all settings
# This should succeed, since absence of potentiel setting name conflict has been
# checked when parsing device definition string.
  outActualConfigurationMap := [@actualConfigurationMap emptyMap] ;
  @configFieldMap configFieldMap [emptyMap] ;
  foreach inConfigRegisterMap do
    @uint defaultValue := (1 << [mRegisterWidth uint]) - 1 ;
    [!? outActualConfigurationMap insertKey !lkey ![mRegisterAddress uint] !defaultValue] ;
    @lstring registerName := lkey ;
    foreach mConfigRegisterMaskMap prefixedby other_ do
      [!?configFieldMap insertKey !other_lkey !registerName !other_mMaskValue !other_mDescription !other_mFieldSettingMap] ;
    end foreach ;
  end foreach ;
#--- Analyze config parts
  @stringset actualSettingNameSet [emptySet] ;
  foreach inConfigDefinitionList do
    foreach mSettingList do
      if [actualSettingNameSet hasKey ![mSettingName string]] then
        error mSettingName: "the '" . mSettingName . "' setting is already defined" ;
      end if ;
      actualSettingNameSet += ![mSettingName string] ;
      @lstring registerName ;
      @luint maskValue ;
      @lstring settingDescription ;
      @fieldSettingMap fieldSettingMap ;
      [configFieldMap searchKey !mSettingName ?registerName ?maskValue ?settingDescription ?fieldSettingMap] ;
      if [fieldSettingMap hasKey ![mSettingValue string]] then
        @uint settingValue ;
        @uint settingMask ;
        [fieldSettingMap searchKey !mSettingValue ?settingValue ?settingMask] ;
        @uint registerValue ;
        [outActualConfigurationMap searchKey !registerName ?* ?registerValue] ;
        registerValue := (registerValue & ~ settingMask) | settingValue ;
        [!?outActualConfigurationMap setMRegisterValueForKey !registerValue ![registerName string]] ;
      elsif [fieldSettingMap count] > 0 then
        @string errorMessage := "the '" . mSettingValue . "' value is not defined for configuration setting '"
        . mSettingName . "'; possible values are:" ;
        foreach fieldSettingMap do
          errorMessage .= "\n-  \"" . lkey . "\"" ;
        end foreach ;
        error mSettingValue: errorMessage ;
      end if ;
    end foreach ;
  end foreach ;
#--- Check if all settings are actually defined
  const @stringset notDefinedSetting := [configFieldMap allKeys] - actualSettingNameSet ;
  if [notDefinedSetting count] > 0 then
    @string errorMessage := "the following configuration settings are not defined:" ;
    foreach notDefinedSetting do
      errorMessage .= "\n-  " . key ;
    end foreach ;
    error here : errorMessage ;
  end if ;
#--- Display conguration information in log file
  foreach outActualConfigurationMap do
    ioListFileContents .= "Register '" . lkey . "' at " . [mRegisterAddress hexString] . " set to " . [mRegisterValue hexString] . "\n" ;
    @configRegisterMaskMap configRegisterMaskMap ;
    [inConfigRegisterMap searchKey !lkey ?* ?* ?configRegisterMaskMap ?*] ;
    foreach configRegisterMaskMap prefixedby others_ do
      @lstring settingName := others_lkey ;
      foreach others_mFieldSettingMap prefixedby second_ do
        if (mRegisterValue & [others_mMaskValue uint]) == second_mValue then
          ioListFileContents .= "  setting '" . settingName . "' set to " . [second_mValue hexString] . " (\"" . second_lkey . "\")\n" ;
        end if ;
      end foreach ;
    end foreach ;
    ioListFileContents .= "\n" ;
  end foreach ;
#--- Detect illegal configurations
  foreach outActualConfigurationMap do
    @illegalMaskList illegalMaskList ;
    [inConfigRegisterMap searchKey !lkey ?* ?* ?* ?illegalMaskList] ;
     foreach illegalMaskList do
      if (mRegisterValue & [mIllegalMask uint]) == [mIllegalValue uint] then
        error here:"illegal setting for '" . lkey . "' register: " . mDescription ;
        ioListFileContents .= "*** ILLEGAL SETTING for register '" . lkey . "' ***\n" ;
        ioListFileContents .= "  message: " . mDescription . "\n\n" ;
      end if ;
    end foreach ;
  end foreach ;
end routine ;

#----------------------------------------------------*

routine buildConfig
 ??@configRegisterMap inConfigRegisterMap
 ??@configDefinitionList inConfigDefinitionList
 ?!@string ioListFileContents
 !@actualConfigurationMap outActualConfigurationMap
:
#--- Log
  ioListFileContents .= ["" stringByLeftAndRightPadding !79 !'*'] . "\n" ;
  ioListFileContents .= "*" . ["MICROCONTROLLER CONFIGURATION" stringByLeftAndRightPadding !77 !' '] . "*\n" ;
  ioListFileContents .= ["" stringByLeftAndRightPadding !79 !'*'] . "\n\n" ;
#--- 
  if [inConfigDefinitionList length] > 0 then
    actualBuildConfig
      !inConfigRegisterMap
      !inConfigDefinitionList
      !?ioListFileContents
      ?outActualConfigurationMap
    ;
  else
    outActualConfigurationMap := [@actualConfigurationMap emptyMap] ;
    ioListFileContents .= "No configuration.\n\n" ;
  end if ;
end routine ;

#----------------------------------------------------*

end semantics ;
