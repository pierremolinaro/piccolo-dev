semantics piccolo_decompose_structured_instructions :
import semantics piccolo_metamodel in "piccolo_metamodel.gSemantics" ;
import semantics ipic_metamodel in "ipic_metamodel.gSemantics" ;
import "ipic_metamodel.gSemantics" ;

#----------------------------------------------------*
#           DECOMPOSE COMPLEX INSTRUCTION            *
#----------------------------------------------------*

abstract method @piccolo_instruction.buildIPICinstructionList
  ?!@uint unused ioLocalLabelIndex
  ?!@ipic_instructionList unused ioGeneratedInstructionList
;

#----------------------------------------------------*

override method @pseudo_LABEL.buildIPICinstructionList
  ?!@uint unused ioLocalLabelIndex
  ?!@ipic_instructionList ioGeneratedInstructionList
:
  ioGeneratedInstructionList += ![@ipic_pseudo_LABEL new
    !mTargetLabel
  ] ;
end method ;

#----------------------------------------------------*

override method @instruction_FDA.buildIPICinstructionList
  ?!@uint unused ioLocalLabelIndex
  ?!@ipic_instructionList ioGeneratedInstructionList
:
  ioGeneratedInstructionList += ![@ipic_instruction_FDA new
    !mInstructionLocation
    !mInstruction_FDA_base_code
    !mRegisterExpression
    !m_W_isDestination
  ] ;
end method ;

#----------------------------------------------------*

override method @instruction_FA.buildIPICinstructionList
  ?!@uint unused ioLocalLabelIndex
  ?!@ipic_instructionList ioGeneratedInstructionList
:
  ioGeneratedInstructionList += ![@ipic_instruction_FA new
    !mInstructionLocation
    !mFAinstruction
    !mRegisterExpression
  ] ;
end method ;

#----------------------------------------------------*

override method @instruction_MOVFF.buildIPICinstructionList
  ?!@uint unused ioLocalLabelIndex
  ?!@ipic_instructionList ioGeneratedInstructionList
:
  ioGeneratedInstructionList += ![@ipic_instruction_MOVFF new
    !mInstructionLocation
    !mSourceRegisterName
    !mDestinationRegisterName
  ] ;
end method ;

#----------------------------------------------------*

override method @instruction_FBA.buildIPICinstructionList
  ?!@uint unused ioLocalLabelIndex
  ?!@ipic_instructionList ioGeneratedInstructionList
:
  ioGeneratedInstructionList += ![@ipic_instruction_FBA new
    !mInstructionLocation
    !mBitOrientedOp
    !mRegisterExpression
    !mBitNumber
  ] ;
end method ;

#----------------------------------------------------*

override method @instruction_conditionalBranch.buildIPICinstructionList
  ?!@uint unused ioLocalLabelIndex
  ?!@ipic_instructionList ioGeneratedInstructionList
:
  ioGeneratedInstructionList += ![@ipic_instruction_conditionalBranch new
    !mInstructionLocation
    !mBranchMode
    !mConditionalBranch
    !mTargetLabel
  ] ;
end method ;

#----------------------------------------------------*

override method @instruction_BRA.buildIPICinstructionList
  ?!@uint unused ioLocalLabelIndex
  ?!@ipic_instructionList ioGeneratedInstructionList
:
  ioGeneratedInstructionList += ![@ipic_instruction_BRA new
    !mInstructionLocation
    !mTargetLabel
  ] ;
end method ;

#----------------------------------------------------*

override method @instruction_CALL.buildIPICinstructionList
  ?!@uint unused ioLocalLabelIndex
  ?!@ipic_instructionList ioGeneratedInstructionList
:
  ioGeneratedInstructionList += ![@ipic_instruction_CALL new
    !mInstructionLocation
    !mTargetLabel
  ] ;
end method ;

#----------------------------------------------------*

override method @instruction_GOTO.buildIPICinstructionList
  ?!@uint unused ioLocalLabelIndex
  ?!@ipic_instructionList ioGeneratedInstructionList
:
  ioGeneratedInstructionList += ![@ipic_instruction_GOTO new
    !mInstructionLocation
    !mTargetLabel
  ] ;
end method ;

#----------------------------------------------------*

override method @instruction_RCALL.buildIPICinstructionList
  ?!@uint unused ioLocalLabelIndex
  ?!@ipic_instructionList ioGeneratedInstructionList
:
  ioGeneratedInstructionList += ![@ipic_instruction_RCALL new
    !mInstructionLocation
    !mTargetLabel
  ] ;
end method ;

#----------------------------------------------------*

override method @instruction_CLRWDT.buildIPICinstructionList
  ?!@uint unused ioLocalLabelIndex
  ?!@ipic_instructionList ioGeneratedInstructionList
:
  ioGeneratedInstructionList += ![@ipic_instruction_CLRWDT new
    !mInstructionLocation
  ] ;
end method ;

#----------------------------------------------------*

override method @instruction_DAW.buildIPICinstructionList
  ?!@uint unused ioLocalLabelIndex
  ?!@ipic_instructionList ioGeneratedInstructionList
:
  ioGeneratedInstructionList += ![@ipic_instruction_DAW new
    !mInstructionLocation
  ] ;
end method ;

#----------------------------------------------------*

override method @instruction_NOP.buildIPICinstructionList
  ?!@uint unused ioLocalLabelIndex
  ?!@ipic_instructionList ioGeneratedInstructionList
:
  ioGeneratedInstructionList += ![@ipic_instruction_NOP new
    !mInstructionLocation
  ] ;
end method ;

#----------------------------------------------------*

override method @instruction_POP.buildIPICinstructionList
  ?!@uint unused ioLocalLabelIndex
  ?!@ipic_instructionList ioGeneratedInstructionList
:
  ioGeneratedInstructionList += ![@ipic_instruction_POP new
    !mInstructionLocation
  ] ;
end method ;

#----------------------------------------------------*

override method @instruction_PUSH.buildIPICinstructionList
  ?!@uint unused ioLocalLabelIndex
  ?!@ipic_instructionList ioGeneratedInstructionList
:
  ioGeneratedInstructionList += ![@ipic_instruction_PUSH new
    !mInstructionLocation
  ] ;
end method ;

#----------------------------------------------------*

override method @instruction_RESET.buildIPICinstructionList
  ?!@uint unused ioLocalLabelIndex
  ?!@ipic_instructionList ioGeneratedInstructionList
:
  ioGeneratedInstructionList += ![@ipic_instruction_RESET new
    !mInstructionLocation
  ] ;
end method ;

#----------------------------------------------------*

override method @instruction_RETURN.buildIPICinstructionList
  ?!@uint unused ioLocalLabelIndex
  ?!@ipic_instructionList ioGeneratedInstructionList
:
  ioGeneratedInstructionList += ![@ipic_instruction_RETURN new
    !mInstructionLocation
  ] ;
end method ;

#----------------------------------------------------*

override method @instruction_RETFIE.buildIPICinstructionList
  ?!@uint unused ioLocalLabelIndex
  ?!@ipic_instructionList ioGeneratedInstructionList
:
  ioGeneratedInstructionList += ![@ipic_instruction_RETFIE new
    !mInstructionLocation
    !mFastReturn
  ] ;
end method ;

#----------------------------------------------------*

override method @instruction_SLEEP.buildIPICinstructionList
  ?!@uint unused ioLocalLabelIndex
  ?!@ipic_instructionList ioGeneratedInstructionList
:
  ioGeneratedInstructionList += ![@ipic_instruction_SLEEP new
    !mInstructionLocation
  ] ;
end method ;

#----------------------------------------------------*

override method @instruction_literalOperation.buildIPICinstructionList
  ?!@uint unused ioLocalLabelIndex
  ?!@ipic_instructionList ioGeneratedInstructionList
:
  ioGeneratedInstructionList += ![@ipic_instruction_literalOperation new
    !mInstructionLocation
    !mLiteralInstruction
    !mLiteralValue
  ] ;
end method ;

#----------------------------------------------------*

override method @instruction_LFSR.buildIPICinstructionList
  ?!@uint unused ioLocalLabelIndex
  ?!@ipic_instructionList ioGeneratedInstructionList
:
  ioGeneratedInstructionList += ![@ipic_instruction_LFSR new
    !mInstructionLocation
    !mFSRindex
    !mRegisterExpression
  ] ;
end method ;

#----------------------------------------------------*

override method @instruction_MOVLB.buildIPICinstructionList
  ?!@uint unused ioLocalLabelIndex
  ?!@ipic_instructionList ioGeneratedInstructionList
:
  ioGeneratedInstructionList += ![@ipic_instruction_MOVLB new
    !mInstructionLocation
    !mPageIndex
  ] ;
end method ;

#----------------------------------------------------*

override method @instruction_MOVAW.buildIPICinstructionList
  ?!@uint unused ioLocalLabelIndex
  ?!@ipic_instructionList ioGeneratedInstructionList
:
  ioGeneratedInstructionList += ![@ipic_instruction_MOVAW new
    !mInstructionLocation
    !mRegisterExpression
  ] ;
end method ;

#----------------------------------------------------*

override method @instruction_TBLRD.buildIPICinstructionList
  ?!@uint unused ioLocalLabelIndex
  ?!@ipic_instructionList ioGeneratedInstructionList
:
  ioGeneratedInstructionList += ![@ipic_instruction_TBLRD new
    !mInstructionLocation
    !mOption
  ] ;
end method ;

#----------------------------------------------------*

override method @instruction_TBLWT.buildIPICinstructionList
  ?!@uint unused ioLocalLabelIndex
  ?!@ipic_instructionList ioGeneratedInstructionList
:
  ioGeneratedInstructionList += ![@ipic_instruction_TBLWT new
    !mInstructionLocation
    !mOption
  ] ;
end method ;

#----------------------------------------------------*

override method @instruction_computed_retlw.buildIPICinstructionList
  ?!@uint unused ioLocalLabelIndex
  ?!@ipic_instructionList ioGeneratedInstructionList
:
  ioGeneratedInstructionList += ![@ipic_instruction_computed_retlw new
    !mInstructionLocation
    !mLiteralValues
    !mUsesRelativeCall
  ] ;
end method ;

#----------------------------------------------------*

override method @instruction_MNOP.buildIPICinstructionList
  ?!@uint unused ioLocalLabelIndex
  ?!@ipic_instructionList ioGeneratedInstructionList
:
  ioGeneratedInstructionList += ![@ipic_instruction_MNOP new
    !mInstructionLocation
    !mOccurrenceFactor
  ] ;
end method ;

#----------------------------------------------------*

override method @instruction_computed_bra.buildIPICinstructionList
  ?!@uint unused ioLocalLabelIndex
  ?!@ipic_instructionList ioGeneratedInstructionList
:
  ioGeneratedInstructionList += ![@ipic_instruction_computed_bra new
    !mInstructionLocation
    !mTargetLabels
    !mUsesRelativeCall
  ] ;
end method ;

#----------------------------------------------------*

override method @instruction_computed_goto.buildIPICinstructionList
  ?!@uint unused ioLocalLabelIndex
  ?!@ipic_instructionList ioGeneratedInstructionList
:
  ioGeneratedInstructionList += ![@ipic_instruction_computed_goto new
    !mInstructionLocation
    !mTargetLabels
    !mUsesRelativeCall
  ] ;
end method ;

#----------------------------------------------------*

override method @instruction_FOREVER.buildIPICinstructionList
  ?!@uint ioLocalLabelIndex
  ?!@ipic_instructionList ioGeneratedInstructionList
:
  @string label0 := "_label_" . [ioLocalLabelIndex string] ; 
  ioLocalLabelIndex ++ ;
  ioGeneratedInstructionList += ![@ipic_pseudo_LABEL new ![@lstring new !label0 !mInstructionLocation]] ;
  foreach mInstructionList do
    [mInstruction buildIPICinstructionList !?ioLocalLabelIndex !?ioGeneratedInstructionList] ;
  end foreach ;
  ioGeneratedInstructionList += ![@ipic_instruction_BRA new !mInstructionLocation ![@lstring new !label0 !mInstructionLocation]] ;
end method ;

#----------------------------------------------------*

override method @instruction_IF_FA_SEMI_COLON.buildIPICinstructionList
  ?!@uint ioLocalLabelIndex
  ?!@ipic_instructionList ioGeneratedInstructionList
:
  @FA_instruction_base_code baseCode ;
  switch mOpCode
    when CPFSEQ : baseCode := [@FA_instruction_base_code CPFSEQ] ;
    when CPFSGT : baseCode := [@FA_instruction_base_code CPFSGT] ;
    when CPFSLT : baseCode := [@FA_instruction_base_code CPFSLT] ;
    when TSTFSZ : baseCode := [@FA_instruction_base_code TSTFSZ] ;
  end switch ;
  ioGeneratedInstructionList += ![@ipic_instruction_FA new
    !mInstructionLocation
    !baseCode
    !mRegisterExpression
  ] ;
#--- Append Instruction
  [mInstruction buildIPICinstructionList !?ioLocalLabelIndex !?ioGeneratedInstructionList] ;
end method ;

#----------------------------------------------------*

override method @instruction_IF_BitTest.buildIPICinstructionList
  ?!@uint ioLocalLabelIndex
  ?!@ipic_instructionList ioGeneratedInstructionList
:
  @bit_oriented_op baseCode ;
  if mSkipIfSet then
    baseCode := [@bit_oriented_op BTFSS] ;
  else
    baseCode := [@bit_oriented_op BTFSC] ;
  end if ;
  ioGeneratedInstructionList += ![@ipic_instruction_FBA new
    !mInstructionLocation
    !baseCode
    !mRegisterExpression
    !mBitNumber
  ] ;
#--- Append Instruction
  [mInstruction buildIPICinstructionList !?ioLocalLabelIndex !?ioGeneratedInstructionList] ;
end method ;

#----------------------------------------------------*

abstract method @conditionExpression.buildIPICinstructionForCondition
  ?!@uint ioLocalLabelIndex
  ??@bool inComplementaryBranch
  ??@location inInstructionLocation
  ??@string inTargetLabel
  ?!@ipic_instructionList ioGeneratedInstructionList
;

#----------------------------------------------------*

override method @bcc_in_structured_if_condition.buildIPICinstructionForCondition
  ?!@uint unused ioLocalLabelIndex
  ??@bool inComplementaryBranch
  ??@location inInstructionLocation
  ??@string inTargetLabel
  ?!@ipic_instructionList ioGeneratedInstructionList
:
  @conditional_branch condition ;
  if inComplementaryBranch then
    getComplementaryBcc !mCondition ?condition ;
  else
    condition := mCondition ;
  end if ;
  ioGeneratedInstructionList += ![@ipic_instruction_conditionalBranch new
    !inInstructionLocation
    ![@condionalBranchMode native]
    !condition
    ![@lstring new !inTargetLabel !inInstructionLocation]
  ] ;
end method ;

#----------------------------------------------------*

override method @registerComparisonCondition.buildIPICinstructionForCondition
  ?!@uint unused ioLocalLabelIndex
  ??@bool inComplementaryBranch
  ??@location inInstructionLocation
  ??@string inTargetLabel
  ?!@ipic_instructionList ioGeneratedInstructionList
:
  @registerComparison comparison ;
  if inComplementaryBranch then
    getRegisterComparisonComplementary !mComparison ?comparison ;
  else
    comparison := mComparison ;
  end if ;
  ioGeneratedInstructionList += ![@ipic_registerComparisonCondition new
    !inInstructionLocation
    !mRegisterExpression
    !inTargetLabel
    !comparison
    !true # By Default, uses BRA
  ] ;
end method ;

#----------------------------------------------------*

override method @incDecRegisterInCondition.buildIPICinstructionForCondition
  ?!@uint unused ioLocalLabelIndex
  ??@bool inComplementaryBranch
  ??@location inInstructionLocation
  ??@string inTargetLabel
  ?!@ipic_instructionList ioGeneratedInstructionList
:
  ioGeneratedInstructionList += ![@ipic_incDecRegisterInCondition new
    !inInstructionLocation
    !mRegisterExpression
    !inTargetLabel
    !mIncrement
    !m_W_isDestination
    !mBranchIfZero ^ inComplementaryBranch
    !true # By Default, uses BRA
  ] ;
end method ;

#----------------------------------------------------*

override method @registerTestCondition.buildIPICinstructionForCondition
  ?!@uint unused ioLocalLabelIndex
  ??@bool inComplementaryBranch
  ??@location inInstructionLocation
  ??@string inTargetLabel
  ?!@ipic_instructionList ioGeneratedInstructionList
:
  ioGeneratedInstructionList += ![@ipic_jump_test_register new
    !inInstructionLocation
    !mRegisterExpression
    !inTargetLabel
    !mBranchIfZero ^ inComplementaryBranch
    !true # By Default, uses BRA
  ] ;
end method ;

#----------------------------------------------------*

override method @negateCondition.buildIPICinstructionForCondition
  ?!@uint ioLocalLabelIndex
  ??@bool inComplementaryBranch
  ??@location inInstructionLocation
  ??@string inTargetLabel
  ?!@ipic_instructionList ioGeneratedInstructionList
:
  [mCondition buildIPICinstructionForCondition
    !?ioLocalLabelIndex
    !not inComplementaryBranch
    !inInstructionLocation
    !inTargetLabel
    !?ioGeneratedInstructionList
  ] ;
end method ;

#----------------------------------------------------*

override method @andCondition.buildIPICinstructionForCondition
  ?!@uint ioLocalLabelIndex
  ??@bool inComplementaryBranch
  ??@location inInstructionLocation
  ??@string inTargetLabel
  ?!@ipic_instructionList ioGeneratedInstructionList
:
  if inComplementaryBranch then
    [mLeftExpression buildIPICinstructionForCondition
      !?ioLocalLabelIndex
      !true
      !inInstructionLocation
      !inTargetLabel
      !?ioGeneratedInstructionList
    ] ;
    [mRightExpression buildIPICinstructionForCondition
      !?ioLocalLabelIndex
      !true
      !inInstructionLocation
      !inTargetLabel
      !?ioGeneratedInstructionList
    ] ;
  else
    @string label0 := "_label_" . [ioLocalLabelIndex string] ; ioLocalLabelIndex ++ ;
    [mLeftExpression buildIPICinstructionForCondition
      !?ioLocalLabelIndex
      !true
      !inInstructionLocation
      !label0
      !?ioGeneratedInstructionList
    ] ;
    [mRightExpression buildIPICinstructionForCondition
      !?ioLocalLabelIndex
      !false
      !inInstructionLocation
      !inTargetLabel
      !?ioGeneratedInstructionList
    ] ;
    ioGeneratedInstructionList += ![@ipic_pseudo_LABEL new ![@lstring new !label0 !inInstructionLocation]] ;
  end if ;
end method ;

#----------------------------------------------------*

override method @bitTest_in_structured_if_condition.buildIPICinstructionForCondition
  ?!@uint unused ioLocalLabelIndex
  ??@bool inComplementaryBranch
  ??@location inInstructionLocation
  ??@string inTargetLabel
  ?!@ipic_instructionList ioGeneratedInstructionList
:
  @bit_oriented_op op ;
  if mBTFSSinstruction ^ inComplementaryBranch then
    op := [@bit_oriented_op BTFSS] ;
  else
    op := [@bit_oriented_op BTFSC] ;
  end if ;
  ioGeneratedInstructionList += ![@ipic_instruction_FBA new
    !inInstructionLocation
    !op
    !mRegisterExpression
    !mBitNumber
  ] ;
  ioGeneratedInstructionList += ![@ipic_instruction_BRA new !inInstructionLocation ![@lstring new !inTargetLabel !inInstructionLocation]] ;
end method ;

#----------------------------------------------------*

override method @instruction_structured_if.buildIPICinstructionList
  ?!@uint ioLocalLabelIndex
  ?!@ipic_instructionList ioGeneratedInstructionList
:
#---
  @string label_nextCondition := "_label_" . [ioLocalLabelIndex string] ; ioLocalLabelIndex ++ ;
  @string label_endOfIfinstruction := "_label_" . [ioLocalLabelIndex string] ; ioLocalLabelIndex ++ ;
#--- Translate condition
  [mIfCondition buildIPICinstructionForCondition !?ioLocalLabelIndex !true !mInstructionLocation !label_nextCondition !?ioGeneratedInstructionList] ;
#--- 'then' instructions
  foreach mThenInstructionList do
    [mInstruction buildIPICinstructionList !?ioLocalLabelIndex !?ioGeneratedInstructionList] ;
  end foreach ;
#--- 'elsif' parts
  foreach mElsifPartList do
    ioGeneratedInstructionList += ![@ipic_instruction_BRA new !mInstructionLocation ![@lstring new !label_endOfIfinstruction !mInstructionLocation]] ;
    ioGeneratedInstructionList += ![@ipic_pseudo_LABEL new ![@lstring new !label_nextCondition !mInstructionLocation]] ;
    label_nextCondition := "_label_" . [ioLocalLabelIndex string] ; ioLocalLabelIndex ++ ;
    [mCondition buildIPICinstructionForCondition !?ioLocalLabelIndex !true !mInstructionLocation !label_nextCondition !?ioGeneratedInstructionList] ;
    foreach mInstructionList do
      [mInstruction buildIPICinstructionList !?ioLocalLabelIndex !?ioGeneratedInstructionList] ;
    end foreach ;
  end foreach ;
#--- 'else' instructions
  if [mElseInstructionList length] > 0 then
    ioGeneratedInstructionList += ![@ipic_instruction_BRA new !mInstructionLocation ![@lstring new !label_endOfIfinstruction !mInstructionLocation]] ;
  end if ;
  ioGeneratedInstructionList += ![@ipic_pseudo_LABEL new ![@lstring new !label_nextCondition !mInstructionLocation]] ;
  foreach mElseInstructionList do
    [mInstruction buildIPICinstructionList !?ioLocalLabelIndex !?ioGeneratedInstructionList] ;
  end foreach ;
  if ([mElseInstructionList length] > 0) | ([mElsifPartList length] > 0) then
    ioGeneratedInstructionList += ![@ipic_pseudo_LABEL new ![@lstring new !label_endOfIfinstruction !mInstructionLocation]] ;
  end if ;
end method ;

#----------------------------------------------------*

override method @instruction_do_while.buildIPICinstructionList
  ?!@uint ioLocalLabelIndex
  ?!@ipic_instructionList ioGeneratedInstructionList
:
#--- Generate label
  @string labelInstructionBegin := "_label_" . [ioLocalLabelIndex string] ; ioLocalLabelIndex ++ ;
#--- Define label
  ioGeneratedInstructionList += ![@ipic_pseudo_LABEL new ![@lstring new !labelInstructionBegin !mInstructionLocation]] ;
#--- Repeated instructions
  foreach mRepeatedInstructionList do
    [mInstruction buildIPICinstructionList !?ioLocalLabelIndex !?ioGeneratedInstructionList] ;
  end foreach ;
#--- while parts
  foreach mWhilePartList do
    if [mInstructionList length] == 0 then
      [mCondition buildIPICinstructionForCondition !?ioLocalLabelIndex !false !mInstructionLocation !labelInstructionBegin !?ioGeneratedInstructionList] ;
    else
      @string nextBranchLabel := "_label_" . [ioLocalLabelIndex string] ; ioLocalLabelIndex ++ ;
      [mCondition buildIPICinstructionForCondition !?ioLocalLabelIndex !true !mInstructionLocation !nextBranchLabel !?ioGeneratedInstructionList] ;
      foreach mInstructionList do
        [mInstruction buildIPICinstructionList !?ioLocalLabelIndex !?ioGeneratedInstructionList] ;
      end foreach ;
      ioGeneratedInstructionList += ![@ipic_pseudo_LABEL new ![@lstring new !nextBranchLabel !mInstructionLocation]] ;
    end if ;
  end foreach ;
end method ;

#----------------------------------------------------*

override method @instruction_IF_IncDec.buildIPICinstructionList
  ?!@uint ioLocalLabelIndex
  ?!@ipic_instructionList ioGeneratedInstructionList
:
#---
#---
  @instruction_FDA_base_code baseCode ;
  if mIncrement & mSkipIfZero then
    baseCode := [@instruction_FDA_base_code INCFSZ] ;
  elsif mIncrement & not mSkipIfZero then
    baseCode := [@instruction_FDA_base_code INFSNZ] ;
  elsif (not mIncrement) & mSkipIfZero then
    baseCode := [@instruction_FDA_base_code DECFSZ] ;
  else
    baseCode := [@instruction_FDA_base_code DCFSNZ] ;
  end if ;
  ioGeneratedInstructionList += ![@ipic_instruction_FDA new
    !mInstructionLocation
    !baseCode
    !mRegisterExpression
    !m_W_isDestination
  ] ;
#--- Repeated instructions
  [mInstruction buildIPICinstructionList !?ioLocalLabelIndex !?ioGeneratedInstructionList] ;
end method ;

#----------------------------------------------------*

routine buildAssemblyInstructionList
  ??@piccoloModel inPiccoloModel
  ??@bool inHasHighInterrupt
  ??@bool inHasLowInterrupt
  !@ipic_instructionList outGeneratedInstructionList
:
#-------------------------------- First Pass
  outGeneratedInstructionList := [@ipic_instructionList emptyList] ;
  @uint localLabelIndex := 0 ;
#--- At zero, generate 'goto main' instruction
  if inHasLowInterrupt | inHasHighInterrupt then
    outGeneratedInstructionList += ![@ipic_instruction_GOTO new !here ![@lstring new !"main" !here]] ;
  end if ;
#--- At 0x08, generate 'bra _fast_interrupt' instruction, if both interrupt are defined
  if inHasLowInterrupt & inHasHighInterrupt then
    outGeneratedInstructionList += ![@ipic_pseudo_ORG new !0x8] ;
    outGeneratedInstructionList += ![@ipic_instruction_BRA new !here ![@lstring new !"_fast_interrupt" !here]] ;
  end if ;
#--- First, handle low interrupt (if any)
  if inHasLowInterrupt then
    outGeneratedInstructionList += ![@ipic_pseudo_ORG new !0x18] ;
    foreach [inPiccoloModel mInterruptDefinitionList] do
      if [mInterruptName string] == "low" then
        foreach mInstructionList do
          [mInstruction buildIPICinstructionList !?localLabelIndex !? outGeneratedInstructionList] ;
        end foreach ;
      #--- Ajouter l'instruction REFFIE
        outGeneratedInstructionList += ![@ipic_instruction_RETFIE new !here !true] ;
      end if ;
    end foreach ;
  end if ;
#--- Then, handle high interrupt (if any)
  if inHasHighInterrupt then
    if inHasLowInterrupt then
      outGeneratedInstructionList += ![@ipic_pseudo_LABEL new ![@lstring new !"_fast_interrupt" !here]] ;
    else
      outGeneratedInstructionList += ![@ipic_pseudo_ORG new !0x8] ;
    end if ;
    foreach [inPiccoloModel mInterruptDefinitionList] do
      if [mInterruptName string] == "high" then
        foreach mInstructionList do
          [mInstruction buildIPICinstructionList !?localLabelIndex !?outGeneratedInstructionList] ;
        end foreach ;
      #--- Ajouter l'instruction REFFIE
        outGeneratedInstructionList += ![@ipic_instruction_RETFIE new !here !true] ;
      end if ;
    end foreach ;
  end if ;
#--- Handle routines
  foreach [inPiccoloModel mRoutineDefinitionList] do
    outGeneratedInstructionList += ![@ipic_pseudo_LABEL new !mRoutineName] ;
    foreach mInstructionList do
      [mInstruction buildIPICinstructionList !?localLabelIndex !? outGeneratedInstructionList] ;
    end foreach ;
  end foreach ;
#--- computed_goto2 routine needed ?
  if [inPiccoloModel mNeedsComputedGoto4] then
    outGeneratedInstructionList += ![@ipic_pseudo_LABEL new ![@lstring new !"_computed_goto_4" !here]] ;
    outGeneratedInstructionList += ![@ipic_instruction_FDA new
      !here
      ![@instruction_FDA_base_code ADDWF]
      ![@registerExpression new ![@lstring new !"WREG" !here] ![@luint new !0 !here]]
      !true
    ] ; # ADDWF WREG, W
  end if ;
  if [inPiccoloModel mNeedsComputedGoto2] then
    outGeneratedInstructionList += ![@ipic_pseudo_LABEL new ![@lstring new !"_computed_goto_2" !here]] ;
  end if ;
  if [inPiccoloModel mNeedsComputedGoto2] | [inPiccoloModel mNeedsComputedGoto4] then
    outGeneratedInstructionList += ![@ipic_instruction_FDA new
      !here
      ![@instruction_FDA_base_code ADDWF]
      ![@registerExpression new ![@lstring new !"WREG" !here] ![@luint new !0 !here]]
      !true
    ] ; # ADDWF WREG, W
    outGeneratedInstructionList += ![@ipic_instruction_FDA new
      !here
      ![@instruction_FDA_base_code ADDWF]
      ![@registerExpression new ![@lstring new !"TOSL" !here] ![@luint new !0 !here]]
      !false
    ] ; # ADDWF TOSL, F
    outGeneratedInstructionList += ![@ipic_instruction_literalOperation new
      !here
      ![@literal_instruction_opcode MOVLW]
      ![@luint new !0 !here]
    ] ; # MOVLW 0
    outGeneratedInstructionList += ![@ipic_instruction_FDA new
      !here
      ![@instruction_FDA_base_code ADDWFC]
      ![@registerExpression new ![@lstring new !"TOSH" !here] ![@luint new !0 !here]]
      !false
    ] ; # ADDWFC TOSH, F
    outGeneratedInstructionList += ![@ipic_instruction_RETURN new
      !here
    ] ; # RETURN 0
  end if ;
end routine ;

#----------------------------------------------------*

end semantics ;
