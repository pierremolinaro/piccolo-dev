semantics piccolo_metamodel :
  import option piccolo_options in "piccolo_options.ggs" ;

#----------------------------------------------------*

abstract class @instruction {
  @location mInstructionLocation ;

  method instructionLength !@uint outLength :
    outLength := 2 ;
  end method ;
}

#----------------------------------------------------*

class @pseudo_ORG extends @instruction {
  @uint mOrigin ;

  override method instructionLength !@uint outLength :
    outLength := 0 ;
  end method ;
}

#----------------------------------------------------*

list @instructionList {
  @instruction mInstruction ;
}

#----------------------------------------------------*

class @registerExpression {
  @lstring mRegisterName ;
  @luint mOffset ;
}

#----------------------------------------------------*

enum @instruction_FDA_base_code {
  constructor ADDWF ;
  constructor ADDWFC ;
  constructor ANDWF ;
  constructor COMF ;
  constructor DECF ;
  constructor DECFSZ ;
  constructor DCFSNZ ;
  constructor INCF ;
  constructor INCFSZ ;
  constructor INFSNZ ;
  constructor IORWF ;
  constructor MOVF ;
  constructor RLCF ;
  constructor RLNCF ;
  constructor RRCF ;
  constructor RRNCF ;
  constructor SUBFWB ;
  constructor SUBWF ;
  constructor SUBWFB ;
  constructor SWAPF ;
  constructor XORWF ;
}{
}

#----------------------------------------------------*

routine getFDAinstructionBaseCode
  ??@instruction_FDA_base_code inInstruction
  !@uint outBaseCode
:
  switch inInstruction
  when ADDWF : outBaseCode := 0x2400 ;
  when ADDWFC : outBaseCode := 0x2000 ;
  when ANDWF : outBaseCode := 0x1400 ;
  when COMF : outBaseCode := 0x1C00 ;
  when DECF : outBaseCode := 0x0400 ;
  when DECFSZ : outBaseCode := 0x2C00 ;
  when DCFSNZ : outBaseCode := 0x4C00 ;
  when INCF : outBaseCode := 0x2800 ;
  when INCFSZ : outBaseCode := 0x3C00 ;
  when INFSNZ : outBaseCode := 0x4800 ;
  when IORWF : outBaseCode := 0x1000 ;
  when MOVF : outBaseCode := 0x5000 ;
  when RLCF : outBaseCode := 0x3400 ;
  when RLNCF : outBaseCode := 0x4400 ;
  when RRCF : outBaseCode := 0x3000 ;
  when RRNCF : outBaseCode := 0x4000 ;
  when SUBFWB : outBaseCode := 0x5400 ;
  when SUBWF : outBaseCode := 0x5C00 ;
  when SUBWFB :  outBaseCode := 0x5800 ;
  when SWAPF : outBaseCode := 0x3800 ;
  when XORWF : outBaseCode := 0x1800 ;
  end switch ;
end routine ;

#----------------------------------------------------*

class @instruction_FDA extends @instruction {
  @instruction_FDA_base_code mInstruction_FDA_base_code ;
  @registerExpression mRegisterExpression ;
  @bool m_W_isDestination ;
  method getBaseCode !@uint outBaseCode :
    getFDAinstructionBaseCode !mInstruction_FDA_base_code ?outBaseCode ;
  end method ;
}

#----------------------------------------------------*

enum @FA_instruction_base_code {
  constructor CLRF ;
  constructor CPFSGT ;
  constructor CPFSLT ;
  constructor MOVWF  ;
  constructor MULWF  ;
  constructor NEGF  ;
  constructor SETF  ;
  constructor TSTFSZ ;
}{
  reader mnemonic :
    CLRF   -> "CLRF" ;
    CPFSGT -> "CPFSGT" ;
    CPFSLT -> "CPFSLT" ;
    MOVWF  -> "MOVWF" ;
    MULWF  -> "MULWF" ;
    NEGF   -> "NEGF";
    SETF   -> "SETF"  ;
    TSTFSZ -> "TSTFSZ" ;
  end reader ;
}

#----------------------------------------------------*

routine getFAinstructionBaseCode
  ??@FA_instruction_base_code inInstruction
  !@uint outBaseCode
:
  switch inInstruction
  when CLRF : outBaseCode := 0x6A00 ;
  when CPFSGT : outBaseCode := 0x6400 ;
  when CPFSLT : outBaseCode := 0x6000 ;
  when MOVWF : outBaseCode := 0x6E00 ;
  when MULWF : outBaseCode := 0x0200 ;
  when NEGF : outBaseCode := 0x6C00 ;
  when SETF : outBaseCode := 0x6800 ;
  when TSTFSZ : outBaseCode := 0x6600 ;
  end switch ;
end routine ;


#----------------------------------------------------*

class @instruction_FA extends @instruction {
  @FA_instruction_base_code mFAinstruction ;
  @registerExpression mRegisterExpression ;

  method getBaseCode !@uint outBaseCode :
   getFAinstructionBaseCode !mFAinstruction ?outBaseCode ;
  end method ;
}

#----------------------------------------------------*

class @instruction_MOVFF extends @instruction {
  @registerExpression mSourceRegisterName ;
  @registerExpression mDestinationRegisterName ;
  
  override
  method instructionLength !@uint outLength :
    outLength := 4 ;
  end method ;
}

#----------------------------------------------------*

abstract class @bitNumberExpression {
}

#----------------------------------------------------*

class @bitNumberLiteralValue extends @bitNumberExpression {
  @luint mBitNumberLiteralValue ;
}

#----------------------------------------------------*

class @bitNumberLabelValue extends @bitNumberExpression {
  @lstring mBitNumberLabelValue ;
  @luint mBitNumberIndexValue ;
}

#----------------------------------------------------*

abstract class @instruction_FBA extends @instruction {
  @registerExpression mRegisterExpression ;
  @bitNumberExpression mBitNumber ;
  abstract method getBaseCode !@uint outBaseCode ;
}

#----------------------------------------------------*

class @instruction_BCF extends @instruction_FBA {
  override method getBaseCode !@uint outBaseCode :
    outBaseCode := 0x9000 ;
  end method ;
}

#----------------------------------------------------*

class @instruction_BSF extends @instruction_FBA {
  override method getBaseCode !@uint outBaseCode :
    outBaseCode := 0x8000 ;
  end method ;
}

#----------------------------------------------------*

class @instruction_BTFSC extends @instruction_FBA {
  override method getBaseCode !@uint outBaseCode :
    outBaseCode := 0xB000 ;
  end method ;
}

#----------------------------------------------------*

class @instruction_BTFSS extends @instruction_FBA {
  override method getBaseCode !@uint outBaseCode :
    outBaseCode := 0xA000 ;
  end method ;
}

#----------------------------------------------------*

class @instruction_BTG extends @instruction_FBA {
  override method getBaseCode !@uint outBaseCode :
    outBaseCode := 0x7000 ;
  end method ;
}

#----------------------------------------------------*

enum @conditional_branch {
  constructor bz ;
  constructor bnz ;
  constructor bn ;
  constructor bnn ;
  constructor bc ;
  constructor bnc ;
  constructor bov ;
  constructor bnov ;
}{
}

#----------------------------------------------------*

routine getBccBaseCode ??@conditional_branch inConditionalBranch !@uint outBaseCode :
  switch inConditionalBranch
  when bz   : outBaseCode := 0xE000 ;
  when bnz  : outBaseCode := 0xE100 ;
  when bn   : outBaseCode := 0xE600 ;
  when bnn  : outBaseCode := 0xE700 ;
  when bc   : outBaseCode := 0xE200 ;
  when bnc  : outBaseCode := 0xE300 ;
  when bov  : outBaseCode := 0xE400 ;
  when bnov : outBaseCode := 0xE500 ;
  end switch ;
end routine ;

#----------------------------------------------------*

routine getComplementaryBcc ??@conditional_branch inConditionalBranch !@conditional_branch outComplementary :
  switch inConditionalBranch
  when bz   : outComplementary := [@conditional_branch bnz] ;
  when bnz  : outComplementary := [@conditional_branch bz] ;
  when bn   : outComplementary := [@conditional_branch bnn] ;
  when bnn  : outComplementary := [@conditional_branch bn] ;
  when bc   : outComplementary := [@conditional_branch bnc] ;
  when bnc  : outComplementary := [@conditional_branch bc] ;
  when bov  : outComplementary := [@conditional_branch bnov] ;
  when bnov : outComplementary := [@conditional_branch bov] ;
  end switch ;
end routine ;

#----------------------------------------------------*

class @instruction_conditionalBranch extends @instruction {
  @conditional_branch mConditionalBranch ;
  @lstring mTargetLabel ;
}

#----------------------------------------------------*

class @instruction_BRA extends @instruction {
  @lstring mTargetLabel ;
}

#----------------------------------------------------*

class @instruction_CALL extends @instruction {
  @lstring mTargetLabel ;
  override
  method instructionLength !@uint outLength :
    outLength := 4 ;
  end method ;
}

#----------------------------------------------------*

class @instruction_GOTO extends @instruction {
  @lstring mTargetLabel ;
  override
  method instructionLength !@uint outLength :
    outLength := 4 ;
  end method ;
}

#----------------------------------------------------*

class @instruction_RCALL extends @instruction {
  @lstring mTargetLabel ;
}

#----------------------------------------------------*

class @instruction_CLRWDT extends @instruction {
}

#----------------------------------------------------*

class @instruction_DAW extends @instruction {
}

#----------------------------------------------------*

class @instruction_NOP extends @instruction {
}

#----------------------------------------------------*

class @instruction_POP extends @instruction {
}

#----------------------------------------------------*

class @instruction_PUSH extends @instruction {
}

#----------------------------------------------------*

class @instruction_RESET extends @instruction {
}

#----------------------------------------------------*

class @instruction_RETURN extends @instruction {
}

#----------------------------------------------------*

class @instruction_SLEEP extends @instruction {
}

#----------------------------------------------------*

abstract class @instruction_literalOperation extends @instruction {
  @luint mLiteralValue ;
  abstract method getBaseCode !@uint outBaseCode ;
}

#----------------------------------------------------*

class @instruction_ADDLW extends @instruction_literalOperation {
  override method getBaseCode !@uint outBaseCode :
    outBaseCode := 0x0F00 ;
  end method ;
}

#----------------------------------------------------*

class @instruction_ANDLW extends @instruction_literalOperation {
  override method getBaseCode !@uint outBaseCode :
    outBaseCode := 0x0B00 ;
  end method ;
}

#----------------------------------------------------*

class @instruction_IORLW extends @instruction_literalOperation {
  override method getBaseCode !@uint outBaseCode :
    outBaseCode := 0x0900 ;
  end method ;
}

#----------------------------------------------------*

class @instruction_MOVLW extends @instruction_literalOperation {
  override method getBaseCode !@uint outBaseCode :
    outBaseCode := 0x0E00 ;
  end method ;
}

#----------------------------------------------------*

class @instruction_MULLW extends @instruction_literalOperation {
  override method getBaseCode !@uint outBaseCode :
    outBaseCode := 0x0D00 ;
  end method ;
}

#----------------------------------------------------*

class @instruction_RETLW extends @instruction_literalOperation {
  override method getBaseCode !@uint outBaseCode :
    outBaseCode := 0x0C00 ;
  end method ;
}

#----------------------------------------------------*

class @instruction_SUBLW extends @instruction_literalOperation {
  override method getBaseCode !@uint outBaseCode :
    outBaseCode := 0x0800 ;
  end method ;
}

#----------------------------------------------------*

class @instruction_XORLW extends @instruction_literalOperation {
  override method getBaseCode !@uint outBaseCode :
    outBaseCode := 0x0A00 ;
  end method ;
}

#----------------------------------------------------*

class @instruction_LFSR extends @instruction {
  @luint mFSRindex ;
  @registerExpression mRegisterExpression ;
  
  override
  method instructionLength !@uint outLength :
    outLength := 4 ;
  end method ;
}

#----------------------------------------------------*

class @instruction_MOVLB extends @instruction {
  @luint mPageIndex ;
}

#----------------------------------------------------*

class @instruction_LABEL extends @instruction {
  @lstring mTargetLabel ;

  override
  method instructionLength !@uint outLength :
    outLength := 0 ;
  end method ;
}

#----------------------------------------------------*

class @instruction_MOVAW extends @instruction {
  @registerExpression mRegisterExpression ;
}

#----------------------------------------------------*

enum @tableAccessOption {
  constructor simpleAccess ;
  constructor postIncrement ;
  constructor postDecrement ;
  constructor preIncrement ;
}{
}

#----------------------------------------------------*

class @instruction_TBLRD extends @instruction {
  @tableAccessOption mOption ;
}

#----------------------------------------------------*

class @instruction_TBLWT extends @instruction {
  @tableAccessOption mOption ;
}

#----------------------------------------------------*

class @instruction_MNOP extends @instruction {
  @luint mOccurrenceFactor ;
  override
  method instructionLength !@uint outLength :
    outLength := [mOccurrenceFactor uint] * 2 ;
  end method ;
}

#----------------------------------------------------*

class @instruction_FOREVER extends @instruction {
  @instructionList mInstructionList ;
  override
  method instructionLength !@uint outLength :
    outLength := 2 ; # BRA at the end of instruction list
    foreach mInstructionList do
      @uint lg ; [mInstruction instructionLength ?lg] ;
      outLength := outLength + lg ;
    end foreach ;
  end method ;
}

#----------------------------------------------------*

abstract class @instruction_IF_SEMI_COLON extends @instruction {
  @registerExpression mRegisterExpression ;
  @instruction mInstruction ;

  abstract method getBaseCode !@uint outBaseCode ;

  override
  method instructionLength !@uint outLength :
    [mInstruction instructionLength ?outLength] ;
    outLength := outLength + 2 ;
  end method ;
}

#----------------------------------------------------*

class @instruction_IF_CPF_NotEqual extends @instruction_IF_SEMI_COLON {
  override
  method getBaseCode !@uint outBaseCode :
    outBaseCode := 0x6200 ; # CPFSEQ
  end method ; 
}

#----------------------------------------------------*

class @instruction_IF_CPF_LowerOrEqual extends @instruction_IF_SEMI_COLON {
  override
  method getBaseCode !@uint outBaseCode :
    outBaseCode := 0x6400 ; # CPFSGT
  end method ; 
}

#----------------------------------------------------*

class @instruction_IF_CPF_GreaterOrEqual extends @instruction_IF_SEMI_COLON {
  override
  method getBaseCode !@uint outBaseCode :
    outBaseCode := 0x6000 ; # CPFSLT
  end method ; 
}

#----------------------------------------------------*

class @instruction_IF_TST_NotZero extends @instruction_IF_SEMI_COLON {
  override
  method getBaseCode !@uint outBaseCode :
    outBaseCode := 0x6600 ; # TSTFSZ
  end method ; 
}

#----------------------------------------------------*

class @instruction_IF_BitTest extends @instruction_IF_SEMI_COLON {
  @bitNumberExpression mBitNumber ;
  @bool mSkipIfSet ;

  override
  method getBaseCode !@uint outBaseCode :
    if mSkipIfSet then
      outBaseCode := 0xA000 ; # BTFSS
    else
      outBaseCode := 0xB000 ; # BTFSC
    end if ;
  end method ; 
}

#----------------------------------------------------*

class @instruction_computed_retlw extends @instruction {
  @luintlist mLiteralValues ;
  @bool mUsesRelativeCall ;

  override
  method instructionLength !@uint outLength :
    outLength := [mLiteralValues length] * 2 + 2 ;
    if not mUsesRelativeCall then
      outLength := outLength + 2 ;
    end if ;
  end method ;
}

#----------------------------------------------------*

class @instruction_computed_bra extends @instruction {
  @lstringlist mTargetLabels ;
  @bool mUsesRelativeCall ;

  override
  method instructionLength !@uint outLength :
    outLength := [mTargetLabels length] * 2 + 2 ;
    if not mUsesRelativeCall then
      outLength := outLength + 2 ;
    end if ;
  end method ;
}

#----------------------------------------------------*

class @instruction_computed_goto extends @instruction {
  @lstringlist mTargetLabels ;
  @bool mUsesRelativeCall ;

  override
  method instructionLength !@uint outLength :
    outLength := [mTargetLabels length] * 4 + 2 ;
    if not mUsesRelativeCall then
      outLength := outLength + 2 ;
    end if ;
  end method ;
}

#----------------------------------------------------*

abstract class @structured_if_condition {
  abstract method conditionLength !@uint outLength ;

}

#----------------------------------------------------*

class @bcc_in_structured_if_condition extends @structured_if_condition {
  @conditional_branch mCondition ;
  override method conditionLength !@uint outLength :
    outLength := 2 ;
  end method ;
}

#----------------------------------------------------*

class @bitTest_in_structured_if_condition extends @structured_if_condition {
  @registerExpression mRegisterExpression ;
  @bitNumberExpression mBitNumber ;
  @bool mBTFSSinstruction ;
  override method conditionLength !@uint outLength :
    outLength := 4 ; # BTFSx, BRA instructions
  end method ;
  method baseCode !@uint outBaseCode :
    if mBTFSSinstruction then
      outBaseCode := 0xA000 ;  # BTFSS
    else
      outBaseCode := 0xB000 ;  # BTFSC
    end if ;
  end method ;
}

#----------------------------------------------------*

class @instruction_structured_if extends @instruction {
  @structured_if_condition mIfCondition ;
  @instructionList mThenInstructionList ;
  @instructionList mElseInstructionList ;

  override
  method instructionLength !@uint outLength :
    [mIfCondition conditionLength ?outLength] ;
    foreach mThenInstructionList do
      @uint lg ; [mInstruction instructionLength ?lg] ;
      outLength := outLength + lg ;
    end foreach ;
    if [mElseInstructionList length] > 0 then
      outLength := outLength + 2 ; # Bra for skking else part
    end if ;
    foreach mElseInstructionList do
      @uint lg ; [mInstruction instructionLength ?lg] ;
      outLength := outLength + lg ;
    end foreach ;
  end method ;
}

#----------------------------------------------------*

list @interruptDefinitionList {
  @lstring mInterruptName ;
  @instructionList mInstructionList ;
}

#----------------------------------------------------*

list @routineDefinitionList {
  @lstring mRoutineName ;
  @luint mBanksel ;
  @bool mHasNoReturn ;
  @instructionList mInstructionList ;
}

#----------------------------------------------------*

list @declarationInRamList {
  @lstring mName ;
  @luint mSize ;
}

#----------------------------------------------------*

list @ramDefinitionList {
  @lstring mBankName ;
  @declarationInRamList mDeclaration ;
}

#----------------------------------------------------*

struct @piccoloModel {
  @lstring mProgramName ;
  @lstring mDeviceName ;
  @ramDefinitionList mRamDefinitionList ;
  @interruptDefinitionList mInterruptDefinitionList ;
  @routineDefinitionList mRoutineDefinitionList ;
  @bool mNeedsComputedGoto2 ;
  @bool mNeedsComputedGoto4 ;
}

#----------------------------------------------------*

end semantics ;
